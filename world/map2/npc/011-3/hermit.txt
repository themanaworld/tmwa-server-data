//

011-3,30,23,0|script|Arkim|116
{
    mes "[Arkim the Hermit]";
    mes "\"Do you want to go back outside?\"";
    if (@knowhermitstory == 1) goto L_Short_Menu;
    menu
         "Yes, please!", L_Sure,
         "Not yet.", L_Close,
         "Actually... Why do you live here?", L_Interested;

L_Short_Menu:
    menu
        "Yes, please!", L_Sure,
        "Not yet.", L_Close,
        "What was that deal you were offering?", L_Quest;

L_Sure:
    warp "011-1", 60, 95;
    close;

L_Interested:
    mes "[Arkim the Hermit]";
    mes "\"Oh my, never really thought about it!\"";
    next;
    mes "[Arkim the Hermit]";
    mes "\"I guess mostly I enjoy experimenting with the bats here.\"";
    menu
        "I see...", L_Close,
        "What type of experimenting?", L_Experiment;

L_Experiment:
    mes "[Arkim the Hermit]";
    mes "\"Never really thought about it either...\"";
    next;
    mes "You watch as the hermit's old, lazy eyes open wide, and the old man comes alive with energy.";
    next;
    mes "[Arkim the Hermit]";
    mes "\"I GUESS I REALLY LIKE CUTTING THEM UP TO SEE HOW THEY WORK!!  HAHAHA!\"";
    menu
        "YOU'RE CRAZY!", L_Close,
        "Right... me too!  Can I help?", L_Quest;

L_Close:
    close;

L_Quest:
    set @knowhermitstory, 1;

    set @dq_level, 20;
    set @dq_cost, 4;
    set @dq_count, 3;
    set @dq_name$, "BatWing";
    set @dq_friendly_name$, "bat wings";
    set @dq_money, 600;
    set @dq_exp, 100;

    callfunc "DailyQuest";

    next;

    mes "[Arkim the Hermit]";
    mes "\"And remember: never lend your toothbrush to a slime!\"";
    close;
}
