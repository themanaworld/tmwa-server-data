033-1,66,28,0|script|Yerrnk|216
{
    set @state, ((QUEST_Barbarians & $@Q_Barbarians_MASK) >> $@Q_Barbarians_SHIFT);

    set @yeti_level, 70;

    if (@state == 9) goto L_Done;
    if (@state == 8) goto L_Yeti_Success;
    if (@state == 7) goto L_Hunt_Yeti;
    if (@state == 6) goto L_Warrior;
    if (@state == 5) goto L_Birrod;
    if (@state == 4) goto L_Sorrow;
    if (@state == 3) goto L_Duty;
    if (@state == 2) goto L_Kimarr;
    if (@state == 1) goto L_Fluffy;

    mes "[Barbarian]";
    mes "\"Hello. I'm Yerrnk, first warrior of the Mangarr.\"";
    next;
    mes "Yerrnk looks at you cautiously.";
    mes "[Yerrnk]";
    mes "\"Please take care of your own business.\"";
    goto L_Close;

L_Fluffy:
    mes "[Yerrnk]";
    mes "\"Kimarr talks too much sometimes.\"";
    mes "He sighs.";
    next;
    mes "[Yerrnk]";
    mes "\"Are you going to accept Kimarr's task? It's an ancient ceremony in our tribe. A young member is accepted as an adult after completing it.\"";
    goto L_Close;

L_Kimarr:
    mes "[Yerrnk]";
    mes "\"I think Kimarr wants to give you something.\"";
    goto L_Close;

L_Duty:
    mes "[Yerrnk]";
    mes "\"I'm not sure if you are aware about the meaning of your actions.\"";
    next;
    mes "\"You passed the initiation rite for hunters of our tribe. That means now you are a hunter of our tribe, with all the rights and duties that come along with that.\"";
    next;
    mes "\"So you'll have to obey our laws and traditions, protect and help any member of our tribe that is in need, and show neither weakness nor fear in battle.\"";
    next;
    mes "\"Keep this in mind. If you fail you'll have to receive the elders' judgement.\"";
    goto L_Close;

L_Sorrow:
    mes "[Yerrnk]";
    mes "\"I noticed that you're getting along with Kimarr and Birrod very well. That's good.\"";
    mes "He smiles, but then an expression of worry returns to his face.";
    next;
    goto L_Mission;

L_Mission:
    mes "[Yerrnk]";
    mes "\"Our tribe is going through dire times. We have lived in harmony with the mountain since time beyond memory.";
    mes "Life is hard in the snowy mountains, but this life suits our nature.\"";
    next;
    mes "\"Some weeks ago the Yetis started to act very strangely. Usually they are very shy, but now....";
    mes "They left the secluded areas high up in the mountains they used to live on and started roaming our hunting grounds.\"";
    next;
    mes "\"It became very dangerous to leave the village, so it's even harder to gather enough food to survive.";
    mes "Not only this, but the Yetis are getting more daring every day. They might attack the village itself sooner or later.\"";
    next;
    mes "\"Other wild animals are more aggressive as well, though the Yetis are the most dangerous.";
    mes "Something like this has never happened before. We have no idea what could cause the animals to behave like this.\"";
    next;
    mes "\"That's why we were sent on a mission to seek help from the Sages of Kaizei.";
    mes "The Sages are wizards of great power and wisdom, and we hope they might be able to give us advice.\"";
    next;
    mes "\"We already went to Sage Nikolai, who's residence is located in the same snow mountains we live in.";
    mes "But there happened some severe accident, so they weren't able to spent any attention to our problem and might rather need help themselves.\"";
    next;
    mes "\"That's why we came down here to search for Santa, he's one of the most powerful of the Sages.\"";
    goto L_Close;

L_Birrod:
    mes "[Yerrnk]";
    mes "\"" + strcharinfo(0) + ", I have to admit that I'm surprised by your skills.";
    mes "You've proven your bravery and strength.\"";
    next;
    if (getskilllv(SKILL_POOL) == 0)
        goto L_Not_Ready;
    mes "\"Please talk to Birrod to receive the initiation rite to become a warrior of the Mangarr.\"";
    menu
        "Alright.",L_Close,
        "Can you tell me again what you are here for?",L_Mission;

L_Not_Ready:
    mes "[Yerrnk]";
    mes "\"But you're not yet ready to become a fully accepted warrior of our tribe.";
    mes "You should travel the world to gain the ability to focus on your inner strength.\"";
    menu
        "Alright.",L_Close,
        "What are you doing down here?",L_Mission;

L_Warrior:
    mes "[Yerrnk]";
    mes "\"" + strcharinfo(0) + ", welcome back!";
    mes "Do you have any question?\"";
    menu
        "Everything's all right, thanks.",L_Close,
        "What are you doing down here?",L_Mission,
        "Can you explain the Raging ability to me?",L_Skill,
        "Do you have a task for me?",L_Task;

L_Skill:
    mes "[Yerrnk]";
    mes "\"As a warrior of our tribe it should be the most important thing to you to defend weaker members of the tribe.";
    mes "To achieve this goal our warriors are training the skill to stand the pain of being hit and even use it to get into a state of rage.\"";
    next;
    mes "\"It works only in a melee fight, when you hear your opponent gasping, smell his sweat, and feel the heat of his body.\"";
    next;
    mes "Yerrnk gets excited, then he pauses a moment to calm down.";
    mes "\"Basically you neglect of your own defense but gain the ability to hit your enemy with outstanding might.\"";
    next;
    mes "[Yerrnk]";
    mes "\"You can imagine that this creates some danger. That's why we usually fight as a group, so a weakened warrior can step back and recover while the others continue to fight.";
    mes "It's also beneficial to have a healer, but very few members of our tribe are born with the ability to wield magic.\"";
    goto L_Close;

L_Task:
    if (BaseLevel >= @yeti_level)
        goto L_Yeti;
    mes "[Yerrnk]";
    mes "\"Not at the moment.";
    mes "Try to stop by later and use the time to practice your abilities.\"";
    goto L_Close;

L_Yeti:
    mes "[Yerrnk]";
    mes "\"Indeed, there's something to do that needs a competent warrior.";
    mes "We've got reports about a single Yeti prowling around one of the houses in the woods west of here.\"";
    next;
    mes "\"If you dare to take on this task, seek that Yeti and make the area secure again.";
    mes "To find the house, enter the forest and then go first south and then west.\"";
    set @state, 7;
    callsub S_Update_Mask;
    goto L_Close;

L_Hunt_Yeti:
    mes "[Yerrnk]";
    mes "\"That Yeti is still roaming in the forest west of here.";
    mes "To find the house it's prowling around, enter the forest and then go first south and then west.\"";
    menu
        "I'm on it.",L_Close,
        "What are you doing down here?",L_Mission,
        "Can you explain the Raging ability to me?",L_Skill;

L_Yeti_Success:
    mes "[Yerrnk]";
    mes "\"" + strcharinfo(0) + "! You made it!";
    mes "You proved that you're one of the most skilled warriors of our tribe. Please have this.\"";
    getitem "LeatherTrousers", 1;
    set @state, 9;
    callsub S_Update_Mask;
    goto L_Close;

L_Done:
    mes "Yerrnk smiles at you.";
    mes "[Yerrnk]";
    mes "\"Welcome back, great warrior.\"";
    menu
        "What's the reason you came here?",L_Mission,
        "Can you explain the raging ability to me?",L_Skill,
        "(Leave)",L_Close;

L_Close:
    set @state, 0;
    set @yeti_level, 0;
    close;

S_Update_Mask:
        set QUEST_Barbarians,
            (QUEST_Barbarians & ~($@Q_Barbarians_MASK))
                | (@state << $@Q_Barbarians_SHIFT);
        return;
}
