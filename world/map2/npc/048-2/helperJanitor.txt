// Part of Blue Sage quests
// author: Jenalya
// see bluesageConfig for detailed quest description
// Elias is the janitor of the mansion
// part of investigation subquest, gives initial hint on the visitor with the mask

048-2,36,44,0|script|Debug#BlueSage|110
{
    mes "QUEST_BlueSage: "+ QUEST_BlueSage;
    mes "QL_BSAGE_INVESTIGATE: "+ QL_BSAGE_INVESTIGATE;
    next;
    menu
        "set QL_BSAGE_INVESTIGATE", L_Investigate;

L_Investigate:
    input @state;
    if(@state < 0 || @state > 14) set @state, 0;
    set QL_BSAGE_INVESTIGATE, @state;
    close;

OnInit:
    if (!debug) disablenpc strnpcinfo(0);
    end;
}

048-2,40,41,0|script|Elias|110,7,6
{
    if (QL_BSAGE_INVESTIGATE == 11) goto L_Thank;
    if ((QL_BSAGE_INVESTIGATE == 4) || (QL_BSAGE_INVESTIGATE == 7) || (QL_BSAGE_INVESTIGATE == 10)) goto L_Confirmed;
    if ((QL_BSAGE_INVESTIGATE == 3) || (QL_BSAGE_INVESTIGATE == 6) || (QL_BSAGE_INVESTIGATE == 9)) goto L_Worry;
    if (QUEST_BlueSage > 0 && QL_BSAGE_INVESTIGATE < 13) goto L_ShortHello;

    mes "[Elias]";
    mes "\"Welcome to the residence of Nikolai, member of the Sages of Kaizei.\"";
    next;
    mes "\"Our house is famous for its extraordinary and well equipped library.";
    mes "Ahm, usually well equipped.\"";
    next;
    mes "\"We had an accident with some magic experiments, that caused some slimes to get out of control. They ate most of the books in the library.\"";
    mes "He sighs with a regretful look on his face.";
    next;
    mes "[Elias]";
    mes "\"So, if you came because of the library, I'll have to disappoint you, because there isn't much left.";
    mes "Though you look like a skilled adventurer. If you'd like to offer your help that'd be greatly appreciated.\"";
    if(QL_BSAGE_INVESTIGATE == 13)
        set QL_BSAGE_INVESTIGATE, 14;
    next;
    mes "\"The library is on the third floor.\"";
    goto L_Close;

OnTouch:
    if (BaseLevel >= $@Q_BlueSageMinimumLevel && !QL_BSAGE_INVESTIGATE)
        set QL_BSAGE_INVESTIGATE, 13;
    end;

L_ShortHello:
    mes "[Elias]";
    mes "\"Welcome back. I heard you're helping with the cleanup efforts in the library.";
    mes "Thank you very much.\"";
    if ((QL_BSAGE_INVESTIGATE != 2) && (QL_BSAGE_INVESTIGATE != 5) && (QL_BSAGE_INVESTIGATE != 8))
        goto L_Close;
    menu
        "Can you tell me if there were any unusual visitors before the accident?",L_Next;

L_Next:
    mes "[Elias]";
    mes "\"Mh, let me think. There were quite a few visitors with different concerns, but that's usual at this time of the year.";
    mes "One of them was a bit odd, he was wearing a mask and had a strange way of talking. He said he comes from Thermin, which is a mining town in the west. I figured his face might have gotten deformed in some mining accident, but I didn't ask, that would've been rude.";
    mes "If I remember correctly he wanted to see the library.\"";
    next;
    mes "Elias gets a bit excited now.";
    mes "[Elias]";
    mes "\"Do you think he may have had something to do with the accident? We're a very hospitable house, so I didn't see a reason to deny him the entrance. I mean, his mask might have been a bit unusual, but hey, there could've been many reasons why someone would wear a mask, don't you think?\"";

    set QL_BSAGE_INVESTIGATE, QL_BSAGE_INVESTIGATE + 1;

    menu
        "Sure, but it's interesting that he was here shortly before the accident.",L_Close,
        "I'll ask some more people about that guy.",L_Close;

L_Worry:
    mes "[Elias]";
    mes "\"After your questions I'm really getting worried about this guy with the mask. In retrospect it really seems suspicous.";
    mes "It might be a good idea to ask around if anyone else observed something odd connected to this person.\"";
    goto L_Close;

L_Confirmed:
    mes "[Elias]";
    mes "\"I heard your investigations reminded other people that they observed suspicious behaviour too.\"";
    next;
    mes "\"It's all my fault, I should've paid more attention! You should talk to Chief Oskari about this.\"";
    goto L_Close;

L_Thank:
    mes "[Elias]";
    mes "\"Now that Chief Oskari knows about the sequence of events, the Sages will take care of it. It's really a shame how malicious people can be.\"";
    next;
    mes "\"Thanks a lot for your help revealing the truth.\"";
    goto L_Close;

L_Close:
    close;
}
