// Part of Blue Sage quests
// author: Jenalya
// see bluesageConfig for detailed quest description
// Peetu is the person who caused the accident
// involved investigation subquest

048-2,25,135,0|script|Peetu|130
{
    if (QL_BSAGE_INVESTIGATE >= 12 && QL_BSAGE_INVESTIGATE < 13) goto L_Thank;
    if (QL_BSAGE_INVESTIGATE > 4 && QL_BSAGE_INVESTIGATE < 13) goto L_Investigate;
    if (QL_BSAGE_INVESTIGATE > 1 && QL_BSAGE_INVESTIGATE < 13) goto L_Query;
    if (QL_BSAGE_INVESTIGATE == 1) goto L_Help;

    mes "You see a rather stressed helper, mumbling to himself.";
    if (BaseLevel < $@Q_BlueSageMinimumLevel || !QL_BSAGE_INVESTIGATE)
        goto L_Close;
    mes "[Peetu]";
    mes "\"How could this have happened? I don't understand it... I'm sure I did the sealing correctly. Maybe the ingredients... but that doesn't make sense. They're checked regularly and...\"";
    next;
    mes "He startles as he notices you.";
    mes "[Peetu]";
    mes "\"Oh, hello. What are you doing here? Ahm, visitors usually don't come up here. I mean, I don't mean to be rude, of course. I'm just surprised to see you here.\"";
    menu
        "I'm just having a look around.",L_Close,
        "Sorry, I didn't mean to bother you.",L_Close,
        "Why aren't you downstairs, helping everyone to clean up?",L_Next;

L_Next:
    mes "[Peetu]";
    mes "\"I would! But I'm ... under preliminary suspension.\"";
    mes "He looks close to tears.";
    next;
    mes "[Peetu]";
    mes "\"It's all my fault! It was my job to seal the slimes away for the night, but I somehow messed it up! How could this have happened? And now I'm going to lose my job and I'll have to leave here and no other sage would give me a new appointment and I don't know what else to do! I mean, I spent my whole life on this so far, I don't even know anything else I could do.\"";
    next;
    mes "He seems to be having a nervous breakdown, as he latches onto you and starts sobbing on your shoulder.";
    menu
        "(Try to console him)",L_More,
        "(Shake him and tell him to pull himself together)",L_More,
        "(Push him away from you and leave.)",L_Close;

L_More:
    mes "He calms down a bit.";
    mes "[Peetu]";
    mes "\"Uh. You're probably right. I'm very sorry. I'm just... You know, I wanted to become a scholar of the sages, studying and... Oh, how could I mess that up? Did you see the library? It caused so much damage! What else should they do other than kick me out?\"";
    menu
        "Maybe you should talk to your chief about that?",L_Less,
        "It doesn't make much sense to draw overhasty conclusions.",L_Less,
        "Do you have a clue about what went wrong?",L_Less;

L_Less:
    mes "[Peetu]";
    mes "\"Well... but... I mean... I don't know... ";
    mes "I have an idea! Would you... ahm, would you talk to Chief Oskari for me? And ask him what he plans to do about me? I... I just don't feel capable of doing that myself right now. I'll try to pull myself together in the meanwhile.\"";
    set QL_BSAGE_INVESTIGATE, 1;
    goto L_Close;

L_Help:
    mes "[Peetu]";
    mes "\"Please talk to Chief Oskari for me and find out if I'll get fired.";
    mes "You can probably find him in his office on the second floor.\"";
    goto L_Close;

L_Query:
    mes "[Peetu]";
    mes "\"Did you talk to Chief Oskari?\"";
    menu
        "Yes, and he doesn't plan to fire you.",L_Continue,
        "Not yet.",L_Close;

L_Continue:
    mes "You tell Peetu that Chief Oskari has a good opinion of him and explain your task to find out what exactly happened.";
    next;
    mes "[Peetu]";
    mes "\"Alright, I understand. You see, I'm very confused too, because I usually pay a lot of attention to my work. Especially when doing something as delicate as sealing away the slimes for the night.\"";
    menu
        "Can you describe to me what exactly you did?",L_TextWall;

L_TextWall:
    mes "[Peetu]";
    mes "\"Well, ok, sure. So, as you know we're doing some research on the nature of slimes, their connection to minerals and gemstones, their breeding behaviour, and, well, ahm, doing some magical experiments to analyze their reactions to different kinds of magic.";
    mes "These experiments caused some ... mutations, and the Blue and White Slimes developed some behaviour different from the slimes of other colors, making them more dangerous.\"";
    next;
    mes "\"This is the reason they have to be magically sealed away for the night, since we didn't want them to get out in the wilderness... Oh, I can only imagine what damage they do to the environment!\"";
    mes "He sighs.";
    next;
    mes "\"However, the sealing involves several steps. I usually start to prepare it while the helpers are still working on their research and experiments. That way, when they're done, they can just come and place the slimes under the seal.";
    mes "I take the ingredients for the spell from the shelves, mix them together and cast the spell shortly before the other helpers finish their daily work.\"";
    next;
    mes "\"You can think of the seal as something like a magical cage. This is a simplified explanation, of course. It's rather advanced magic, a modified version of a shield spell, rather focused on the components that reduce the targets ability to move and... Oh, I seem to be losing you. Nevermind.";
    mes "Anyway, I create and open the 'cage', then I wait for all the helpers to drop in the slimes and close it when all are in. When it is closed, I additionally cast a guarding spell to prevent unauthorized access.\"";
    next;
    mes "\"That night when the slimes escaped, I did all of this as usual. Everything seemed alright after I cast the guarding spell, so I went to bed. But I woke up in the middle of the night because I felt the spells... oh, it's hard to describe... it seems the spell which defines the magical cage started to... inflate itself. That caused the guarding spells to vanish, since they're intended to protect against threats from the outside, not the inside.";
    mes "While expanding, the cage went thinner and thinner until it just... faded out of existence!\"";
    next;
    mes "\"This left all the slimes free in the workshop, right next to the library. Of course I hurried there as fast as I could, yelling to wake up everyone, but that caused so much confusion that we fell over each other in the corridor and when we finally reached the librabry, the slimes were already spread all over the room, eating the books and dripping out of the windows.\"";
    next;
    mes "\"We tried to block the windows to prevent them escaping, but as most of the helpers aren't experienced in fighting and only some of us have a higher magical education, the slimes are rather dangerous for most of them.";
    mes "We weren't able to get the situation under control until most of the slimes had escaped.\"";
    next;
    mes "\"Since it was my responsibility to seal them away, I was preliminarily suspended and sent to my room. Since then I've been thinking over and over again about the happenings and how I cast the spell, trying to find the mistake I made.";
    mes "But now that I've described the spells to you, I'm sure I cast them correctly. Thinking again about the effect of the seal inflating, I suppose it might have to do with the ingredients I used for the spell.\"";
    next;
    mes "\"I never tried it, because it makes no sense to do that, but theoretically adding some Silk Cocoons could have such an effect. That's pretty basic nature magic.";
    mes "But... how would that have gotten into the ingredients?\"";
    set QL_BSAGE_INVESTIGATE, QL_BSAGE_INVESTIGATE + 3;
    goto L_Close;

L_Investigate:
    mes "[Peetu]";
    mes "\"Hello. Did you find out anything else yet?\"";
    if (QL_BSAGE_INVESTIGATE != 11)
        menu
            "I'm still collecting information.",L_Close;
    if (@inventorylist_count == 100)
        goto L_TooMany;
    menu
        "Yes, I have already talked with Chief Oskari.",L_ReallyMore,
        "Not yet.",L_Close;

L_ReallyMore:
    mes "You explain your gained insights to Peetu and what conclusions Chief Oskari drew.";
    mes "[Peetu]";
    mes "\"Oh, I'm so relieved!";
    mes "I mean, this is alarming news, but I'm so glad that this isn't my fault and that he feels I deserve to keep my job.\"";
    next;
    mes "\"It isn't much, but please take these Silk Gloves. We use those when we have to protect our hands, but still need to perform magic.\"";
    getitem "SilkGloves", 1;
    goto L_Close;

L_Thank:
    mes "[Peetu]";
    mes "\"Thanks for all your help.\"";
    goto L_Close;

L_TooMany:
    mes "[Peetu]";
    mes "\"Wow, you have a lot of stuff with you! Isn't that uncomfortable?\"";
    goto L_Close;

L_Close:
    set @inventorylist_count, 0;
    close;
}
