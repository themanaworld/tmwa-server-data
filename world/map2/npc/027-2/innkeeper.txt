// author: Jenalya
// love triangle quest, involved npcs: Reid's Ghost, Hamond, Savaric, Aldred, Golbenez
// state 0: first talk
// state 1: if BaseLevel >= 85 player can ask about the inn and dead
// state 2: reid knows now she's dead, can be asked about the ring
// state 3: can be asked about ring, player offered help to find out what happened, she wants something to represent her sadness (frozen yeti tears)
// state 4: can be asked about ring, can be asked about her life
// state 5: if crying child quest is completed she tells about savaric, and her rooms on the first floor can be entered now to read her diary
// state 6: tells the player to read her diary
// state 7: set in diary, if player has seen all memories (@golbanez < 5), can tell her about it
// state 8: can bring rubies or diamonds, if hamond and savaric told so, set their state
// state 9: can bring rubies or diamonds, if hamond and savaric told so, set their state
// state 10: she spoke with hamond and savaric, gives reward mylarin dust (needed for a crafting quest in thermin)
// state 11: done, can ask about mylarin dust again

027-2,104,39,0|script|Reid's Ghost|315
{
    set @Graveyard_Inn_MASK, NIBBLE_0_MASK;
    set @Graveyard_Inn_SHIFT, NIBBLE_0_SHIFT;

    set @state, ((QUEST_Graveyard_Inn & @Graveyard_Inn_MASK) >> @Graveyard_Inn_SHIFT);

    set @Graveyard_Inn_Lover_MASK, NIBBLE_1_MASK;
    set @Graveyard_Inn_Lover_SHIFT, NIBBLE_1_SHIFT;

    set @lover, ((QUEST_Graveyard_Inn & @Graveyard_Inn_Lover_MASK) >> @Graveyard_Inn_Lover_SHIFT);

    set @Graveyard_Inn_Kid_MASK, NIBBLE_2_MASK;
    set @Graveyard_Inn_Kid_SHIFT, NIBBLE_2_SHIFT;

    set @kidstate, ((QUEST_Graveyard_Inn & @Graveyard_Inn_Kid_MASK) >> @Graveyard_Inn_Kid_SHIFT);

    set @Graveyard_Inn_Husband_MASK, NIBBLE_3_MASK;
    set @Graveyard_Inn_Husband_SHIFT, NIBBLE_3_SHIFT;

    set @husband, ((QUEST_Graveyard_Inn & @Graveyard_Inn_Husband_MASK) >> @Graveyard_Inn_Husband_SHIFT);

    set @Graveyard_Inn_Golbanez_MASK, NIBBLE_4_MASK;
    set @Graveyard_Inn_Golbanez_SHIFT, NIBBLE_4_SHIFT;

    set @golbanez, ((QUEST_Graveyard_Inn & @Graveyard_Inn_Golbanez_MASK) >> @Graveyard_Inn_Golbanez_SHIFT);

    set @minLevel, 85;
    set @YETI_TEAR_AMOUNT, 15;
    set @YETI_TEAR_EXP, 100000;
    set @DIAMOND_AMOUNT, 20; // caution, this value needs to be the same as in the lovers script!
    set @RUBY_AMOUNT, 25; // caution, this value needs to be the same as in the husbands script!

    if (@state >= 11) goto L_Happy;
    if (@state == 10) goto L_Thanks;
    if ((@state == 8) || (@state == 9)) goto L_Sorry;
    if (@state == 7) goto L_Savaric;
    if (@state == 6) goto L_Read_Diary;
    if (@state == 5) goto L_Check_Kid;
    if (@state == 4) goto L_Ask_Life;
    if (@state == 3) goto L_Bring_Tears;
    if (@state == 2) goto L_Offer_Help2;
    if (@state == 1) goto L_After_Welcome;

    mes "You see a dead woman lying on the floor and something that appears to be her ghost floating above her dead body.";
    next;
    mes "[Ghost]";
    mes "\"He- I - What - Oh -\"";
    mes "The womans ghost seems to be confused.";
    next;
    mes "[Ghost]";
    mes "Oh, I'm sorry. Welcome to Reid's Inn. My name is Reid, I am the innkeeper of this wonderful place of leisure. Please enjoy your visit here!";
    next;

    set @state, 1;
    callsub S_Update_Mask;

    goto L_Ask_Dead;

L_After_Welcome:
    mes "[Reid's Ghost]";
    mes "\"Please enjoy yourself!\"";
    if (BaseLevel < @minLevel)
            goto L_Close;
    goto L_Ask_Dead;

L_Ask_Dead:
    if (BaseLevel < 85)
            goto L_Close;

    menu
        "What happened to you?", L_Next,
        "Thank you, I'll surely enjoy my visit.", L_Close;

L_Next:
    mes "[Reid's Ghost]";
    mes "\"What happened to me? What do you mean?\"";
    menu
        "Ahm, you don't look very well - actually, you look dead.",L_Next1,
        "You don't seem to be in the best condition.",L_Next1,
        "You are dead!",L_Next1;

L_Next1:
    mes "Reid looks down at her body and turns even more pale, if this is possible.";
    next;
    mes "[Reid's Ghost]";
    mes "\"OH?\"";
    next;
    menu
        "I'm sorry. I didn't know you weren't aware of that.",L_Next2,
        "Yes - do you understand my question now?",L_Next2,
        "Shall I help you to find out what happened to you?", L_Offer_Help1;

L_Next2:
    mes "[Reid's Ghost]";
    mes "\"I'm dead? But why? And why am I still here?\"";
    next;
    mes "She stares into space and doesn't seem to notice you anymore.";
    set @state, 2;
    callsub S_Update_Mask;
    goto L_Close;

L_Offer_Help2:
    mes "Reid's Ghost is still staring into space.";

    if (@kidstate == 5)
        menu
            "Shall I help you to find out what happened to you?", L_Offer_Help1,
            "Hi. I found this in the main hall. Do you know to whom it belongs?", L_Ring,
            "I don't want to bother you.", L_Close;
    goto L_Skip1;

L_Skip1:
    menu
        "Shall I help you to find out what happened to you?", L_Offer_Help1,
        "I don't want to bother you.", L_Close;

L_Offer_Help1:
    mes "\"I.. I don't know you. Why should you help me?\"";
    next;
    mes "\"Bring me proof that you are serious. My life seems to have ended in a terrible and sad way, I can see it very clearly. If you want to help me, please bring me something that represents this immeasurable sadness I am feeling!\"";

    set @state, 3;
    callsub S_Update_Mask;
    goto L_Close;

L_Bring_Tears:
    mes "[Reid's Ghost]";
    mes "\"Did you find something that can represent my sadness?\"";
    next;

    if ((@kidstate == 5)
        && countitem("FrozenYetiTear") > 0)
            menu
                "I found this frozen tear to represent your sadness.", L_Check_Tears,
                "Hi. I found this in the main hall. Do you know to whom it belongs?", L_Ring,
                "I'm still searching.", L_Close;
    if (@kidstate == 5)
        menu
            "Hi. I found this in the main hall. Do you know to whom it belongs?", L_Ring,
            "I'm still searching.", L_Close;
    if (countitem("FrozenYetiTear") > 0)
        menu
            "I found this frozen tear to represent your sadness.", L_Check_Tears,
            "I'm still searching.", L_Close;
    mes "\"You didn't? It seems you're not serious with your offer to help me.\"";
    goto L_Close;

L_Check_Tears:
    if (countitem("FrozenYetiTear") < @YETI_TEAR_AMOUNT)
        goto L_Not_Enough_Tears;
    delitem "FrozenYetiTear", @YETI_TEAR_AMOUNT;

    getexp @YETI_TEAR_EXP, 0;

    set @state, 4;
    callsub S_Update_Mask;

    mes "[Reid's Ghost]";
    mes "\"These tears.. how they shimmer in the candle light. Yes, it seems you are serious about your offer. I think maybe you have the sensibility to help me find out the real meaning behind this terrible feeling.\"";
    next;
    goto L_Ask_Life;

L_Ask_Life:
    mes "[Reid's Ghost]";
    mes "\"But how do you plan to help me?\"";
    next;

    if (@kidstate == 5)
        menu
            "Please tell me about your life. Maybe this will give us a clue about what happened.",L_Skip2,
            "Hi. I found this in the main hall. Do you know to whom it belongs?", L_Ring,
            "I need to think about that.", L_Close;
    goto L_Skip2;

L_Skip2:
    menu
        "Please tell me about your life. Maybe this will give us a clue about what happened.",L_Next4,
        "I need to think about that.", L_Close;

L_Next4:
    mes "[Reid's Ghost]";
    mes "\"About my life? Well, ok.\"";
    next;
    mes "\"I'm the owner of this inn. I inherited it from my parents, who ran it while I was a child.\"";
    next;
    mes "\"My father - he was a very joyful man. But one day, when I was just ten years old, he had an accident while carrying a barrel of beer.\"";
    next;
    mes "\"The barrel smashed his legs and he wasn't able to walk anymore. He got very despondent about that and started to drink.\"";
    next;
    mes "\"My mother and I didn't know how to cheer him up, and he wouldn't listen to us. My mother tried her best to keep the inn running and I tried my best to help her, even though I was still a child.\"";
    next;
    mes "\"But a few years later, my mother lost her will to go on and the inn slowly went down the drain. Everything was dirty and we had less and less patrons.\"";
    next;
    mes "\"I don't blame my mother. She loved my father so much, she couldn't stand to see what was happening to him. When he died because of his drinking, she totally lost her will to live and just lay in bed and followed him.\"";
    next;
    mes "Reid seems to be tortured by her memories.";
    next;
    mes "\"That was a very hard time.\"";
    menu
        "Yes, yes, but what about your life shortly before you died?",L_Next5,
        "I can imagine. I'm very sorry for you",L_Next5;

L_Next5:
    mes "She doesn't seem to pay much attention to what you say.";
    next;
    mes "\"But Hamond, my friend since I was a child, helped me in those bad times. We built up the inn again and we - we married.\"";
    next;
    mes "\"He was so kind and my only light in those dark times. And our little son Aldred gave me back my joy in life.\"";
    next;
    mes "\"Everything was peaceful and I thought I had found my place. But one day -\"";
    next;

    set @state, 5;
    callsub S_Update_Mask;
    goto L_Check_Kid;

L_Check_Kid:
    if (@kidstate == 5)
        menu
            "Hi. I found this in the main hall. Do you know to whom it belongs?", L_Ring;

    if ((Sex == 1)
        && (@kidstate < 7))
            goto L_Man;
    if ((Sex == 0)
        && (@kidstate < 7))
            goto L_Woman;

    mes "\"What I'm going to tell you is very personal, but your acts have shown me that you're a person I can trust. You kindly asked me about that ring you have found... and also, even my son, who is always so reserved with strangers, came to me and spent a long time telling me a lot of nice things about you. So I will tell you about my sad love.\"";
    next;

    mes "[Reid's Ghost]";
    mes "\"It began as a completely normal day. We were taking care of our overnight guests and preparing the inn for the evening rush.\"";
    next;
    mes "\"But that day, a very special patron checked in. His name was Savaric, and he was a student of magic. He came from far away to check some rumors about a legendary mana seed.\"";
    next;
    mes "Reid's Ghost has a completely enchanted look on her face.";
    next;
    mes "\"He really made me feel alive - when he was near, I was feeling like I never felt before. I started to search for reasons to be near him - I told myself that I liked him just because he was interesting, coming from so far away and being a mage.\"";
    next;
    mes "\"But I was lying to myself.\"";
    next;
    mes "\"I - oh, it is really hard to talk about this. Maybe - yes, I could let you read my diary. I'll give you the key to our rooms.\"";
    next;
    mes "She pulls a key out of her pocket as transparent as herself. As you try to grab it, your fingers just go through it.";
    next;
    mes "[Reid's Ghost]";
    mes "\"I keep forgetting! Please take the key out of my pocket.\"";
    mes "She is pointing at her dead body with a sad look on her face. You take the key.";
    next;
    mes "[Reid's Ghost]";
    mes "\"This is the key to our personal room, it is located at the very end of the corridor upstairs. You will find my diary in the bookshelf, hidden behind a book with poems. Hamond doesn't like poems.\"";
    next;
    mes "\"Please go and read it.\"";

    set @state, 6;
    callsub S_Update_Mask;
    goto L_Close;

L_Read_Diary:
    mes "[Reid's Ghost]";
    mes "\"Please go into my room and read the diary. You will find my diary in the bookshelf, hidden behind a book with poems.\"";
    goto L_Close;

L_Savaric:
    mes "[Reid's Ghost]";
    mes "\"Now you know about my recent past and... and my twisted feelings.\"";
    next;
    menu
        "Yes, I feel very sorry for you.",L_Next6,
        "Savaric seems to be a really hot guy, making you so compliant.",L_Tease;

L_Next6:
    mes "[Reid's Ghost]";
    mes "\"Oh, thank you.\"";
    if (@golbanez < 5)
        menu
            "But I still didn't find out what happened. I'll return when I have news.",L_Close;
    menu
        "But I still didn't find out what happened. I'll return when I have news.",L_Close,
        "I found out what happened to you and this place. I'll tell you.",L_Next7;

L_Next7:
    mes "You explain to her about Golbenez, Savaric and the memories you have seen.";
    next;
    mes "[Reid's Ghost]";
    mes "\"Oh, I always knew we weren't doing the right thing! But how could we have known it would end like this?\"";
    next;
    mes "\"I would never have expected Hamond to beat me - and Savaric... I trusted him with my life... And he carelessly wasted it. Mine and everyone else in the inn.\"";
    next;
    mes "\"I'm feeling hurt. And confused. Both Hamond and Savaric turned out not to be the persons I believed them to be.\"";
    set @state, 8;
    callsub S_Update_Mask;
    goto L_Close;

L_Sorry:
    mes "[Reid's Ghost]";
    mes "\"Welcome back.\"";
    if ((@lover == 4)
        && (countitem("Diamond") >= @DIAMOND_AMOUNT)
        && (countitem("Ruby") >= @RUBY_AMOUNT)
        && (@husband == 5))
            menu
                "Savaric asked me to give you something.",L_Diamond,
                "Hamond sent me to bring you this.",L_Ruby,
                "Nevermind.",L_Close;
    if (@lover == 4 && (countitem("Diamond") >= @DIAMOND_AMOUNT))
        menu
            "Savaric asked me to give you something.",L_Diamond,
            "Nevermind.",L_Close;
    if (@husband == 5 && (countitem("Ruby") >= @RUBY_AMOUNT))
        menu
            "Hamond sent me to bring you this.",L_Ruby,
            "Nevermind.",L_Close;
    goto L_Close;

L_Diamond:
    if (countitem("Diamond") < @DIAMOND_AMOUNT)
        goto L_Kidding;
    delitem "Diamond", @DIAMOND_AMOUNT;
    mes "You explain how Savaric feels and what the diamonds mean.";
    next;
    mes "[Reid's Ghost]";
    mes "\"Oh Savaric. I just can't be angry at him. Thanks for your help. I'll go to talk with him.\"";
    set @lover, 5;
    callsub S_Update_Mask_Lover;
    set @state, @state + 1;
    callsub S_Update_Mask;
    goto L_Close;

L_Ruby:
    if (countitem("Ruby") < @RUBY_AMOUNT)
        goto L_Kidding;
    delitem "Ruby", @RUBY_AMOUNT;
    mes "You tell her about Hamonds feelings.";
    next;
    mes "[Reid's Ghost]";
    mes "\"I need to talk to him. Thanks for bringing me this.\"";
    set @husband, 6;
    callsub S_Update_Mask_Husband;
    set @state, @state + 1;
    callsub S_Update_Mask;
    goto L_Close;

L_Kidding:
    mes "[Reid's Ghost]";
    mes "\"Are you kidding? You don't have the items you are talking about. This is not funny.\"";
    goto L_Tease;

L_Tease:
    mes "[Reid's Ghost]";
    mes "\"Don't be mean! I never felt like that before.\"";
    goto L_Close;

L_Thanks:
    mes "[Reid's Ghost]";
    mes "\"I'm so glad, now I know what happened and spoke to both Savaric and Hamond. Thank you so much. Please take this.\"";
    getinventorylist;
    if (@inventorylist_count == 100)
        goto L_Full_Inv;
    getitem "MylarinDust", 1;
    set @state, 11;
    callsub S_Update_Mask;
    next;
    goto L_Mylarin;

L_Mylarin:
    mes "[Reid's Ghost]";
    mes "\"This is called mylarin dust. My mother gave it to me when my father had his accident. She said it is very valuable.\"";
    next;
    mes "\"I'm not sure what it is used for, but I remember her saying something about some special crafting tradition which uses it to make really strong armor.\"";
    next;
    mes "\"Maybe you should try to find someone who can use it.\"";
    goto L_Close;

L_Happy:
    mes "[Reid's Ghost]";
    mes "\"Thank you for all you've done.\"";
    menu
        "Can you tell me again about the powder you gave me?",L_Mylarin,
        "I'm glad I could help.",L_Close;

L_Not_Enough_Tears:
    mes "[Reid's Ghost]";
    set @tears, countitem("FrozenYetiTear");
    if (@tears == 1)
        mes "\"This is beautiful! But one of them isn't enough to represent my sadness.\"";
    if (@tears > 1)
        mes "\"This is beautiful! But " + @tears + " of them aren't enough to represent my sadness.\"";
    goto L_Close;

L_Man:
    mes "[Reid's Ghost]";
    mes "\"What I have to tell is very personal. You're a man - I'm not sure if I want to tell you. I know you helped me already and showed me you are a sensitive man, but I barely know you. I am sorry, but I have to think a little bit more. Maybe later, when I know you better, I can tell you more about it. \"";
    goto L_Close;

L_Woman:
    mes "[Reid's Ghost]";
    mes "\"What I have to tell you is something very personal. You are a woman like me - I'm sure you would understand.\"";
    next;
    mes "\"But I'm not sure if I want to tell you. I know you helped me already and showed me you are a sensitive woman, but I barely know you. I am sorry, but I have to think a little bit more. Maybe later, when I know you better, I can tell you more about it. \"";
    goto L_Close;

L_Ring:
    mes "[Reid's Ghost]";
    mes "\"Oh, this - I - it's mine. It was a present from a very precious person.\"";
    next;
    mes "She reaches out to take the ring, but it just go through her fingers. She looks sad.";
    next;
    mes "\"Ah, I forgot again. It pains me not being able to wear it. Please, take it with you, I don't want it anymore. It hurts too much.\"";
    set @kidstate, 6;
    callsub S_Update_Mask_Kid;
    goto L_Close;

L_Full_Inv:
    mes "[Reid's Ghost]";
    mes "\"Dear, you're carrying so much stuff, you can't take my present. Come back later to take it.\"";
    goto L_Close;

L_Close:
    set @Graveyard_Inn_MASK, 0;
    set @Graveyard_Inn_SHIFT, 0;
    set @state, 0;
    set @Graveyard_Inn_Lover_MASK, 0;
    set @Graveyard_Inn_Lover_SHIFT, 0;
    set @lover, 0;
    set @Graveyard_Inn_Kid_MASK, 0;
    set @Graveyard_Inn_Kid_SHIFT, 0;
    set @kidstate, 0;
    set @Graveyard_Inn_Husband_MASK, 0;
    set @Graveyard_Inn_Husband_SHIFT, 0;
    set @husband, 0;
    set @Graveyard_Inn_Golbanez_MASK, 0;
    set @Graveyard_Inn_Golbanez_SHIFT, 0;
    set @golbanez, 0;
    set @minLevel, 0;
    set @YETI_TEAR_AMOUNT, 0;
    set @YETI_TEAR_EXP, 0;
    set @DIAMOND_AMOUNT, 0;
    set @RUBY_AMOUNT, 0;
    set @tears, 0;
    close;

S_Update_Mask_Kid:
    set QUEST_Graveyard_Inn,
        (QUEST_Graveyard_Inn & ~(@Graveyard_Inn_Kid_MASK))
            | (@kidstate << @Graveyard_Inn_Kid_SHIFT);
    return;

S_Update_Mask_Husband:
    set QUEST_Graveyard_Inn,
        (QUEST_Graveyard_Inn & ~(@Graveyard_Inn_Husband_MASK))
            | (@husband << @Graveyard_Inn_Husband_SHIFT);
    return;

S_Update_Mask_Lover:
    set QUEST_Graveyard_Inn,
        (QUEST_Graveyard_Inn & ~(@Graveyard_Inn_Lover_MASK))
            | (@lover << @Graveyard_Inn_Lover_SHIFT);
    return;

S_Update_Mask:
    set QUEST_Graveyard_Inn,
        (QUEST_Graveyard_Inn & ~(@Graveyard_Inn_MASK))
            | (@state << @Graveyard_Inn_SHIFT);
    return;
}
