// Author: Jenalya, Jesusalva, Ledmitz

026-2,38,118,0|script|Cerhan|311
{
    mes "[Cerhan]";
    mes "\"Ah, hello! It's good to see another normal human at this strange place!\"";
    next;
    menu
        "That's true. What are you doing here?",L_Story,
        "Do you know of a way to make usable items? I could really use a boost.", L_Lab,
        "Yes, this place is really strange. I'm going to have another look around.", L_Close;

L_Story:
    mes "[Cerhan]";
    mes "\"I came from Thermin, a town in the Kazei area.\"";
    next;
    mes "\"I'm an experienced weapon master and I was thinking about establishing a smithy here. I'll need some more equipment, though.\"";
    next;
    if (countitem(MylarinDust) > 0)
        menu
            "Do you know something about 'Mylarin Dust'?", L_Mylarin,
            "Do you work with bows or only swords and armor?", L_Banshee,
            "Good luck with that.", L_GoodLuckWith;
    goto L_GoodLuckWith;

L_GoodLuckWith:
    mes "[Cerhan]";
    mes "\"If you're interested in weapons and armor, you may want to come back later.\"";
    if (countitem(SandCutter) > 0)
        menu
            "[Leave]",L_Close,
            "Actually, I exchanged my Mylarin Dust with a bunny girl.", L_SandCutter,
            "Do you work with bows or only swords and armor?", L_Banshee;
    close;

L_SandCutter:
    mes "[Cerhan]";
    mes "\"What?! Are you mad?! Do you know how many night desert warriors the bunny girl, Ashley, even killed?!\"";
    next;
    mes "[Cerhan]";
    mes "\"Gosh, I fear for those whom wrong her. I hope you did not provoke her while doing this exchange.\"";
    next;
    mes "[Cerhan]";
    mes "\"Ugh... I really should finishing settin up my shop, but... I'm so lazy.\"";
    goto L_Close;

L_Mylarin:
    mes "[Cerhan]";
    mes "\"Mylarin dust?! Where did you get that? That's amazing!\"";
    next;
    mes "\"I can craft you an amazing strong armor with that - uhm, well, first I need to set up my smithy here.\"";
    next;
    mes "\"Please come back, when I'm ready for that. Mylarin dust... amazing.\"";
    goto L_Close;

L_Banshee:
    mes "[Cerhan]";
    mes "\"I work mostly with ##Bdaggers##b and armor, not swords. Umfrey is selling arrows; He should be at your right, talk to him instead.\"";
    if (countitem(BansheeBow) < 1) goto L_Close;
    next;
    mes "[Cerhan]";
    mes "\"Actually, what the fletching. How could you curse your [@@"+ImperialBow+"|@@] like that?! It is ruined! Would you really sacrifice anything for just some extra points in attack speed? Don't you have any virtues?!\""; // TRANSLATORS: Fletching is the fin-shaped aerodynamic stabilization device attached on arrows, bolts, darts, or javelins.
    next;
    mes "[Cerhan]";
    mes "\"Sure, I can see it is much more powerful now as a [@@"+BansheeBow+"|@@] but really. A cursed item. Aren't you afraid of dying?!\"";
    next;
    mes "[Cerhan]";
    mes "\"Wyara might believe that a @@"+PurificationPotion+"|@@ should only be used to purify impurities in nature, but at Thermin, we make do with what we have, so I'll offer you a deal. I can remove the curse on your bow and restore it to its original glory...\"";
    next;
    mes "[Cerhan]";
    mes "\"...For only 5,000 GP. And please note, that it will be purified, not uncursed. If it was lying in, say, a chest for too long and you put it there again, the curse will resurrect and it'll be ruined again.\"";
    next;
    if (Zeny < 5000) goto L_NoMoney;
    mes "[Cerhan]";
    mes "\"So whaddaya say? Do we have a deal?\"";
    menu
        "I'm a bit broke right now, so maybe later.", L_NoMoney,
        "Sure, please purify my Banshee Bow.", L_Purify;
L_Lab:
    mes "[Cerhan]";
    mes "\"I've seen Leofwin use the gear on that table over there. She makes stuff for her shop and in some cases, even sells the required items.\"";
    mes "\"If you don't have the money to buy things outright, she'll let anyone use her stuff if they know what they're doing.\"";
    mes "\"Good luck with your experiments, if you bother to try any. Speaking of that, I haven't had an iron potion yet today and I'll be groggy and stiff until I do.\"";
    mes "\"Do remember, the moor can be a menacing place to those that lack caution. Keep your head up and chin tucked, out there.\"";
    close;

L_NoMoney:
    next;
    mes "[Cerhan]";
    mes "\"Just bring me the money, and I'll do it in a jiff.\"";
    close;

L_Purify:
    if (countitem(BansheeBow) < 1) goto L_Banshee;
    if (Zeny < 5000) goto L_NoMoney;
    set Zeny, Zeny - 5000;
    delitem BansheeBow, 1;
    getitem ImperialBow, 1;
    mes "[Cerhan]";
    mes "\"And here we go, your Imperial Bow was restored to its former glory! Should be much easier to survive now, eh?\"";
    if ((checkweight(ArmorBreaker, 50) == 0) || (@inventorylist_count == 100)) goto L_Close;
    next;
    mes "[Cerhan]";
    mes "\"I'll even give you a little memento. Now go, and make me proud!\"";
    getitem ArmorBreaker, 50; // In average, each arrow went for 100 gp, a bargain
    close;

L_Close:
    close;
}
