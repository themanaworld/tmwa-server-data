// A professor at Tulimshar's magic academy

024-1.gat,83,51,0|script|Tondar|168,
{
    mes "[Tondar]";
    mes "\"We're not accepting any new students right now.\"";
    if (getskilllv(SKILL_MAGIC) > 0)
        goto L_may_ask;
    goto L_end;

L_may_ask:
    next;
    menu
        "Can you teach me a spell?", L_askspell,
        "Do you have a magic library?", L_library,
        "Are you sure?  I can pay well...", L_nopay,
        "That's unfortunate.", L_end,
        "Goodbye, then.", L_end;

L_library:
    mes "[Tondar]";
    mes "\"Of course we do.  It is only for graduate students, alumni, and faculty.\"";
    next;
    menu
        "Thank you, and goodbye.", L_end,
        "Nobody else is allowed inside?", L_Next;

L_Next:
    mes "[Tondar]";
    mes "\"Well, it is possible to get a special permit from the headmaster.  But he is currently on sabbatical.\"";
    close;

L_askspell:
    mes "[Tondar]";
    mes "\"We only teach spells to students.\"";
    next;
    menu
        "Oh, come on... just one little spell!", L_nopay,
        "Pretty please?", L_spell,
        "I can pay you, too...", L_nopay;

L_spell:
    mes "[Tondar]";
    mes "\"Well, all right; this one can't do much harm.  Press your hands together and say `" + getspellinvocation("ask-magic-exp") + "'.\"";
    mes "\"This will release a steady flow of magic within you.  Focus and try to control it; it is a good meditative practice.\"";
    close;

L_nopay:
    mes "[Tondar]";
    mes "The wizard frowns angrily.";
    mes "\"Do not tempt me to teach you a different kind of lesson, young one!  Begone!\"";
    goto L_end;

L_end:
    close;
}
