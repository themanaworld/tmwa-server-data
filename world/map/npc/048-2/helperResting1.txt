// Part of Blue Sage quests
// author: Jenalya
// see bluesageConfig for detailed quest description
// Lenita is one of the helpers working on sorting the bookpages, but is currently having a break
// can give second hint about the visitor in investigation subquest

048-2,36,26,0|script|Lenita|363
{
    set @investigate, ((QUEST_BlueSage & $@Q_BlueSageInvestigate_MASK) >> $@Q_BlueSageInvestigate_SHIFT);
    set @bookpages, ((QUEST_BlueSage & $@Q_BlueSageBookPages_MASK) >> $@Q_BlueSageBookPages_SHIFT);

    if (@bookpages == 254) goto L_Thank;
    if (@bookpages > 0) goto L_Progress;

    mes "[Lenita]";
    mes "\"Oh, hello. You didn't choose a good time to visit. The library is a total mess. The slimes got out of control and ate most of the books.\"";
    next;
    mes "\"We're trying to repair some of the valuable and important books. It's so much work!";
    mes "I was up until late last night, and woke up so early today... I'm having a break right now.\"";
    goto L_Investigate;

L_Progress:
    mes "[Lenita]";
    mes "\"Teuvo said you're helping to collect some of the missing bookpages to repair the books. That's very kind of you! It's so much work to do, so every bit of help is welcome.\"";
    goto L_Investigate;

L_Thank:
    mes "[Lenita]";
    mes "\"Wow, Ensio told me you collected so many of the lost book pages.\"";
    goto L_Investigate;

L_Investigate:
    if ((@investigate == 3) || (@investigate == 4)) goto L_Ask_Visitor;
    if ((@investigate == 5) || (@investigate == 8)) goto L_Ask_Helper;
    if ((@investigate == 6) || (@investigate == 7) || (@investigate == 9) || (@investigate == 10)) goto L_Ask_Both;
    goto L_Close;

L_Ask_Visitor:
    menu
        "Can you tell me anything about the visitor with the mask?",L_Visitor,
        "I need to leave.",L_Close;

L_Ask_Helper:
    menu
        "What's your opinion of Peetu and how he does his work?",L_Helper,
        "See you later.",L_Close;

L_Ask_Both:
    menu
        "Can you tell me anything about the visitor with the mask?",L_Visitor,
        "What's your opinion of Peetu and how he does his work?",L_Helper,
        "Bye.",L_Close;

L_Visitor:
    mes "[Lenita]";
    mes "\"With a mask? Yeah, I remember. That was a strange guy. He came to visit the library, but he lurked around at the workshop area and they had to send him back to the books.\"";
    if ((@investigate != 3) && (@investigate != 6) && (@investigate != 9))
        goto L_Close;
    set @investigate, @investigate + 1;
    callfunc "updateBlueSageInvestigate";
    goto L_Close;

L_Helper:
    mes "[Lenita]";
    mes "\"Mh, I can't really tell since I usually work in the library while he works at the workshop. Maybe you should ask around among the people who work there.\"";
    goto L_Close;

L_Close:
    set @investigate, 0;
    set @bookpages, 0;
    close;
}
