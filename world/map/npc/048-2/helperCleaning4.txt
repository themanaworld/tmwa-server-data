// Part of Blue Sage quests
// author: Jenalya
// see bluesageConfig for detailed quest description
// Kristian works on cleaning the library
// can give second hint about the visitor in investigation subquest

048-2,131,119,0|script|Kristian|366
{
    set @slimes, ((QUEST_BlueSage & $@Q_BlueSageSlimes_MASK) >> $@Q_BlueSageSlimes_SHIFT);
    set @investigate, ((QUEST_BlueSage & $@Q_BlueSageInvestigate_MASK) >> $@Q_BlueSageInvestigate_SHIFT);

    if (@slimes == 255) goto L_Thank;
    if (@slimes > 0) goto L_Progress;

    mes "[Kristian]";
    mes "\"Welcome. Please don't go deeper into the library, there are still some slimes left. But in spite of that we have made quite a bit of progress. You should have seen the mess just after most of the slimes escaped!\"";
    goto L_Investigate;

L_Progress:
    mes "[Kristian]";
    mes "\"Wow, it's really generous of you to fight those slimes for us.\"";
    goto L_Investigate;

L_Thank:
    mes "[Kristian]";
    mes "\"I'm so glad that you took care of the remaining slimes. Thanks!\"";
    goto L_Investigate;

L_Investigate:
    if ((@investigate == 3) || (@investigate == 4)) goto L_Ask_Visitor;
    if ((@investigate == 5) || (@investigate == 8)) goto L_Ask_Helper;
    if ((@investigate == 6) || (@investigate == 7) || (@investigate == 9) || (@investigate == 10)) goto L_Ask_Both;
    goto L_Close;

L_Ask_Visitor:
    menu
        "Can you tell me anything about the visitor with the mask?",L_Visitor,
        "I need to leave.",L_Close;

L_Ask_Helper:
    menu
        "What's your opinion of Peetu and how he does his work?",L_Helper,
        "See you later.",L_Close;

L_Ask_Both:
    menu
        "Can you tell me anything about the visitor with the mask?",L_Visitor,
        "What's your opinion of Peetu and how he does his work?",L_Helper,
        "Bye.",L_Close;

L_Visitor:
    mes "[Kristian]";
    mes "\"Oh, there was such a visitor, yes. Strange guy, and didn't really know how to behave. He sneaked into the workshop area all the time, I think he was rather curious about the things going on there.\"";
    next;
    mes "\"But still, he can't just walk in there, fiddle with the experiments and disturb the helpers doing their work.\"";
    if ((@investigate != 3) && (@investigate != 6) && (@investigate != 9))
        goto L_Close;
    set @investigate, @investigate + 1;
    callfunc "updateBlueSageInvestigate";
    goto L_Close;

L_Helper:
    mes "[Kristian]";
    mes "\"Peetu? I think he's a good guy. Very focused on his work, I think. I don't have that much to do with him, so I can't really tell.\"";
    goto L_Close;

L_Close:
    set @investigate, 0;
    set @slimes, 0;
    close;
}
