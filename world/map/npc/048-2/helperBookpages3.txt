// Part of Blue Sage quests
// author: Jenalya
// see bluesageConfig for detailed quest description
// Teuvo is one of the helpers working on sorting the bookpages
// can give second hint about Peetu in investigation subquest

048-2,99,87,0|script|Teuvo|366
{
    set @investigate, ((QUEST_BlueSage & $@Q_BlueSageInvestigate_MASK) >> $@Q_BlueSageInvestigate_SHIFT);
    set @bookpages, ((QUEST_BlueSage & $@Q_BlueSageBookPages_MASK) >> $@Q_BlueSageBookPages_SHIFT);

    if (@bookpages == 31) goto L_Thank;
    if (@bookpages > 0) goto L_Progress;

    mes "[Teuvo]";
    mes "\"I always thought it was a bad idea to play around with the slimes. And as if holding them in here isn't bad enough, no, they also had to mess around with magic.\"";
    mes "He seems to be rather upset.";
    goto L_Investigate;

L_Progress:
    mes "[Teuvo]";
    mes "\"Did you find some bookpages? Ensio will take them.\"";
    goto L_Investigate;

L_Thank:
    mes "[Teuvo]";
    mes "\"Thanks for helping out here.\"";
    goto L_Investigate;

L_Investigate:
    if ((@investigate == 3) || (@investigate == 4)) goto L_Ask_Visitor;
    if ((@investigate == 5) || (@investigate == 8)) goto L_Ask_Helper;
    if ((@investigate == 6) || (@investigate == 7) || (@investigate == 9) || (@investigate == 10)) goto L_Ask_Both;
    goto L_Close;

L_Ask_Visitor:
    menu
        "Can you tell me anything about the visitor with the mask?",L_Visitor,
        "I need to leave.",L_Close;

L_Ask_Helper:
    menu
        "What's your opinion of Peetu and how he does his work?",L_Helper,
        "See you later.",L_Close;

L_Ask_Both:
    menu
        "Can you tell me anything about the visitor with the mask?",L_Visitor,
        "What's your opinion of Peetu and how he does his work?",L_Helper,
        "Bye.",L_Close;

L_Visitor:
    mes "[Teuvo]";
    mes "\"With a mask? Hm. I don't remember.\"";
    goto L_Close;

L_Helper:
    mes "[Teuvo]";
    mes "\"Peetu? He's one of those magic wielders who think they can do anything. Heh, you see the result here.";
    mes "But to be fair, from what I've seen Peetu was always very attentive and dutiful.\"";
    if ((@investigate != 5) && (@investigate != 6) && (@investigate != 7))
        goto L_Close;
    set @investigate, @investigate + 3;
    callfunc "updateBlueSageInvestigate";
    goto L_Close;

L_Close:
    set @investigate, 0;
    set @bookpages, 0;
    close;
}
