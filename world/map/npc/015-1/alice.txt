// A herb collector giving the players some hints about finding and brewing
// herbs.

015-1.gat,52,53,0|script|Alice|106,
{
    set @cat, ((Katze & NIBBLE_0_MASK) >> NIBBLE_0_SHIFT);
    set @Hiss, ((Katze & NIBBLE_1_MASK) >> NIBBLE_1_SHIFT);

    mes "[Alice]";
    mes "\"Greeting, fellow wanderer.";
    mes "Are you collecting herbs, too?\"";
    next;
    if (@Hiss == 2 && @cat < 4)
        menu
            "Yes, sure.", L_findmany,
            "Herbs?", L_herbs,
            "Nope, just enjoying the scenery.", L_Close,
            "I saw the cat, but it doesn't like me.", L_cat;
    if (@Hiss == 1)
        menu
            "Yes, sure.", L_findmany,
            "Herbs?", L_herbs,
            "Nope, just enjoying the scenery.", L_Close,
            "There is a monster in that cave!", L_cave;
    menu
        "Yes, sure.", L_findmany,
        "Herbs?", L_herbs,
        "Nope, just enjoying the scenery.", L_Close;

L_findmany:
    mes "[Alice]";
    mes "\"And? Did you already find a lot?\"";
    next;
    menu
        "Yes, I already got plenty of them.", L_plenty,
        "No, I am out of luck.", -;
    mes "[Alice]";
    mes "\"Oooh, how sad. Let me give you a little tip:";
    mes "Herbs are often growing in places where they can't be spotted that easily. Don't forget to look behind the trees, too.\"";
    goto L_Close;

L_plenty:
    mes "[Alice]";
    mes "\"That's great.";
    mes "When you are not that good at brewing you should consider letting someone else brew them for you.";
    mes "Good brewers get much better healing potions from them.";
    mes "The most important characteristic of a good brewer is intelligence.\"";
    goto L_Close;

L_herbs:
    mes "[Alice]";
    mes "\"Yes, some powerful magic herbs started to grow around here and a mile to the southeast.";
    mes "Caul in Hurnscald found a way to brew magical healing potions from them, and I have heard a rumor that they may also be good for making colors.";
    mes "Now everyone is running around in the woods to harvest the herbs.\"";
    goto L_Close;

L_cave:
    mes "[Alice]";
    mes "\"Haha, that is no monster! It's just a sweet little cat.\"";
    next;
    menu
        "But it wanted to kill me!", -,
        "A cat!? I'll go and kill it!", -;
    mes "[Alice]";
    mes "\"No no, I'm sure it was only frightened. It gets upset when you've got clothes or weapons on you.\"";
    next;
    menu
        "Ok, let's try it without clothes then", -,
        "What a stupid cat!", -;
    mes "[Alice]";
    mes "\"Please don't do her any harm. See you later!\"";
    goto L_Close;

L_cat:
    mes "[Alice]";
    mes "\"You would have to tame her first. Maybe she's thirsty or hungry. Cats also tend to get bored without something to play with. Good luck!\"";
    next;
    menu
        "Thank you so much!", L_Close,
        "Are you crazy? Taming a cat?", -;
    mes "[Alice]";
    mes "\"Well, it's up to you.\"";
    goto L_Close;

L_Close:
    set @cat, 0;
    set @Hiss, 0;
    close;
}
