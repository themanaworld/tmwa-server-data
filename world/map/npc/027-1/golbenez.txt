027-1.gat,68,89,0|script|#Golbenez#_M|204,
{
    set @fee, 666;

    mes "[Golbenez]";
    mes "\"Welcome mortal. I finished the creation of the place of leisure.\"";
    next;
    if (BaseLevel < 60)
        goto L_Weak;
    mes "\"I will bring you there for a small fee of " + @fee + "GP.\"";
    menu
        "What? You already got so much money!",-,
        "Ok, bring me there.",L_Warp;
    mes "[Golbenez]";
    mes "\"Come back when you've changed your mind. It's a wonderful place.\"";
    close;

L_Warp:
    if (Zeny < @fee)
        goto L_No_Money;
    set Zeny, Zeny - @fee;
    warp "027-2.gat", 105, 44;
    close;

L_No_Money:
    mes "[Golbenez]";
    mes "\"Don't try to betray me, pitiful human.\"";
    next;
    mes "\"Come back when you can pay my service.\"";
    close;

L_Weak:
    mes "\"It is astonishing how WEAK you humans can be.\"";
    next;
    mes "\"Come back when you're a bit more experienced.\"";
    close;

// Golbenez resets the graveyard monsters when no players are present
// He's such an evil guy!
OnInit:
    initnpctimer;
    end;

OnTimer10000:
    if (getmapusers("027-1.gat") == 0)
        killmonsterall "027-1.gat";

    setnpctimer 0;
    end;
}



