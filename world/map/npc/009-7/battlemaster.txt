009-7,40,35,0|script|Battle Master#Duels|322
{
    if($SANGUINE & $@SV_BMDBit != 0) goto L_Disabled;
    mes "[Battle Master]";
    mes "\"Hey, you seem tough enough! Would you like to prove your skills?\"";
    next;
    menu
        "PvP Cave (50gp)", L_Pvp,
        "Last Man Standing (150gp)", L_Brodomir,
        "Nevermind.", L_Next;

L_Disabled:
    mes "[Battle Master]";
    mes "I am busy, come back later.";
    close;

L_Next:
    mes "[Battle Master]";
    mes "\"Ha ha, coward.\"";
    close;

L_Brodomir:
    if (Zeny < 150) goto L_NoMoney;
    set Zeny, Zeny - 150;
    mes "[Battle Master]";
    mes "\"Get ready!\"";
    close2;
    set DUELS, DUELS | $@DuelPvpBit; // tell the arena to send the player back here when he exits
    warp "009-6", 33, 37;
    savepoint "009-7", 39, 37;
    end;

L_Pvp:
    if (Zeny < 50) goto L_NoMoney;
    set Zeny, Zeny - 50;
    mes "[Battle Master]";
    mes "\"Get ready!\"";
    close2;
    set DUELS, DUELS | $@DuelPvpBit; // tell the arena to send the player back here when he dies
    warp "001-3", 0, 0;
    savepoint "009-7", 39, 37;
    end;

L_NoMoney:
    mes "\"Wait a second, you don't have enough money.\"";
    close;
}


// death and exit handlers below

function|script|fightclub_GoBack
{
    set @Duel_PVP, DUELS & $@DuelPvpBit;
    if(@Duel_PVP != 0) goto L_GoBack;
    return;

L_GoBack:
    set DUELS, DUELS &~ $@DuelPvpBit;
    warp "009-7", 39, 37;
    gmcommand "@alive";
    message strcharinfo(0), "Thank you for participating!";
    return;
}
009-6,36,48,0|script|#GoBack2Duels|127,0,1
{
    callfunc "fightclub_GoBack";
    end;
}
009-5,53,74,0|script|#GoBack3Duels|127,3,1
{
    callfunc "fightclub_GoBack";
    end;
}
009-3,158,83,0|script|#GoBack5Duels|127,7,0
{
    callfunc "fightclub_GoBack";
    end;
}


001-2,130,22,0|script|#GoBack4Duels|127,1,1
{
    callfunc "fightclub_GoBack";
    end;
}
001-3,73,28,0|script|#GoBack1Duels|127,0,1
{
    callfunc "fightclub_GoBack";
    end;
}
