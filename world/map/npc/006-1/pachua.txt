006-1.gat,24,113,0|script|Pachua|143
{
    setarray @npc_loc, 24, 113, 4;
    callfunc "PCtoNPCRange";
    if (@npc_check)
        goto L_Close;

    set @halloween_npc_id, $@halloween_npc_pachua;
    callfunc "TrickOrTreat";

    set @LEATHER_PATCH_PRICE, 300;
    set @wants_leather_patch, QUEST_Forestbow_state & NIBBLE_4_MASK;

    if (QUEST_MIRIAM_cheat != 0) goto L_warp_cheat;
    if (QUEST_MIRIAM_start != 0) goto L_smoke;
    goto L_Begin;

L_Begin:
    mes "[Chief Pachua]";
    mes "";
    mes "\"How!\"";
    next;

    if (getequipid(equip_head) == 643 || getequipid(equip_head) == 644) goto L_WearingCowboy;
    if (getequipid(equip_legs) == 642) goto L_WearingChaps;

    mes "[Chief Pachua]";
    mes "";
    mes "\"For generations my tribe has been crafting special clothes out of different items.\"";
    next;
    goto L_Check_Shops;

L_Check_Shops:
    if((countitem("JeansShorts") > 0 && countitem("SnakeSkin") > 9) && (countitem("FancyHat") > 0 && countitem("SnakeSkin") > 1)) goto L_Super_store;
    if(countitem("JeansShorts") > 0 && countitem("SnakeSkin") > 9) goto L_Chaps_store;
    if(countitem("FancyHat") > 0 && countitem("SnakeSkin") > 1) goto L_Cowboy_store;

    mes "[Chief Pachua]";
    mes "";
    mes "\"Maybe if you bring me the right materials I can make something for you.\"";
    next;
    if (@wants_leather_patch)
        menu
            "Wait, can you make a leather patch for me?", L_leather_patch,
            "OK, bye.", L_Close;
    goto L_Close;

S_CheckStuff:
    mes "[Chief Pachua]";
    mes "";
    mes "\"Let me see what you have there.\"";
    next;
    return;

L_Super_store:
    callsub S_CheckStuff;
    mes "[Chief Pachua]";
    mes "";
    mes "\"Ahh you have lots of good items to work with.\"";
    mes "";
    mes "\"With them I can make you either";
    mes "a Cowboy hat or Snake Skin Chaps\"";
    next;
    if (@wants_leather_patch)
        menu
            "Cowboy hat, please.", L_BuyCowboy,
            "Snake Skin Chaps sound good.", L_BuyChaps,
            "Can you make a leather patch?", L_leather_patch,
            "Not now, maybe later.", L_NoDeal;
    menu
        "Cowboy hat, please.", L_BuyCowboy,
        "Snake Skin Chaps sound good.", L_BuyChaps,
        "Not now, maybe later.", L_NoDeal;

L_Cowboy_store:
    callsub S_CheckStuff;
    mes "[Chief Pachua]";
    mes "";
    mes "\"To make you a Cowboy hat I will need:";
    mes "1 Fancy hat";
    mes "2 Snake skins";
    mes "5.000 GP\"";
    mes "";
    mes "\"Do we have a deal?\"";
    next;
    if (@wants_leather_patch)
        menu
            "Yes, that's fine.", L_BuyCowboy,
            "Can you make a leather patch?", L_leather_patch,
            "On second thought, maybe later.", L_NoDeal;
    menu
        "Yes, that's fine.", L_BuyCowboy,
        "On second thought, maybe later.", L_NoDeal;

L_Chaps_store:
    callsub S_CheckStuff;
    mes "[Chief Pachua]";
    mes "";
    mes "\"To make you a pair of Snake Skin Chaps I will need:";
    mes "1 Jeans Shorts";
    mes "10 Snake skins";
    mes "10.000 GP\"";
    mes "";
    mes "\"Do we have a deal?\"";
    next;
    if (!@wants_leather_patch)
        menu
            "Yes, that's fine.", L_BuyChaps,
            "On second thought, maybe later.", L_NoDeal;
    menu
        "Yes, that's fine.", L_BuyChaps,
        "Can you make a leather patch?", L_leather_patch,
            "On second thought, maybe later.", L_NoDeal;

L_BuyChaps:
    setarray @delitem$, "SnakeSkin", "JeansShorts";
    setarray @delitem, 10, 1;
    setarray @getitem$, "JeansChaps";
    setarray @getitem, 1;
    set @delzeny, 10000;
    callfunc "CheckInventory";
    if(@check_fail) close;
    goto L_DealDone;

L_BuyCowboy:
    set @hat$, "WhiteCowboyHat";
    if(rand(1,2) == 1) set @hat$, "BlackCowboyHat";
    setarray @delitem$, "SnakeSkin", "FancyHat";
    setarray @delitem, 2, 1;
    setarray @getitem$, @hat$;
    setarray @getitem, 1;
    set @delzeny, 5000;
    callfunc "CheckInventory";
    if(@check_fail) close;
    goto L_DealDone;

L_leather_patch:
    mes "[Chief Pachua]";
    mes "";
    mes "\"If you just want a piece of leather, then yes, I can make that. Bring me a snake skin and " + @LEATHER_PATCH_PRICE + " GP.\"";
    next;
    menu
        "Here you are.", L_Next,
        "OK, I'll be back later.", L_Close,
        "That's too expensive!.", L_NoDeal;

L_Next:
    setarray @delitem$, "SnakeSkin";
    setarray @delitem, 1;
    setarray @getitem$, "LeatherPatch";
    setarray @getitem, 1;
    set @delzeny, @LEATHER_PATCH_PRICE;
    callfunc "CheckInventory";
    if(@check_fail) close;
    goto L_DealDone;

L_DealDone:
    mes "[Chief Pachua]";
    mes "";
    mes "\"Here you are!\"";
    mes "";
    mes "\"Come back any time.\"";
    goto L_Close;

L_NoDeal:
    mes "[Chief Pachua]";
    mes "";
    mes "\"Alright, but you won't get a better deal anywhere else!\"";
    goto L_Close;

L_WearingCowboy:
    mes "[Chief Pachua]";
    mes "";
    mes "\"Ah, I see that you are wearing a hat made with the ancient methods of my tribe.\"";
    next;
    goto L_Check_Shops;

L_WearingChaps:
    mes "[Chief Pachua]";
    mes "";
    mes "\"Ah, I see that you are wearing pants made by my tribe.\"";
    next;
    goto L_Check_Shops;

L_Close:
    set @LEATHER_PATCH_PRICE, 0;
    set @wants_leather_patch, 0;
    set @month, 0;
    set @start_day, 0;
    set @end_day, 0;
    set @temp, 0;
    close;

L_smoke:
    message strcharinfo(0), "Pachua quickly inhales from his pipe and releases a ring of smoke towards the sky!";
    set QUEST_MIRIAM_run, gettimetick(2) - QUEST_MIRIAM_start;
    set QUEST_MIRIAM_start, 0;
    end;

L_warp_cheat:
    if (@warp_cheat == 1) goto L_Begin;
    message strcharinfo(0), "Pachua releases a ring of smoke towards the sky! But, by the look on his face, you can tell he is suspicious about your methods...";
    set @warp_cheat, 1;
    end;
}
