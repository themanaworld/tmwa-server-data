// author: Jenalya, wushin
// Reino asks for reed bundles and water to make wrapping paper
// also for some dyes to make them colorful
// later he can be asked for those papers to wrap boxes

030-2.gat,140,31,0|script|Reino|328
{
    callfunc "XmasStates";

    if($@xmas_time)
        goto L_XmasMain;
    goto L_OffSeason;

L_OffSeason:
    mes "[Reino]";
    mes "\"Hrmm... I forgot what the real color of my hand was... \"";
    goto L_Close;

L_XmasMain:
    if (@xmas_helper_bit) goto L_Helping;

    mes "[Reino]";
    mes "\"Excuse me, but please don't interrupt. We're all very busy.\"";
    goto L_Close;

L_Helping:
    mes "[Reino]";
    mes "\"Oh, I see you're here to help. Maybe you can assist me with a problem we have.";
    mes "We're nearly out of wrapping paper for the present boxes.";
    mes "That's very very bad. Can you imagine how it would look if the presents didn't have wrapping paper?\"";
    next;
    mes "\"So, eh, I can only give you some of the remaining wrapping paper if you bring me the needed material to make new wrapping paper.";
    mes "For making one roll of wrapping paper, I need " + $@xmas_reed_amount + " Reed Bundles, " + $@xmas_water_amount + " Bottle of Water and some dye to make it colored.\"";
    next;
    goto L_Again;

L_Again:
    mes "\"Let's see... These are the regulations for wrapping paper colors and designs for this year. Tell me which you want.\"";
    mes "He shows you an official looking piece of paper.";
    menu
        "White dotted (" + $@xmas_poa_amount +" Piles of Ash)", L_White,
        "Yellow dotted (" + $@xmas_wrap_yellow_amount + " Yellow Dyes)", L_Yellow,
        "Blue dotted (" + $@xmas_wrap_ltblue_amount + " Light Blue Dyes)", L_Blue,
        "Purple striped (" + $@xmas_wrap_purple_amount + " Purple Dye)", L_Purple,
        "Green (" + $@xmas_wrap_green_amount + " Green Dye)", L_Green,
        "Why do you have a regulation for wrapping papers?!", L_Regulation,
        "I'm on my way.",L_Close;

L_White:
    setarray @delitem$, "ReedBundle", "BottleOfWater", "PileOfAsh";
    setarray @delitem, $@xmas_reed_amount, $@xmas_water_amount, $@xmas_poa_amount;
    setarray @getitem$, "WhiteWrap";
    setarray @getitem, $@xmas_wrap_reward;
    callfunc "CheckInventory";
    if(@check_fail) close;
    mes "[Reino]";
    mes "\"This one is classical.\"";
    goto L_MadePaper;

L_Yellow:
    setarray @delitem$, "ReedBundle", "BottleOfWater", "YellowDye";
    setarray @delitem, $@xmas_reed_amount, $@xmas_water_amount, $@xmas_wrap_yellow_amount;
    setarray @getitem$, "YellowWrap";
    setarray @getitem, $@xmas_wrap_reward;
    callfunc "CheckInventory";
    if(@check_fail) close;
    mes "[Reino]";
    mes "\"Right, bright paper brings good vibes.\"";
    goto L_MadePaper;

L_Blue:
    setarray @delitem$, "ReedBundle", "BottleOfWater", "LightBlueDye";
    setarray @delitem, $@xmas_reed_amount, $@xmas_water_amount, $@xmas_wrap_ltblue_amount;
    setarray @getitem$, "BlueWrap";
    setarray @getitem, $@xmas_wrap_reward;
    callfunc "CheckInventory";
    if(@check_fail) close;
    mes "[Reino]";
    mes "\"Plain and simple. Here.\"";
    goto L_MadePaper;

L_Purple:
    setarray @delitem$, "ReedBundle", "BottleOfWater", "PurpleDye";
    setarray @delitem, $@xmas_reed_amount, $@xmas_water_amount, $@xmas_wrap_purple_amount;
    setarray @getitem$, "PurpleWrap";
    setarray @getitem, $@xmas_wrap_reward;
    callfunc "CheckInventory";
    if(@check_fail) close;
    mes "[Reino]";
    mes "\"Mh, personally I don't like it that much, but here you go.\"";
    goto L_MadePaper;

L_Green:
    setarray @delitem$, "ReedBundle", "BottleOfWater", "GreenDye";
    setarray @delitem, $@xmas_reed_amount, $@xmas_water_amount, $@xmas_wrap_green_amount;
    setarray @getitem$, "GreenWrap";
    setarray @getitem, $@xmas_wrap_reward;
    callfunc "CheckInventory";
    if(@check_fail) close;
    mes "[Reino]";
    mes "\"Yeah, this is really Christmas-styled.\"";
    goto L_MadePaper;

L_MadePaper:
    next;
    mes "\"Do you need more wrapping paper?\"";
    menu
        "Yes.",L_Again,
        "No, I have all I need.",L_Close;

L_Regulation:
    mes "Reino seems irritated.";
    mes "[Reino]";
    mes "\"Well, because, ah - there would be total chaos without regulations!";
    mes "You know, this Christmas business requires a lot of coordination. Things have to be well-ordered.\"";
    goto L_Close;

L_Close:
    close;
}
