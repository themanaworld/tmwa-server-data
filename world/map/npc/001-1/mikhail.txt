// Variables used: nibble 0 of QUEST_SouthTulimshar

001-1.gat,50,46,0|script|Mikhail|120,{

    // This NPC previously used the variable TMW_Quest
    callfunc "ClearVarTMW_Quest";

    set @state, ((QUEST_SouthTulimshar & NIBBLE_0_MASK) >> NIBBLE_0_SHIFT);

    if (@state >= 6) goto L_Done;
    if (@state == 5) goto L_Progress;
    if (@state == 4) goto L_Start;
    if (@state < 4) goto L_Convince_Mikhail_First;

L_Convince_Mikhail_First:
    mes "[Mikhail]";
    mes "\"Hello...\"";
    mes "";
    mes "It seems that the boy would like to say more, but does not dare to open up to you. Maybe you should help someone else first, so that he sees your intentions are good.";
    goto L_Close;

L_Start:
    mes "[Mikhail]";
    mes "\"My grandma sent me to get five Maggot Slimes from the maggots in the desert, but they scare me so bad!\"";
    next;
    mes "\"You look like a nice person.  Would you go get them for me?\"";
    menu
        "Of course, I'll go get them for you.", L_accept,
        "I've got other things to do right now.", L_Close;

L_accept:
    mes "[Mikhail]";
    mes "\"Thank you so much!  I'll wait for you here.\"";
    set @state, 5;
    callsub S_Update_Var;
    close;

L_Progress:
    mes "[Mikhail]";
    mes "\"Did you bring me the five Maggot Slimes I need?\"";
    next;
    menu
        "Yes, here they are, kiddo!", L_try,
        "Not yet, but I'll be back soon.", L_Close;

L_try:
    if countitem("MaggotSlime") >= 5
        goto L_get;
    mes "[Mikhail]";
    mes "\"It doesn't look like you have them all...\"";
    goto L_Close;

L_get:
    delitem "MaggotSlime", 5;
    getexp 100, 0;
    set @state, 6;
    callsub S_Update_Var;
    mes "[Mikhail]";
    mes "\"Ooh!  Thank you so much!  I can get back to my grandma now!\"";
    goto L_Close;

L_Done:
    mes "[Mikhail]";
    mes "\"Thanks again for helping me get those Maggot Slimes!\"";
    goto L_Close;

L_Close:
    set @state, 0;
    close;

S_Update_Var:
    set QUEST_SouthTulimshar, (QUEST_SouthTulimshar & ~(NIBBLE_0_MASK) | (@state << NIBBLE_0_SHIFT));
    return;
}
