//

001-1.gat,108,55,0|script|Luca|102,
{
    set @EXP_BRAWLING, 2000;

    if (BaseLevel >= 35) goto L_teach;

    if (BaseLevel >= 25) goto L_teach_soon;

    mes "[Luca the Hunter]";
    mes "\"Ouch! It hurts, this wound I got from battle.\"";
    next;
    menu "Tell me about it",L_Exp,"Nevermind",L_Nev;

L_Exp:
    mes "[Luca the Hunter]";
    mes "\"I was just coming back from a long journey. I ran into a group of scorpions and started fighting them for experience.\"";
    next;
    mes "[Luca the Hunter]";
    mes "\"Then, out of a cave came a BLACK one! I had NEVER seen it before!\"";
    next;
    mes "[Luca the Hunter]";
    mes "\"Luckily I had a camera with me! Here's a picture of it... Let me find it, I put it in my pocket somewhere...\"";
    next;
    mes "[Luca the Hunter]";
    mes "\"Oh man! My pockets have been ripped clean off!\"";
    goto L_Close;

L_Nev:
    mes "[Luca the Hunter]";
    mes "\"Hmpf!\"";
    goto L_Close;

L_teach_soon:
    mes "[Luca the Hunter]";
    mes "\"You've grown quite a bit stronger since I first saw you, strolling around town like that! You know, if you train a little more, maybe we can help each other out a little?\"";
    goto L_Close;

L_teach:
    if (getskilllv(SKILL_POOL)) goto L_teachmore;

    mes "[Luca the Hunter]";
    mes "\"Hey there! You've become quite the adventurer, haven't you? I think it's time that someone taught you some basic skills.\"";
    next;
    mes "[Luca the Hunter]";
    mes "\"If you want, I can help with that!\"";
    next;
    menu
        "Sure, that sounds fun!", L_Next,
        "I'm not interested.", L_Nev;

L_Next:
    mes "[Luca the Hunter]";
    mes "\"All right! It's not all that easy, though. First you have to learn how to focus. That's because you can learn many skills, but you can't focus on all of them all the time.\"";
    next;
    mes "[Luca the Hunter]";
    mes "\"When you can do that, you can learn some real skills, and when you know them, you can come back to me to tell me which ones you want to focus on. Deal?\"";
    next;
    menu
        "Yeah!", L_teach0_follow,
        "Certainly!", L_teach0_follow,
        "Wait... what do you mean?", L_CallExplain,
        "Please explain some more.", L_CallExplain,
        "No.", L_Nev;
L_CallExplain:
    callsub S_explain;
    goto L_teach0_follow;

L_teach0_follow:
    mes "[Luca the Hunter]";
    mes "\"Great! Now, before I can give you that skill, I have to make sure that those stories about you are all true and that you really are experienced enough.\"";
    next;
    mes "[Luca the Hunter]";
    mes "\"So I'm going to ask you some easy questions.\"";
    next;
    mes "[Luca the Hunter]";
    mes "\"First, who is the man who guards the entrance to the old Tulimshar underground arena?\"";
    next;
    input @answer$;
    if (@answer$ != "Phaet" && @answer$ != "phaet") goto L_wronganswer;
    mes "[Luca the Hunter]";
    mes "\"Good. Next, what's the name of the pipe-smoker who lives on top of Lore Mountain and makes leathery goods there?\"";
    next;
    input @answer$;
    if (@answer$ != "Pachua" && @answer$ != "pachua") goto L_wronganswer;
    mes "[Luca the Hunter]";
    mes "\"Great! One more: Who was the adventurer who built Dimond's Cove for Dimond?\"";
    next;
    input @answer$;
    if (@answer$ != "Merlin" && @answer$ != "merlin") goto L_wronganswer;
    mes "[Luca the Hunter]";
    mes "Luca laughs in excitement.";
    mes "\"All right! You're the real thing, my friend!\"";
    next;
    mes "[Luca the Hunter]";
    mes "\"Now stand over there. This should only take a second...\"";
    mes "He takes off a pendant he is wearing and holds it up.";
    next;
    mes "[Luca the Hunter]";
    mes "\"See that fang at the end of the chain? Keep staring at it.\"";
    next;
    mes "[Luca the Hunter]";
    mes "He starts swinging the pendant back and forth.";
    mes "\"And whatever you do, don't blink. Don't look away, and don't blink.\"";
    next;
    mes "[Luca the Hunter]";
    mes "The world slows down around you as you focus on the pendant going back...";
    next;
    mes "and forth...";
    next;
    mes "and back...";
    next;
    mes "and forth...";
    next;
    mes "and back...";
    next;
    mes "and...";
    next;
    mes "You feel relaxed.";
    next;
    mes "[Luca the Hunter]";
    mes "\"... still there?\"";
    mes "You hear the sound of fingers snapping. Why would someone do that on such a calm day?";
    next;
    mes "[Luca the Hunter]";
    mes "\"Oh, good... I think it worked. Nice job!\"";
    mes "He grins.";
    mes "[You gain 10,000 experience points]";
    mes "[You learned Skill Focus]";
    setskill SKILL_POOL, 1;
    getexp 10000, 0;
    next;
    goto L_teachmore2;

L_wronganswer:
    mes "[Luca the Hunter]";
    mes "\"No, that was wrong. I suppose you're not as experienced as I thought you'd be.\"";
    goto L_Close;

S_explain:
    mes "[Luca the Hunter]";
    mes "\"Alright, what do you want to know?\"";
    next;
    goto L_Explain_loop;

L_Explain_loop:
    menu
        "What's skill focus?", L_explain_focus,
        "What skills are there?", L_explain_skills,
        "How do skills work?", L_explain_work,
        "Thanks, I think I got it!", L_Return;
L_Return:
    return;

L_explain_focus:
    mes "[Luca the Hunter]";
    mes "\"Well, you can learn many, many skills. But you can't really use all of them at once, not even old Auldsbels' head is big enough for that! So you have to focus.\"";
    next;
    mes "[Luca the Hunter]";
    mes "\"But you can change that focus whenever you want. It works like this: You come to me, you tell me what you want to focus on, and we do exercises until it's stuck in your head.\"";
    next;
    mes "[Luca the Hunter]";
    mes "\"If there's not enough space in your head, you'll first have to stop thinking about some other thing. There's an exercise I learned to do that, so when you talk to me you can just let me know what you want to forget.\"";
    next;
    mes "[Luca the Hunter]";
    if (getskilllv(SKILL_POOL) == 0)
        mes "\"Right now you can't focus on anything yet, so I'll first have to teach you a simple skill to keep your focus.\"";
    if (getskilllv(SKILL_POOL) == 1)
        mes "\"Right now you can only focus on one thing at a time. So if you want to change what your focus is, you have to forget whatever else you've focused on.\"";
    if (getskilllv(SKILL_POOL) > 1)
        mes "\"You can focus on " + getskilllv(SKILL_POOL) + " skills right now.\"";
    next;
    mes "[Luca the Hunter]";
    mes "\"You can come back to me to focus or unfocus a skill whenever you want, I'm here all day.\"";
    next;
    mes "\"For unfocusing you need to bring a potion; a special potion. That potion is not the easiest to get, but I am sure you are able to.\"";
    next;
    goto L_Explain_loop;

L_explain_skills:
    mes "[Luca the Hunter]";
    mes "\"Well, I don't really know... lots, I think. I can teach you brawling, which is good when you've run out of arrows or don't have a weapon around.\"";
    next;
    mes "[Luca the Hunter]";
    mes "\"But there's other skills around. Some mages need special skills to concentrate, so ask around in that crowd, if that's your thing.\"";
    next;
    mes "[Luca the Hunter]";
    mes "\"I've also heard that there is someone in Hurnscald who can help you make your body more resistant against some things. But body and mind belong together, so you probably have to focus for that, too.\"";
    next;
    mes "[Luca the Hunter]";
    mes "\"Other than that, just ask around! There have to be some people who can teach you something...\"";
    next;
    goto L_Explain_loop;

L_explain_work:
    mes "[Luca the Hunter]";
    mes "\"Well, skills allow you to be better in some things, or allow to do what you couldn't do without them. When you've learned a skill and are focusing on it, then it affects what you do.\"";
    next;
    mes "[Luca the Hunter]";
    mes "\"Now, when you start out with a skill, you won't be very good at it yet. But if you keep practicing you'll learn how to get better.\"";
    next;
    mes "[Luca the Hunter]";
    mes "\"My old teacher always called that getting `skill points' and that you can see them with [F5], whoever that is.\"";
    next;
    //TODO:
    //mes "[Luca the Hunter]";
    //mes "\"She also said that with this [F5] thing you can get better at those skills, and that it costs you as many skill points as the level you want to go to.\"";
    //next;
    mes "[Luca the Hunter]";
    mes "\"She always would have one cactus potion too many... but she was an amazing teacher.\"";
    mes "He nods emphatically.";
    next;
    goto L_Explain_loop;

L_teachmore:
    mes "[Luca the Hunter]";
    mes "\"Good to see you again!\"";
    next;
    goto L_teachmore2;

L_teachmore2:
    setarray @choices, 0,0,0,0,0;
    setarray @choice$, "", "", "", "", "";
    set @choice_nr, 0;

    set @C_focus, 1;
    set @C_unfocus, 2;
    set @C_teachbrawling, 3;
    set @C_teachnothing, 4;
    set @C_explainagain, 5;
    set @C_nvm, 6;
    set @C_focusoverview, 7;

    getactivatedpoolskilllist;
    if (!((getskilllv(SKILL_POOL) - @skilllist_count) > 0)) goto L_teachmore2_nof;
    getunactivatedpoolskilllist;
    if (@skilllist_count == 0) goto L_teachmore2_nof;
    set @choices[@choice_nr], @C_focus;
    set @choice$[@choice_nr], "I would like to focus.";
    set @choice_nr, @choice_nr + 1;
    goto L_teachmore2_nof;

L_teachmore2_nof:
    getactivatedpoolskilllist;
    if (@skilllist_count == 0) goto L_teachmore2_nounf;
    set @choices[@choice_nr], @C_unfocus;
    set @choice$[@choice_nr], "I would like to unfocus.";
    set @choice_nr, @choice_nr + 1;
    goto L_teachmore2_nounf;

L_teachmore2_nounf:
    if (!(getskilllv(SKILL_BRAWLING)))
        set @choices[@choice_nr], @C_teachbrawling;
    if (getskilllv(SKILL_BRAWLING))
        set @choices[@choice_nr], @C_teachnothing;
    set @choice$[@choice_nr], "Can you teach me a skill?";
    set @choice_nr, @choice_nr + 1;

    set @choices[@choice_nr], @C_explainagain;
    set @choice$[@choice_nr], "Can you explain skills again?";
    set @choice_nr, @choice_nr + 1;

    if (!(getskilllv(SKILL_POOL))) goto L_teachmore2_noovv;
    set @choices[@choice_nr], @C_focusoverview;
    set @choice$[@choice_nr], "Can you tell me what skills I'm currently focused on?";
    set @choice_nr, @choice_nr + 1;
    goto L_teachmore2_noovv;

L_teachmore2_noovv:
    set @choices[@choice_nr], @C_nvm;
    set @choice$[@choice_nr], "I'm done for now, thanks!";
    set @choice_nr, @choice_nr + 1;

    menu
        @choice$[0],L_MenuItems,
        @choice$[1],L_MenuItems,
        @choice$[2],L_MenuItems,
        @choice$[3],L_MenuItems,
        @choice$[4],L_MenuItems;

L_MenuItems:
    set @menu, @menu - 1;
    if (@choices[@menu] == 0) goto L_Close;
    if (@choices[@menu] == @C_focus) goto L_focus;
    if (@choices[@menu] == @C_unfocus) goto L_unfocus;
    if (@choices[@menu] == @C_teachbrawling) goto L_teach_brawling;
    if (@choices[@menu] == @C_teachnothing) goto L_teach_nothing;
    if (@choices[@menu] == @C_explainagain) goto L_teachmore_explain;
    if (@choices[@menu] == @C_focusoverview) goto L_focus_overview;
    if (@choices[@menu] == @C_nvm) goto L_Close;
    goto L_Close;

L_teachmore_explain:
    callsub S_explain;
    goto L_teachmore2;

L_focus:
    getunactivatedpoolskilllist;
    set @skilllist_id[@skilllist_count], 0;
    set @skilllist_name$[@skilllist_count], "Nevermind";
    set @skilllist_name$[@skilllist_count + 1], "";
    menu
        @skilllist_name$[0], L_MenuItems1,
        @skilllist_name$[1], L_MenuItems1,
        @skilllist_name$[2], L_MenuItems1,
        @skilllist_name$[3], L_MenuItems1,
        @skilllist_name$[4], L_MenuItems1,
        @skilllist_name$[5], L_MenuItems1,
        @skilllist_name$[6], L_MenuItems1,
        @skilllist_name$[7], L_MenuItems1;

L_MenuItems1:
    set @menu, @menu - 1;
    if (@skilllist_id[@menu] == 0) goto L_teachmore2;

    poolskill @skilllist_id[@menu];
    goto L_teachmore2;

L_unfocus:
    if (countitem("GrimaceOfDementia") > 0) goto L_unfocus_menu;

    if (MAGIG_FLAGS & MFLAG_KNOWS_UNFOCUS_RECIPE) goto L_nopotion;

    mes "[Luca the Hunter]";
    mes "\"Unfocusing is one of the harder parts though, because it is sunk in. You see?\"";
    next;
    mes "\"So to remove that mental focus we will need a special potion.\"";
    next;
    mes "\"Maybe you should consult some very skilled alchemists.\"";
    next;
    mes "\"Ah yes, of course you will need a recipe. Although the alchemist usually knows it.\"";
    next;
    mes "\"The potion is called Grimace of Dementia. So the alchemist will know for sure.\"";
    set MAGIC_FLAGS, MAGIC_FLAGS | MFLAG_KNOWS_UNFOCUS_RECIPE;
    menu
        "Ok, I will get a Grimace of Dementia potion", L_Close,
        "Alright, be right back!", L_Close;

L_nopotion:
    mes "[Luca the Hunter]";
    mes "\"Hey, you really need that dementia potion for unfocusing.\"";
    next;
    mes "\"Come back when you've found a suitable alchemist who can mix such a potion.\"";
    next;
    goto L_teachmore2;

L_unfocus_menu:
    mes "[Luca the Hunter]";
    mes "\"Ahh, I see you have a dementia potion.\"";
    next;
    mes "\"What skill would you like to unfocus?\"";

    getactivatedpoolskilllist;

    set @skilllist_id[@skilllist_count], 0;
    set @skilllist_name$[@skilllist_count], "Nevermind";
    set @skilllist_name$[@skilllist_count + 1], "";
    menu
        @skilllist_name$[0], L_MenuItems2,
        @skilllist_name$[1], L_MenuItems2,
        @skilllist_name$[2], L_MenuItems2,
        @skilllist_name$[3], L_MenuItems2,
        @skilllist_name$[4], L_MenuItems2,
        @skilllist_name$[5], L_MenuItems2,
        @skilllist_name$[6], L_MenuItems2,
        @skilllist_name$[7], L_MenuItems2;

L_MenuItems2:
    set @menu, @menu - 1;
    if (@skilllist_id[@menu] == 0) goto L_teachmore2;

    if (countitem("GrimaceOfDementia") == 0) goto L_nopotion;
    delitem "GrimaceOfDementia", 1;

    unpoolskill @skilllist_id[@menu];
    goto L_Close;

L_focus_overview:
    getactivatedpoolskilllist;

    if (@skilllist_count != 0) goto L_focus_overview1;
    mes "[Luca the Hunter]";
    mes "\"You aren't focused on a skill at the moment.\"";
    next;
    goto L_teachmore2;

L_focus_overview1:
    mes "[Luca the Hunter]";
    set @message$, "\"At the moment you are focused on ";
    goto L_focus_overview2;

L_focus_overview2:
    set @skilllist_count, @skilllist_count-1;
    set @message$, @message$ + @skilllist_name$[@skilllist_count];
    if (@skilllist_count == 0) goto L_focus_overview3;
    set @message$, @message$ + ", ";
    goto L_focus_overview2;

L_focus_overview3:
    set @message$, @message$ + ".\"";
    mes @message$;
    next;
    goto L_teachmore2;

L_teach_brawling:
    mes "[Luca the Hunter]";
    mes "\"Well, yes, I can teach you brawling. It's a nice thing, if you have nothing left but your hands to defend yourself.\"";
    next;
    mes "He is laughing.";
    mes "\"And is handy, if some sapling starts getting naughty.\"";
    next;
    mes "He raises his hands in front of his face and clenches his fists.";
    mes "\"Ok, let's go! Hold your hands like this.\"";
    next;
    mes ".......";
    next;
    mes "Some bruises later.";
    mes "\"Great! I think, you got it.\"";
    next;
    set @SUP_id, SKILL_BRAWLING;
    set @SUP_lvl, 1;
    set @SUP_name$, "Brawling";
    set @SUP_xp, @EXP_BRAWLING;
    callfunc "SkillUp";
    goto L_Close;

L_teach_nothing:
    mes "[Luca the Hunter]";
    mes "\"I was able to teach you brawling. For other professions you should visit someone else.\"";
    next;
    mes "\"You see, I am an adventurer. Everybody has his tricks. But you really should visit someone who is a teacher, not an adventurer, now.\"";
    next;
    goto L_teachmore2;

L_Close:
    set @SUP_id, 0;
    set @SUP_lvl, 0;
    set @SUP_name$, "";
    set @SUP_xp, 0;
    set @EXP_BRAWLING, 0;
    set @answer$, "";
    set @message$, "";
    cleararray @choices[0], 0, 4;
    cleararray @choice$[0], "", 4;
    set @choice_nr, 0;
    set @C_focus, 0;
    set @C_unfocus, 0;
    set @C_teachbrawling, 0;
    set @C_teachnothing, 0;
    set @C_explainagain, 0;
    set @C_nvm, 0;
    set @C_focusoverview, 0;
    close;
}
