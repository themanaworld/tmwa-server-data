001-1,71,23,0|script|Eomie|164
{
    set @grass_amount, 5;
    set @water_amount, 2;
    set @ice_amount, 2;

    set @silk_exp, 600;

    if (QL_ANWAR >= 14) goto L_Done;
    if (QL_ANWAR >= 9) goto L_CastedSpell;
    if (QL_ANWAR == 8) goto L_GetStuff;

    mes "[Eomie]";
    mes "\"I maintain this little oasis in the desert.\"";
    if (QL_ANWAR != 7)
        goto L_Close;
    menu
        "I need to learn a spell to use Tinris' magic fertilizer. Can you help me out?",L_Next;

L_Next:
    mes "[Eomie]";
    mes "\"Oh? Well, that's a rather difficult task.";
    mes "I'd be glad to help you, but unfortunately, I can't. If I were to interrupt the magic I use to keep this alive, it would die.\"";
    next;
    mes "\"It's unusually hot at the moment – even for this desert area – so it's really out of the question.\"";
    menu
        "Is there any chance you could help me?",L_Continue;

L_Continue:
    mes "She hesitates.";
    set QL_ANWAR, 8;
    goto L_GetStuff;

L_GetStuff:
    // QL_ANWAR == 8
    mes "[Eomie]";
    mes "\"I could try to briefly interrupt my magic and then cast the spell on the fertilizer, but I would need to prepare the grass beforehand.";
    mes "Bring me " + @grass_amount + " " + getitemlink("GrassSeed") + ", " + @water_amount + " " + getitemlink("BottleOfWater") + ", and " + @ice_amount + " " + getitemlink("IceCube") + ".\"";
    menu
        "I've got everything.",L_More,
        "Where can I get Grass Seeds?",L_Hurnscald,
        "How can I get Ice Cubes? We're in the middle of the desert!",L_Mine,
        "Ok. I'll go and get them.",L_Close;
L_More:
    if ((countitem("GrassSeed") < @grass_amount) || (countitem("BottleOfWater") < @water_amount) || (countitem("IceCube") < @ice_amount))
        goto L_NoItem;

    delitem "GrassSeed", @grass_amount;
    delitem "BottleOfWater", @water_amount;
    delitem "IceCube", @ice_amount;
    set QL_ANWAR, 9;
    mes "[Eomie]";
    mes "\"Very good!\"";
    mes "She takes the ingredients you brought her and uses them to cast a cool mist into the air.";
    misceffect sfx_magic_nature;
    next;
    mes "[Eomie]";
    mes "\"That should last long enough to keep the plants alive while I cast the spell on the fertilizer. Besides, I can replant anything that dies with the seeds you brought me.\"";
    next;
    mes "She takes the fertilizer and casts some invocations. After a minute, she hands the now-glowing substance back to you.";
    mes "[Eomie]";
    mes "\"That should work now. I'm glad you're taking care of the plants! Not enough people do that nowadays.\" %%1";
    goto L_Close;

L_Hurnscald: // player asked how to get Grass Seeds
    mes "[Eomie]";
    mes "\"Squirrels tend to pick up " + getitemlink("GrassSeed") + " as they gather food. You can find them around Hurnscald if you ride the ferry over.\"";
    goto L_Close;

L_Mine: // player asked how to get Ice Cubes
    mes "[Eomie]";
    mes "\"Quite the quandary – isn't it? Naturally, you can easily find them on the continent of Kaizei, which is far up north and covered with snow most time of the year. But it might be a bit difficult to get there.\"";
    next;
    mes "\"I heard that Ice Goblins were seen in the caves surrounding Nivalis. Maybe you can find " + getitemlink("IceCube") + " there.\"";
    goto L_Close;

L_CastedSpell: // QL_ANWAR >= 9 but below 14
    mes "[Eomie]";
    mes "\"Thankfully, the grass wasn't damaged. I hope Anwar finds the magic fertilizer to be useful.\"";
    if (QL_ANWAR != 13)
        goto L_Close;
    menu
        "It was. He even created this Silk Headband as a present for you!",L_Present,
        "(Mumble something and leave)",L_Close;

L_Present:
    if (countitem("SilkHeadband") < 1)
        goto L_NoItem;
    delitem "SilkHeadband", 1;
    getexp @silk_exp, 0;
    set QL_ANWAR, 14;
    mes "[Eomie]";
    mes "\"Oh, that's so nice of him! Please tell him that I really like it!\"";
    mes "She smiles.";
    goto L_Close;

L_Done: // QL_ANWAR >= 14
    mes "[Eomie]";
    mes "\"Hello! I'm happy to see someone like you so dedicated to the environment.\" %%8";
    goto L_Close;

L_NoItem:
    mes "[Eomie]";
    mes "\"Where is it?\" %%3";
    goto L_Close;

L_Close:
    set @grass_amount, 0;
    set @water_amount, 0;
    set @ice_amount, 0;
    close;
}
