// Emote NPC allows for players to learn the emote skill

001-1.gat,30,45,0|script|Entertainer|165,{
    if (getskilllv(SKILL_EMOTE) > 0) goto L_Has;

    mes "[Entertainer]";
    mes "\"Yes how can I help you?\"";
    menu
        "How are you showing emotions above your head?", L_Learn,
        "Never mind", -;
    goto L_Close;

L_Learn:
    mes "[Entertainer]";
    mes "\"They are called emotes, I use them to express how I am feeling.";
    mes "I can teach you if you'd like?\"";
    menu
        "Yes please.", L_Learn2,
        "I don't see the point right now.", -;
    goto L_Close;

L_Learn2:
    setskill SKILL_EMOTE, 1;
    mes "[Entertainer]";
    mes "\"All you have to do is press alt and a number.";
    mes "The number determines what emotion will be shown.";
    mes "Some clients will also show an emote shortcut-bar with the F12 button\"";
    goto L_Close;

L_Has:
    mes "[Entertainer]";
    mes "\"The entertainment life is a hard life...\" *sigh*";
    goto L_Close;

L_Close:
    close;

OnTimer3000:
    emotion rand(EMOTE_DISGUST, EMOTE_TEARS);
    setnpctimer 0;

OnInit:
    if (debug >= 2) end;
    initnpctimer;
}
