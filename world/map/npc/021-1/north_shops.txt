// Northern shopping plaza

// More high-end stuff to satisfy the richer people in Royal Tulimshar

// Some clothing
021-1.gat,135,38,0|shop|Inar|108,CottonShirt :-1,CottonShorts :-1,CottonBoots :-1,DesertShirt :-1,SilkRobe :-5,DesertHat :-4

021-1.gat,129,40,0|script|Well|400,{
    callfunc "WaterBottle";
}

// An officer (for security)
021-1.gat,128,36,0|script|Officer|150,{
    mes "[Officer]";
    mes "I'm here for extra security for this market.";
    close;
}

// A snobby store that won't sell to the player
// takes part in quest given by 021-2/kylian.txt
021-1.gat,138,29,0|script|Latoy|106,{

    set QUEST_NorthTulimshar, QUEST_NorthTulimshar | $@knowLatoyNT;
    set @kylian, ((QUEST_NorthTulimshar & $@businessmanNT_MASK) >> $@businessmanNT_SHIFT);

    mes "[Latoy]";
    mes "\"There's nothing here you could want or afford.\"";
    if (@kylian != 7)
        goto L_Close;
    next;
    mes "Maybe this is the kind of shop Kylian is looking for?";
    goto L_Close;

L_Close:
    set @kylian, 0;
    close;
}

