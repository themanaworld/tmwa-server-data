// Children playing Hide and Seek
// Quest uses Nibble 0 and 1 of QUEST_NorthTulimshar
// Bit 0: started quest at Ayasha
// Bit 1 - 5: found one of the kids each
// bit 6: talked to Ayasha after finding all kids
// bit 7: unused
// Author: Jenalya

-|script|#hideandseekNTconfig|-1,
{
    end;

OnInit:
    set $@hideandseekNT_MASK, BYTE_0_MASK;
    set $@hideandseekNT_SHIFT, BYTE_0_SHIFT;

    set $@hideandseekNT_started,    0x00000001;
    set $@hideandseekNT_found1,     0x00000002;
    set $@hideandseekNT_found2,     0x00000004;
    set $@hideandseekNT_found3,     0x00000008;
    set $@hideandseekNT_found4,     0x00000010;
    set $@hideandseekNT_found5,     0x00000020;
    set $@hideandseekNT_ended,      0x00000040;
    end;
}

021-1.gat,87,66,0|script|Ayasha|258,
{
    set @child_number, 0;
    callfunc "XmasList";

    set @state, ((QUEST_NorthTulimshar & $@hideandseekNT_MASK) >> $@hideandseekNT_SHIFT);

    set @EXP, 75;

    if (@state > 63) goto L_Done;
    if (@state == 63) goto L_Reward;
    if (@state > 0) goto L_Doing;

    mes "You see a girl who is holding her hands in front of her face.";
    mes "[Ayasha]";
    mes "\"98... 99... 100!\"";
    mes "She takes away her hands and looks up.";
    menu
        "Hello, what are you doing?",L_Next,
        "(Leave)",L_Close;

L_Next:
    mes "[Ayasha]";
    mes "\"I'm playing hide and seek with my friends!";
    mes "Do you want to play with us?\"";
    menu
        "Sure, sounds like fun.",L_Next1,
        "I'm busy with more important things!",L_Close;

L_Next1:
    mes "[Ayasha]";
    mes "\"Great! You can take my turn with searching. You need to find all five of my friends.";
    mes "They're hiding somewhere in this part of the city, you don't have to pass an archway to find them. But they're allowed to hide on the rooftops.\"";

    set QUEST_NorthTulimshar, QUEST_NorthTulimshar | $@hideandseekNT_started;
    goto L_Close;

L_Doing:
    mes "[Ayasha]";
    mes "\"You still need to find some of my friends. There are five of them.";
    mes "Remember, they're hiding somewhere in this part of the city, you don't have to pass an archway to find them. But it is allowed to hide on the buildings roof.\"";
    goto L_Close;

L_Reward:
    mes "[Ayasha]";
    mes "\"Oh, you found them all! Good job! Now it's my turn with searching again. Go and hide!\"";
    getexp @EXP, 0;
    set QUEST_NorthTulimshar, QUEST_NorthTulimshar | $@hideandseekNT_ended;
    menu
        "I think I need to do other things now.",L_Next2;

L_Next2:
    mes "[Ayasha]";
    mes "\"Oh, ok.";
    mes "Have you already seen the bazaar? It's a very exciting place! I love the entertainer. She can make so many funny faces.\"";
    goto L_Close;

L_Done:
    mes "[Ayasha]";
    mes "\"Hello! It was fun to play with you.";
    mes "When you go to the bazaar, you can say hello to Mikhail. He's very nice! But very shy.\"";
    goto L_Close;

L_Close:
    set @state, 0;
    set @EXP, 0;
    close;
}

021-1.gat,63,107,0|script|Latif|262,
{
    set @child_number, 1;
    callfunc "XmasList";
    set @state, ((QUEST_NorthTulimshar & $@hideandseekNT_MASK) >> $@hideandseekNT_SHIFT);

    set @EXP, 15;

    if (@state > 63) goto L_Done;
    if ((@state > 0) && !(QUEST_NorthTulimshar & $@hideandseekNT_found1)) goto L_Find;
    if (@state > 0) goto L_Doing;

    mes "You see a kid hiding behind a box.";
    menu
        "What are you doing there?",L_Next,
        "(Leave)",L_Close;

L_Next:
    mes "[Latif]";
    mes "\"Hiding. We're playing hide and seek. Don't say that is boring. It's fun.\"";
    menu
        "I agree, it's fun.",L_Agree,
        "Hide and seek? Lame.",L_Next1;

L_Next1:
    mes "[Latif]";
    mes "\"My friend Vincent thinks so too. He doesn't want to join us playing. Do you know what he does instead?";
    mes "He builds action figures out of Bug Legs! That is lame.\"";
    goto L_Close;

L_Agree:
    mes "[Latif]";
    mes "\"Yeah, right? My friend Vincent doesn't thinks so. He doesn't want to join us playing. Do you know what he does instead?";
    mes "He builds action figures out of Bug Legs! That is lame.\"";
    goto L_Close;

L_Find:
    mes "[Latif]";
    mes "\"Hey! Ah, you found me. Well done. But my hiding place wasn't that difficult this time.\"";
    getexp @EXP, 0;
    set QUEST_NorthTulimshar, QUEST_NorthTulimshar | $@hideandseekNT_found1;
    next;
    mes "\"Here, do you want a candy?\"";
    getitem "Candy", 1;
    goto L_Close;

L_Doing:
    mes "[Latif]";
    mes "\"You didn't find all of us yet? Seems like the other ones found a better place to hide.\"";
    goto L_Close;

L_Done:
    mes "[Latif]";
    mes "\"Inside that building over there is a workshop. My father works there and wants me to do that too when I'm grown up. But I don't want do that! I want to go on an adventure! Just like you.\"";
    goto L_Close;

L_Close:
    set @state, 0;
    set @EXP, 0;
    close;
}

021-1.gat,123,116,0|script|Charda|260,
{
    set @child_number, 2;
    callfunc "XmasList";
    set @state, ((QUEST_NorthTulimshar & $@hideandseekNT_MASK) >> $@hideandseekNT_SHIFT);

    set @EXP, 15;

    if (@state > 63) goto L_Done;
    if ((@state > 0) && !(QUEST_NorthTulimshar & $@hideandseekNT_found2)) goto L_Find;
    if (@state > 0) goto L_Doing;

    mes "[Charda]";
    mes "\"Hello. Psht, be careful. I'm playing hide and seek with Ayasha. You'll draw attention to my hiding place.\"";
    next;
    mes "\"Do you want to go to the bazaar? It's west and then south from here. Ian is there, he helps all those adventurers that come here.\"";
    goto L_Close;

L_Find:
    mes "[Charda]";
    mes "\"Oh! Got me! You're good at this.\"";
    getexp @EXP, 0;
    set QUEST_NorthTulimshar, QUEST_NorthTulimshar | $@hideandseekNT_found2;
    next;
    mes "She takes something out of her pocket.";
    mes "[Charda]";
    mes "\"I'm hungry now. Do you want chocolate too?\"";
    getitem "ChocolateBar", 1;
    next;
    mes "\"My mom doesn't want me to eat so much chocolate, but I got it from the trader on the bazaar. He sells many different things.";
    mes "I convinced him to teach me how to trade. It's fun.\"";
    goto L_Close;

L_Doing:
    mes "[Charda]";
    mes "\"Did you find the others yet?\"";
    goto L_Close;

L_Done:
    mes "[Charda]";
    mes "\"Hey! We're still playing hide and seek. What are you going to do? Will you go to Hurnscald with the ferry? The mother of my friends Rossy and Julia come from there. They are on a vacation there at the moment.\"";
    next;
    mes "\"Rossy told me that it's a very beautiful place. Lots of trees and flowers and grass, everything's green.\"";
    mes "She sighs.";
    mes "[Charda]";
    mes "\"Sounds wonderful.\"";
    goto L_Close;

L_Close:
    set @state, 0;
    set @EXP, 0;
    close;
}

021-1.gat,78,87,0|script|Faris|259,
{
    set @child_number, 3;
    callfunc "XmasList";
    set @state, ((QUEST_NorthTulimshar & $@hideandseekNT_MASK) >> $@hideandseekNT_SHIFT);

    set @EXP, 25;

    if (@state > 63) goto L_Done;
    if ((@state > 0) && !(QUEST_NorthTulimshar & $@hideandseekNT_found3)) goto L_Find;
    if (@state > 0) goto L_Doing;

    mes "[Faris]";
    mes "\"Oh, hi there. What are you doing here on the roof?\"";
    menu
        "I'm just having a look around.",L_Next,
        "It's nice place here, isn't it?",L_Next;

L_Next:
    mes "[Faris]";
    mes "\"He, you're cool. I'm hiding here from Ayasha, we're playing hide and seek. This is the best hiding place ever.\"";
    goto L_Close;

L_Find:
    mes "[Faris]";
    mes "\"You found me! Well done. You look a bit thirsty, here.\"";
    getexp @EXP, 0;
    getitem "CactusDrink", 1;
    set QUEST_NorthTulimshar, QUEST_NorthTulimshar | $@hideandseekNT_found3;
    next;
    mes "\"It's really cool you're playing with us. I'm sure you have a lot of other things to do.";
    mes "You're an adventurer, right? I want to become an adventurer too when I'm grown up. I'll talk to Aidan then.\"";
    next;
    mes "\"Aidan is on the bazaar and you can register there for monster points. That's something the Wizard's Council made up. They reward you for slaying monsters. It's so cool.\"";
    goto L_Close;

L_Doing:
    mes "[Faris]";
    mes "\"Are you still searching for the others? Nobody else is up here.\"";
    goto L_Close;

L_Done:
    mes "[Faris]";
    mes "\"I'll go to the bazaar and ask Bernard for some soup when we're done with playing. His soup is delicious!\"";
    goto L_Close;

L_Close:
    set @state, 0;
    set @EXP, 0;
    close;
}

021-1.gat,98,25,0|script|Rasin|263,
{
    set @child_number, 4;
    callfunc "XmasList";
    set @state, ((QUEST_NorthTulimshar & $@hideandseekNT_MASK) >> $@hideandseekNT_SHIFT);

    set @EXP, 15;

    if (@state > 63) goto L_Done;
    if ((@state > 0) && !(QUEST_NorthTulimshar & $@hideandseekNT_found4)) goto L_Find;
    if (@state > 0) goto L_Doing;

    mes "[Rasin]";
    mes "\"The harbor is up north from here. There you can take a ferry to other places.";
    mes "And now go away, I'm playing hide and seek and Ayasha shouldn't see me here. You'll draw her attention when you talk to me.\"";
    goto L_Close;

L_Find:
    mes "[Rasin]";
    mes "\"Ah, you found me. I need to find a better place to hide next time.\"";
    getexp @EXP, 0;
    set QUEST_NorthTulimshar, QUEST_NorthTulimshar | $@hideandseekNT_found4;
    goto L_Close;

L_Doing:
    mes "[Rasin]";
    mes "\"Hehe, I'm curious when you'll have found all of us.\"";
    goto L_Close;

L_Done:
    mes "[Rasin]";
    mes "\"Thanks for playing with us! It was fun!";
    mes "Will you leave Tulimshar to go on an adventure in other places of the world?\"";
    goto L_Close;

L_Close:
    set @state, 0;
    set @EXP, 0;
    close;
}

021-1.gat,155,25,0|script|Ghada|265,
{
    set @child_number, 5;
    callfunc "XmasList";
    set @state, ((QUEST_NorthTulimshar & $@hideandseekNT_MASK) >> $@hideandseekNT_SHIFT);

    set @EXP, 15;

    if (@state > 63) goto L_Done;
    if ((@state > 0) && !(QUEST_NorthTulimshar & $@hideandseekNT_found5)) goto L_Find;
    if (@state > 0) goto L_Doing;

    mes "You see a girl standing behind the building. She has a look of intense concentration on her face.";
    menu
        "Hi, what are you doing there?",L_Next,
        "(Leave)",L_Close;

L_Next:
    mes "[Ghada]";
    mes "\"Oh, hello. I'm hiding. We're playing hide and seek.\"";
    goto L_Close;

L_Find:
    mes "[Ghada]";
    mes "\"Oh no! You found me! You must be very clever.";
    mes "My friend Sandra is clever too, but she said it's a waste of time to play hide and seek.\"";
    getexp @EXP, 0;
    set QUEST_NorthTulimshar, QUEST_NorthTulimshar | $@hideandseekNT_found5;
    goto L_Close;

L_Doing:
    mes "[Ghada]";
    mes "\"Are you still searching for the others? I'm sure you'll find them soon.\"";
    goto L_Close;

L_Done:
    mes "[Ghada]";
    mes "\"It's so nice that you played with us! You're about the same age as my older sister, but she never plays with us. Her name is Sarah.\"";
    goto L_Close;

L_Close:
    set @state, 0;
    set @EXP, 0;
    close;
}
