//Connected to the inspector quest, see 009-1/inspector.txt

009-1,29,43,0|script|Old Woman|154
{
    if (BaseLevel < 40) goto L_Lower;

    mes "[Old Woman]";
    mes "\"Hello deary.\"";
    if ((QL_INSPECTOR >= 1  && QL_INSPECTOR <= 7) || QL_INSPECTOR == 9)
        goto L_NohMask;
    close;

L_Lower:
    mes "[Old Woman]";
    mes "\"Watch out for these flowers. They don't like to be messed with.\"";
    if ((QL_INSPECTOR >= 1  && QL_INSPECTOR <= 7) || QL_INSPECTOR == 9)
        goto L_NohMask;
    close;

L_NohMask:
    next;
    if (QL_INSPECTOR == 7)
        goto L_NohMask_Alibi;
    callfunc "ProcessEquip";
    if (@torsoC == cDarkBlue && @legsC == cDarkBlue)
        goto L_NohMask_Fake;
    if (QL_INSPECTOR >= 3 && QL_INSPECTOR <= 6)
        goto L_Close;
    menu
        "Have you seen anything strange lately?", L_NohMask_FirstAsk,
        "Do you know anything about the recent robberies?", L_NohMask_FirstAsk,
        "Hello", L_Close;

L_NohMask_FirstAsk:
    mes "[Old Woman]";
    mes "\"Yes, but I'm only talking to the inspector himself!\"";
    if (QL_INSPECTOR != 1)
        goto L_Close;
    set QL_INSPECTOR, 2;
    goto L_Close;

L_NohMask_Alibi:
    menu
        "Was your husband with you at home all night the last night that the troupe was in town?", L_Next;

L_Next:
    mes "[Old Woman]";
    mes "\"Yes, we were both at home all night.\"";
    set QL_INSPECTOR, 8;
    goto L_Close;

L_NohMask_Fake:
    if (QL_INSPECTOR == 9) goto L_NohMask_Fake_Satchel;
    if (QL_INSPECTOR >= 3 && QL_INSPECTOR <= 6) goto L_NohMask_Filler;

    mes "[Old Woman]";
    mes "\"I saw someone sneaking around town wearing a theater mask. It looked like one of the masks used by the troupe that was in town recently.\"";
    set QL_INSPECTOR, 3;
    goto L_Close;

L_NohMask_Filler:
    mes "[Old Woman]";
    mes "\"I hope you catch that naughty person!\"";
    goto L_Close;

L_NohMask_Fake_Satchel:
    mes "[Old Woman]";
    mes "\"I've remembered something else. The night the troupe left, I saw someone with a theater mask take a large satchel out of town. He was heading north.\"";
    set QL_INSPECTOR, 10;
    goto L_Close;

L_Close:
    close;
}
