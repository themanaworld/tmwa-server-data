// Authors: alastrim, Ali-g, Dark Mage

025-1.gat,80,88,0|script|Rossy|114,
{
    set @child_number, 6;
    callfunc "XmasList";

    set @Cherry_Amount, 10;
    set @Cherry_EXP, 2000;

    set @CactusDrink_Amount, 24;
    set @MaggotSlime_Amount, 32;
    set @GambodgeHerb_Amount, 50;
    set @AlizarinHerb_Amount, 43;
    set @BugLeg_Amount, 30;
    set @Potion_EXP, 3000;

    set @RedRose_Amount, 15;
    set @RedTulip_Amount, 15;
    set @Flower_EXP, 2500;

    set @Finish_EXP, 5000;
    set @Finish_Money, 5000;

    if (FLAGS & FLAG_ROSSI_COMPLETED) goto L_FinalEnd;
    if (Rossy_Quest == 17 || Rossy_Quest == 18) goto L_End;

    if (Rossy_Quest >= 14 && Rossy_Quest <= 16) goto L_Julia;
    if (Rossy_Quest >= 13) goto L_Best;
    if (Rossy_Quest >= 11) goto L_Allergic;
    if (Rossy_Quest >= 8 && Rossy_Quest < 11) goto L_Donet;
    if (Rossy_Quest == 7) goto L_BT;
    if (Rossy_Quest == 6) goto L_Check2;
    if (Rossy_Quest == 5) goto L_Let;
    if (Rossy_Quest == 4 || Rossy_Quest == 5) goto L_Cherry_Done;
    if (Rossy_Quest == 3) goto L_Check;
    if (Rossy_Quest == 2) goto L_Mother;

    mes "A young girl looks at you in tears.";
    if (Rossy_Quest < 1)
        close;
    next;
    menu
        "Hey, hey... Why are you crying? Are you hurt?", -,
        "Sorry kid, I don't have time to play with children.", L_No;
    mes "[Young Girl]";
    mes "\"No. I'm crying because I lost all my fruits. My sister and I were collecting some near Dimond's Cove, but then I stumbled on a giant snail and it attacked us!";
    next;
    mes "But we were lucky, because the snail is really slow and we ran away from it. Julia ran to the left and I ran to the right. The snail got pretty confused, because we are identical twins. The problem is that I dropped the basket full of fruits on the ground and that evil snail ate them all! And now I can't find Julia to help me collect more fruits.\"";
    next;
    menu
        "Did you say Julia? I talked to you mother. She is very worried about you.", -;
    mes "[Rossy]";
    mes "\"Really? But she let us play in the woods. There is no reason to be worried. Unless...\"";
    next;
    mes "Rossy suddenly gets a strange look on her face, mumbling something about Julia.";
    next;
    mes "[Rossy]";
    mes "\"Could you do me a favor? Please, tell my mother we are alright. There is.. uh... there is no reason to worry about us.\"";
    next;
    menu
        "Ok, if you say so.", -,
        "Sorry, I have no time for this.", L_No;
    mes "[Rossy]";
    mes "\"Thank you very much!\"";
    set Rossy_Quest, 2;
    close;

L_No:
    close;

L_Mother:
    mes "[Rossy]";
    mes "\"Please, tell my mother we are fine. If you feel like helping, come back after you talk to my mother. Maybe you could help us find some fruits.\"";
close;

L_Get_Cherry:
    mes "[Young Girl]";
    mes "\"No, you don't have " + @Cherry_Amount + " cherries. I know how to count. I hope you get them soon.\"";
close;

L_Check:
    mes "[Rossy]";
    mes "\"Hey, that is nice of you to come back. I was thinking about giving a basket full of cherries to my mother, to make her happy, you know. But we'd need at least " + @Cherry_Amount + " cherries, and it would take ages for me to get so many by myself!\"";
    next;
    menu
        "That is no problem for me. Just wait and I will come back with the cherries.", L_No,
        "Hey, I have some cherries in my backpack!", -,
        "You should get them yourself.", L_No;
    if(countitem("Cherry") < @Cherry_Amount)
        goto L_Get_Cherry;
    mes "[Rossy]";
    set Rossy_Quest, 4;
    getexp @Cherry_EXP, 0;
    mes "\"I can't believe it, you got them all! Please give them to my mother. Tell her it is a gift from me and Julia and there is no reason to worry about us.\"";
    next;
    mes "Rossy mumbles to herself.";
    mes "[Rossy]";
    mes "\"I hope...\"";
    next;
    mes "[Rossy]";
    mes "\"Oh, after you give the cherries to my mother, please come back here. If you have some free time, of course.\"";
    close;

L_Cherry_Done:
    mes "[Rossy]";
    mes "\"Please, try to distract my mother. I don't want her to be worried about us. While you do this, I will try to find Julia.\"";
    close;

L_Let:
    mes "[Rossy]";
    mes "\"Hello "+ strcharinfo(0) +", nice to see you again. Hey, what are you hiding behind your back? Is that a... letter?\"";
    next;
    menu
        "Your mother asked me to hand you this letter. She told me it was from your teacher.", L_Let2,
        "Not at all. I need to go now, see you next time.", L_No;

L_Let2:
    mes "The little girl grabs the piece of paper and reads it quickly. Her face turns pale.";
    menu
        "What does it say?", -;
    mes "[Rossy]";
    mes "\"Oh no, I completely forgot! My magic school exam! I need to make a potion and give it to my teacher today! But I haven't found Julia yet and I don't even have any of the ingredients required. Could you get them for me, please?\"";
    next;
    set Rossy_Quest, 6;
    mes "\"I need " + @CactusDrink_Amount + " Cactus Drinks, " + @MaggotSlime_Amount + " Maggot Slimes, " + @GambodgeHerb_Amount + " Gamboge Herbs, " + @AlizarinHerb_Amount + " Alizarin Herbs and " + @BugLeg_Amount + " Bug Legs.\"";
    next;
    mes "\"Please come back to see me when you have the ingredients. I know the recipe by heart.\"";
    close;

L_NE:
    mes "[Rossy]";
    mes "\"Please bring me " + @CactusDrink_Amount + " Cactus Drinks, " + @MaggotSlime_Amount + " Maggot Slimes, " + @GambodgeHerb_Amount + " Gamboge Herbs, " + @AlizarinHerb_Amount + " Alizarin Herbs and " + @BugLeg_Amount + " Bug Legs.\"";
    next;
    mes "\"Please hurry!\"";
    close;

L_Check2:
    if(countitem("CactusDrink") < @CactusDrink_Amount)
        goto L_NE;
    if(countitem("MaggotSlime") < @MaggotSlime_Amount)
        goto L_NE;
    if(countitem("GambogeHerb") < @GambodgeHerb_Amount)
        goto L_NE;
    if(countitem("AlizarinHerb") < @AlizarinHerb_Amount)
        goto L_NE;
    if(countitem("BugLeg") < @BugLeg_Amount)
        goto L_NE;
    delitem "CactusDrink", @CactusDrink_Amount;
    delitem "MaggotSlime", @MaggotSlime_Amount;
    delitem "GambogeHerb", @GambodgeHerb_Amount;
    delitem "AlizarinHerb", @AlizarinHerb_Amount;
    delitem "BugLeg", @BugLeg_Amount;
    mes "Rossy takes all the ingredients and places them on the ground. She starts to mix some herbs with the cactus drinks and appears to cast some spells. She seems to know what she is doing.";
    next;
    mes "[Rossy]";
    mes "\"Oh, thank you "+ strcharinfo(0) +"! It is done. If you don't mind, please give this potion to my teacher.\"";
    set Rossy_Quest, 7;
    getexp @Potion_EXP, 0;
    message strcharinfo(0), "Rossy hands you the potion, holding the bottle very carefully.";
    next;
    mes "\"My teacher's name is David. He is a very young magician and lives near the magic school in the north of Tulimshar.\"";
    next;
    mes "\"Please, give it to him as soon as you can!\"";
close;

L_BT:
    mes "[Rossy]";
    mes "\"Great. Now if you could just give it to my teacher. His name is David. He is a very young magician and lives near the magic school in the north of Tulimshar.\"";
    next;
    mes "\"You should take the ferry, that'll be faster than swimming!\"";
close;

L_Donet:
    if (Rossy_Quest == 10
        && countitem("RedRose") >= @RedRose_Amount)
            goto L_RG;
    menu
        "David took your potion and he tested it in front of me. It worked perfectly!", -;
    mes "[Rossy]";
    mes "\"Yay! Phew... That was close, I almost forgot about my exam. Please, tell my mother about it. I am sure she will be proud of me.\"";
close;

L_RG:
    if (countitem("RedRose") < @RedRose_Amount)
        goto L_No;
    delitem "RedRose", @RedRose_Amount;
    menu
        "Your mother was so happy that she asked me to bring you these flowers.", -;
    mes "[Rossy]";
    mes "\"How nice!\"";
    next;
    mes "Rossy takes the flowers from your hands and suddenly throws them on the ground. She begins sneezing.";
    set Rossy_Quest, 11;
    menu
        "I presume you don't like roses, right?", -;
    mes "[Rossy]";
    mes "\"I am allergic to roses, my mother should already know that!\"";
close;

L_Allergic:
    mes "Rossy sneezes a lot.";
    if (Rossy_Quest == 12
        && countitem("RedTulip") >= @RedTulip_Amount)
            goto L_Sorry;
close;

L_Sorry:
    menu
        "Your mother is sorry about the roses. Here are some beautiful red tulips instead.", L_See;

L_See:
    mes "[Rossy]";
    if (countitem("RedTulip") < @RedTulip_Amount)
        goto L_No;
    delitem "RedTulip", @RedTulip_Amount;
    set Rossy_Quest, 13;
    mes "\"That's better! Go and tell my mother that she is the nicest mother in the world.\"";
    next;
    mes "\"Now I should really find my sister. Come back later.\"";
    getexp @Flower_EXP, 0;
close;

L_Best:
    mes "[Rossy]";
    mes "\"Thank you "+ strcharinfo(0) +", you helped me so much!\"";
    next;
    menu
        "Did you find your sister? I am starting to get worried too.", -;
    mes "\"I think I have an idea where she might be. Come back in a few minutes, I will tell you if she is ok.\"";
    set Rossy_Quest, 14;
close;

L_Julia:
    mes "[Rossy]";
    mes "\"I saw Julia! She was running away from something and entered a cave not far from here. I heard her scream. Please can you enter the cave and save her? I'm so scared!\"";
close;

L_End:
    mes "[Rossy]";
    mes "\"Oh no, Julia... Why would you go to that scary place? Have you seen her yet?\"";
    menu
        "She'll be back soon, trust me. I saved her.", L_Found,
        "I didn't see her yet, sorry.", L_No;

L_Found:
    mes "[Rossy]";
    if (Rossy_Quest == 17)
        set Rossy_Quest, 18;
    if (Rossy_Quest == 18)
        set Rossy_Quest, 19;
    mes "\"Oh "+ strcharinfo(0) +", thank you very much! Take this, you deserve it!\"";
    next;
    mes "Rossy hands you a huge bag full of coins.";
    getexp @Finish_EXP, 0;
    set Zeny, Zeny + @Finish_Money;
    if (Rossy_Quest == 19)
        goto L_Clear;
close;

L_Clear:
    set Rossy_Quest, 0;
    set cavefights, 0;
    set FLAGS, FLAGS | FLAG_ROSSI_COMPLETED;
close;


L_FinalEnd:
    mes "[Rossy]";
    mes "\"Julia and I are playing hide and seek right now. It is fun in the forest!\"";
    mes "The young girl laughs quietly.";
close;

}
