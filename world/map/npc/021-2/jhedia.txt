// Ingot maker takes iron ore, gives ingots for future crafting purposes.

021-2.gat,70,124,0|script|Jhedia|160
{
    set @Cost_Per_Ingot, 1000;
    set @Iron_Ore_Per, 5;
    set @Coal_Per, 10;

    mes "[Jhedia]";
    mes "\"I forge Iron Ore into ingots. Ingots are more useful for skilled smiths, like armorers, because the ore is refined and made into useful units which can be worked with. To make ingots, I need " + @Iron_Ore_Per + " Iron Ore and " + @Cost_Per_Ingot + " gp per ingot. I also need " + @Coal_Per + " Coal for my forge.\"";
    if((Zeny < @Cost_Per_Ingot) || (countitem("IronOre") < @Iron_Ore_Per) || (countitem("Coal") < @Coal_Per)) close;
    next;
    mes "[Jhedia]";
    mes "\"How many ingots would you like?\"";
    input @ingot_count;

    if (@ingot_count == 0)
        goto L_Jhedia_Bye;

    setarray @delitem$, "IronOre", "Coal";
    setarray @delitem, @ingot_count * @Iron_Ore_Per, @ingot_count * @Coal_Per;
    setarray @getitem$, "IronIngot";
    setarray @getitem, @ingot_count;
    set @delzeny, @ingot_count * @Cost_Per_Ingot;
    callfunc "CheckInventory";
    close;

L_Jhedia_Bye:
    mes "[Jhedia]";
    mes "\"Goodbye then.\"";
    close;
}
