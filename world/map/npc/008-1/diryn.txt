// This is a teleportation (warp) NPC.  Its purpose is to offer expensive, fast trips throughout the world.
// Author: Wombat
// cost depending on level and adapted to new scripting guidelines: Jenalya

008-1.gat,81,82,0|script|Diryn the Traveler|103,{
    if (BaseLevel < 45)
        goto L_LowerCost;

    set @cost_druidtree, 1500;
    set @cost_graveyard, 1500;
    set @cost_magichouse, 1000;
    set @cost_terranitecave, 1500;
    set @cost_tulimshar, 1000;
    set @cost_nivalis, 1500;

L_Start:
    mes "[Diryn]";
    mes "\"Greetings.  I am Diryn the Traveler, Assistant Councilor of Tulimshar and teleporter extraordinaire.  Tulimshar is expanding our influence throughout the world, so I was sent here to aid all who are willing and able to take on the threats to our interests.\"";
    next;
    mes "\"The monsters of the world have grown out of control, harming people, trade and travel.  Needless to say, Tulimshar is not happy with this problem.  Our solution: Rally those powerful enough to combat the monster threat and send them to hot spots to challenge the monster threat head on.  However, teleportation to these areas is not only extremely dangerous, but also highly expensive.   Do you think you got what it takes?\"";
    menu
        "I sure do!", L_Diryn_Yes,
        "No, thank you.", L_Diryn_No;

L_Diryn_Yes:
    mes "[Diryn]";
    mes "\"Excellent.  I can send you to many places, but again, the fees are large. Where shall I send you?\"";
    menu
        "Druid Tree (" + @cost_druidtree + " GP)", L_Diryn_Druid_Tree,
        "Graveyard (" + @cost_graveyard + " GP)", L_Diryn_Graveyard,
        "Magic House (" + @cost_magichouse + " GP)", L_Diryn_Magic_House,
        "Terranite Cave (" + @cost_terranitecave + " GP)", L_Diryn_Terranite,
        "Tulimshar Town Square (" + @cost_tulimshar + " GP)", L_Diryn_Tulimshar,
        "Sage Nikolai's Mansion (" + @cost_nivalis + " GP)", L_Diryn_Nivalis,
        "I'm not interested.", L_Diryn_No;

L_Diryn_Druid_Tree:
    if (Zeny < @cost_druidtree)
        goto L_NoMoney;
    mes "[Diryn]";
    mes "\"Be fearless!\"";
    close2;
    set Zeny, Zeny - @cost_druidtree;
    warp "005-1.gat",73,36;
    goto L_Clearvars;

L_Diryn_Graveyard:
    if (Zeny < @cost_graveyard)
        goto L_NoMoney;
    mes "[Diryn]";
    mes "\"Be fearless!\"";
    close2;
    set Zeny, Zeny - @cost_graveyard;
    warp "027-1.gat",70,85;
    goto L_Clearvars;

L_Diryn_Magic_House:
    if (Zeny < @cost_magichouse)
        goto L_NoMoney;
    mes "[Diryn]";
    mes "\"Be fearless!\"";
    close2;
    set Zeny, Zeny - @cost_magichouse;
    warp "013-1.gat",45,92;
    goto L_Clearvars;

L_Diryn_Terranite:
    if (Zeny < @cost_terranitecave)
        goto L_NoMoney;
    mes "[Diryn]";
    mes "\"Be fearless!\"";
    close2;
    set Zeny, Zeny - @cost_terranitecave;
    warp "012-3.gat",445,65;
    goto L_Clearvars;

L_Diryn_Tulimshar:
    if (Zeny < @cost_tulimshar)
        goto L_NoMoney;
    mes "[Diryn]";
    mes "\"Be fearless!\"";
    close2;
    set Zeny, Zeny - @cost_tulimshar;
    warp "001-1.gat",44,70;
    goto L_Clearvars;

L_Diryn_Nivalis:
    if (Zeny < @cost_nivalis)
        goto L_NoMoney;
    mes "[Diryn]";
    mes "\"Be fearless!\"";
    close2;
    set Zeny, Zeny - @cost_nivalis;
    warp "048-1.gat",65,45;
    goto L_Clearvars;

L_Diryn_No:
    mes "[Diryn]";
    mes "\"Perhaps some day you will have the courage to help us.  Enjoy your stay here in Hurnscald.\"";
    goto L_Clearvars;

L_NoMoney:
    mes "[Diryn]";
    mes "\"Sorry, you don't have enough.  Maybe next time.\"";
    goto L_Clearvars;

L_LowerCost:
    set @cost_druidtree, 750;
    set @cost_graveyard, 750;
    set @cost_magichouse, 500;
    set @cost_terranitecave, 750;
    set @cost_tulimshar, 500;
    set @cost_nivalis, 500;
    goto L_Start;

L_Clearvars:
    set @cost_druidtree, 0;
    set @cost_graveyard, 0;
    set @cost_magichouse, 0;
    set @cost_terranitecave, 0;
    set @cost_tulimshar, 0;
    set @cost_nivalis, 0;
    end;
}
