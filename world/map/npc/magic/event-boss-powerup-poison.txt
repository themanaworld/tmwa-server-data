-|script|powerup-masspoison|32767
{
    end;

OnCast:
    if (call("boss_powerup_checks")) end;
    if (getmapflag(getmap(), MF_TOWN)) goto L_Fail_town;
    set @_M_BLOCK, 1; // block casting, until the timer clears it
    addtimer 16000, "Magic Timer::OnClear"; // set the new debuff
    sc_start SC_COOLDOWN, 16000, 0, BL_ID;
    misceffect FX_MAGIC_SNAKE_CAST, strcharinfo(0);
    set @dist, 12;
    foreach 0, getmap(), (POS_X - @dist), (POS_Y - @dist), (POS_X + @dist), (POS_Y + @dist), strnpcinfo(0) + "::OnHit";
    end;

OnHit:
    if (@target_id == BL_ID) end;               // Do not poison caster
    if ((get(Hp, @target_id)) < 1) end;         // Do not poison dead!
    message strcharinfo(0, @target_id), "[boss-masspoison] : "+strcharinfo(0, BL_ID)+" poisoned you!";
    misceffect FX_EMOTE_DISGUST, strcharinfo(0, @target_id);
    sc_start SC_POISON, 1, 60, @target_id;
    end;

L_Fail_town:
    message strcharinfo(0), "[boss-masspoison] : can't mass-poison in towns!";
    end;

OnInit:
    set .school, SKILL_MAGIC;
    set .invocation$, chr(MAGIC_SYMBOL) + "mappoison"; // used in npcs that refer to this spell
    void call("magic_register", "OnCast");
    set .level, 0;
    set .exp_gain, 0;
    end;
}
