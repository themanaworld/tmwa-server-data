-|script|enchant-lifestone|32767
{
    if(call("magic_checks")) end;
    if (Sp < 15) end;
    if (getskilllv(.school) < .level) end;
    if   (countitem("BugLeg") >= 1) delitem "BugLeg", 1;
    elif (countitem("MaggotSlime") >= 1) delitem "MaggotSlime", 1;
    elif (countitem("MauveHerb") >= 1 && countitem("AlizarinHerb") >= 1 && countitem("CobaltHerb") >= 1 && countitem("GambogeHerb") >= 1) goto L_Herbs;
    else end;
    goto L_Proceed;

L_Herbs:
    delitem "MauveHerb", 1;
    delitem "AlizarinHerb", 1;
    delitem "CobaltHerb", 1;
    delitem "GambogeHerb", 1;
    goto L_Proceed;

L_Proceed:
    set @_M_BLOCK, 1; // block casting, until the timer clears it
    addtimer 4000, "Magic Timer::OnClear"; // set the new debuff
    sc_start SC_COOLDOWN, 4000, 0, BL_ID;
    callfunc "adjust_spellpower";
    set Sp, Sp - 15;
    set CASTS, CASTS + 1;
    if (CASTS < 0) set CASTS, 1; // overflow
    misceffect FX_MAGIC_STONE_CAST, strcharinfo(0);
    getitem "Lifestone", 1;
    callfunc "magic_exp";
    end;

OnInit:
    set .school, SKILL_MAGIC;
    set .invocation$, chr(MAGIC_SYMBOL) + "manpahil"; // used in npcs that refer to this spell
    void call("magic_register");
    set .level, 2;
    set .exp_gain, 1;
    end;
}
