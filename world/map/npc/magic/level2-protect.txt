-|script|protect|32767
{
    set @target_id, getcharid(3, @args$);
    if (@target_id < 1 || !(isloggedin(@target_id)) || get(INVISIBLE, @target_id) == 1)
        set @target_id, BL_ID; // fallback to self
    callfunc "adjust_spellpower";
    if (distance(BL_ID, @target_id) >= (@spellpower/30)+2) set @target_id, BL_ID;
    if (BL_ID == @target_id) set @args$, strcharinfo(0);
    if (sc_check(SC_PHYS_SHIELD_ITEM, @target_id)) goto L_Itsanc; // itsanc has top priority and can't be overwritten by betsanc
    if(call("magic_checks")) end;
    if (Sp < 14) end;
    set .@level, getskilllv(.school);
    if (.@level < .level) end;
    if (getskilllv(SKILL_MAGIC) < .level) end;
    if (.@level <= 3 && countitem("HardSpike") < 1) end;
    if (get(GM, @target_id) == 1) end;
    if (get(Hp, @target_id) < 1) end;
    if (getequipid(equip_head, strcharinfo(0, @target_id)) == 888) end;
    if (get(@antiprotect, @target_id) > 0) end;
    if (getmap() == "033-1") goto L_SpecialRules6;
    goto L_Pay;

L_Pay:
    if (.@level <= 3) delitem "HardSpike", 1;
    set @_M_BLOCK, 1; // block casting, until the timer clears it
    addtimer 1500, "Magic Timer::OnClear"; // set the new debuff
    sc_start SC_COOLDOWN, 1500, 0, BL_ID;
    set Sp, Sp - 14;
    set CASTS, CASTS + 1;
    if (CASTS < 0) set CASTS, 1; // overflow
    misceffect FX_MAGIC_SHIELD_CAST, strcharinfo(0);
    callfunc "magic_exp";
    misceffect FX_MAGIC_SHIELD, @args$;
    set .@betsanc_time, (@spellpower*1000)+5000;
    sc_start SC_PHYS_SHIELD, .@betsanc_time, max(15,(@spellpower/20))+5, @target_id;
    message @args$, "Shield : You feel more protected.";
    set @betsanc_future, gettimetick(0) + .@betsanc_time - 100, @target_id;
    addtimer .@betsanc_time - 100, strnpcinfo(0)+"::OnEnd", @target_id;
    end;

OnEnd:
    if (gettimetick(0) - @betsanc_future < 0) end;
    if (sc_check(SC_PHYS_SHIELD) != 1) end;
    message strcharinfo(0), "Shield : You feel less protected.";
    misceffect FX_MAGIC_SHIELD_ENDS, strcharinfo(0);
    end;

L_Itsanc:
    if (BL_ID == @target_id) goto L_ItsancSelf;
    goto L_ItsancOther;

L_ItsancSelf:
    message strcharinfo(0), "Shield : You are still itsanced, This blocks your spell.";
    end;

L_ItsancOther:
    message strcharinfo(0), "Shield : Your target is still itsanced, This blocks your spell.";
    end;


L_SpecialRules6:
    if ($@KIMARR_EVENT < 1) goto L_Pay;
    if (@target_id == BL_ID) goto L_Pay;
    message strcharinfo(0), "You can't cast this spell at others here.";
    end;

OnInit:
    set .school, SKILL_MAGIC_NATURE;
    set .invocation$, chr(MAGIC_SYMBOL) + "betsanc"; // used in npcs that refer to this spell
    void call("magic_register");
    set .level, 2;
    set .exp_gain, 2;
    end;
}
