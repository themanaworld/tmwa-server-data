-|script|smwmushroom|32767
{
    end;

OnCast:
    if(call("magic_checks")) end;
    if (Sp < 35) end;
    if (getskilllv(SKILL_MAGIC) < .level) end;
    if (getskilllv(.school) < .level) end;
    if (countitem("DarkCrystal") < 1 || countitem("SmallMushroom") < 1) end;
    if (OrumQuest <= 36) end;
    delitem "DarkCrystal", 1;
    delitem "SmallMushroom", 1;
    set @_M_BLOCK, 1; // block casting, until the timer clears it
    addtimer 15000, "Magic Timer::OnClear"; // set the new debuff
    callfunc "adjust_spellpower";
    set Sp, Sp - 35;
    misceffect FX_MAGIC_DARKRED, strcharinfo(0);
    misceffect FX_PENTAGRAM_BUILDUP, strcharinfo(0);
    callfunc "magic_exp";
    set .@puppet$, "#"+strnpcinfo(0)+"#"+BL_ID;
    set .@puppet, puppet(getmap(), POS_X, POS_Y, .@puppet$, 127);
    if (.@puppet < 1) end;
    set .count, (@spellpower/250)+1, .@puppet;
    set .master, BL_ID, .@puppet;
    set .lifetime, @spellpower*80, .@puppet;
    addnpctimer 4000-(@spellpower*9), .@puppet$+"::OnSummon";
    addnpctimer 6000, .@puppet$+"::OnDestroy";
    end;

OnSummon:
    if(get(Hp, .master) < 1) destroy; // destroy if master is missing
    if(getmap(.master) != strnpcinfo(3)) destroy; // destroy if master left the map
    specialeffect FX_PENTAGRAM_BURST;
    set .@i, 0;
    set .@x, getnpcx();
    set .@y, getnpcy();
    set .@map$, strnpcinfo(3);
    callsub S_SummonAll;
    end;

OnDestroy:
    destroy;

S_SummonAll:
    summon .@map$, rand(.@x-2,.@x+2), rand(.@y-2,.@y+2), .master, 1106, 2, .lifetime;
    set .@i, .@i + 1;
    if (.@i < .count) goto S_SummonAll;
    destroy;

OnInit:
    set .school, SKILL_MAGIC_DARK;
    set .invocation$, chr(MAGIC_SYMBOL) + "helorp"; // used in npcs that refer to this spell
    void call("magic_register", "OnCast");
    set .level, 2;
    set .exp_gain, 3;
    end;
}
