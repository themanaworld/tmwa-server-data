-|script|smwmushroom|32767
{
    end;

OnCast:
    if(call("magic_checks")) end;
    if (Sp < 42) end;
    if (getskilllv(SKILL_MAGIC) < .level) end;
    if (getskilllv(.school) < .level) end;
    if (countitem("DarkCrystal") < 1 || countitem("SmallMushroom") < 1) end;
    if (OrumQuest <= 36) end;
    if (getmap() == "033-1") goto L_SpecialRules6;
    goto L_Pay;

L_Pay:
    delitem "DarkCrystal", 1;
    delitem "SmallMushroom", 1;
    set @_M_BLOCK, 1; // block casting, until the timer clears it
    addtimer 15000, "Magic Timer::OnClear"; // set the new debuff
    sc_start SC_COOLDOWN, 15000, 0, BL_ID;
    callfunc "adjust_spellpower";
    set Sp, Sp - 42;
    set CASTS, CASTS + 1;
    if (CASTS < 0) set CASTS, 1; // overflow
    misceffect FX_MAGIC_WICKED_CAST, strcharinfo(0);
    callfunc "magic_exp";
    set .@puppet$, "#"+strnpcinfo(0)+"#"+BL_ID;
    set .@puppet, puppet(getmap(), POS_X, POS_Y, .@puppet$, 127);
    if (.@puppet < 1) end;
    set .count, (@spellpower/180)+1, .@puppet;
    set .master, BL_ID, .@puppet;
    set .lifetime, @spellpower*300, .@puppet;
    addnpctimer 4000-(@spellpower*9), .@puppet$+"::OnSummon";
    addnpctimer 6000, .@puppet$+"::OnDestroy";
    end;

OnSummon:
    if (!(isloggedin(.master))) destroy;
    if(get(Hp, .master) < 1) destroy; // destroy if master is missing
    if(getmap(.master) != strnpcinfo(3)) destroy; // destroy if master left the map
    specialeffect FX_MAGIC_WICKED_SPAWN;
    set .@i, 0;
    set .@x, getnpcx();
    set .@y, getnpcy();
    set .@map$, strnpcinfo(3);
    callsub S_SummonAll;
    end;

OnDestroy:
    destroy;

S_SummonAll:
    set .@rnd, rand(0, 9);

    if   (.@rnd < 6) summon .@map$, rand(.@x-2,.@x+2), rand(.@y-2,.@y+2), .master, "Wicked Mushroom Summon", WickedMushroom, 2, .lifetime;
    elif (.@rnd < 9) summon .@map$, rand(.@x-2,.@x+2), rand(.@y-2,.@y+2), .master, "Moonshroom Summon", Moonshroom, 2, .lifetime;
    elif (.@rnd == 9) summon .@map$, rand(.@x-2,.@x+2), rand(.@y-2,.@y+2), .master, "Evil Mushroom Summon", EvilMushroom, 2, .lifetime;
    set .@i, .@i + 1;
    if (.@i < .count) goto S_SummonAll;
    destroy;

L_SpecialRules6:
    if ($@KIMARR_EVENT < 1) goto L_Pay;
    if ($@Fluffy_FighterID == BL_ID) goto L_Pay;
    message strcharinfo(0), "You can't summon here unless hunting fluffies.";
    end;

OnInit:
    set .school, SKILL_MAGIC_DARK;
    set .invocation$, chr(MAGIC_SYMBOL) + "helorp"; // used in npcs that refer to this spell
    void call("magic_register", "OnCast");
    set .level, 2;
    set .exp_gain, 3;
    end;
}
