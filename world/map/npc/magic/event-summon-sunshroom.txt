-|script|smsmushrooms|32767
{
    end;

OnCast:
    if(call("magic_checks")) end;
    if (Sp < 66) end;
    if (getmapflag(getmap(), MF_TOWN)) end;
    if (getskilllv(SKILL_MAGIC) < .level) end;
    if (getskilllv(.school) < .level) end;
    if (countitem("SmallMushroom") < 1 || countitem("Root") < 1) end;
    if (getmap() == "033-1") goto L_SpecialRules6;
    goto L_Pay;

L_Pay:
    delitem "SmallMushroom", 1;
    delitem "Root", 1;
    set @_M_BLOCK, 1; // block casting, until the timer clears it
    addtimer 20000, "Magic Timer::OnClear"; // set the new debuff
    sc_start SC_COOLDOWN, 20000, 0, BL_ID;
    callfunc "adjust_spellpower";
    set Sp, Sp - 33;
    set CASTS, CASTS + 1;
    if (CASTS < 0) set CASTS, 1; // overflow
    misceffect FX_MAGIC_SPIKY_CAST, strcharinfo(0);
    callfunc "magic_exp";
    set .@puppet$, "#"+strnpcinfo(0)+"#"+BL_ID;
    set .@puppet, puppet(getmap(), POS_X, POS_Y, .@puppet$, 127);
    if (.@puppet < 1) end;
    set .count, (@spellpower/120)+1, .@puppet;
    set .master, BL_ID, .@puppet;
    set .lifetime, @spellpower*400, .@puppet;
    addnpctimer 5000-(@spellpower*9), .@puppet$+"::OnSummon";
    addnpctimer 6000, .@puppet$+"::OnDestroy";
    end;

OnSummon:
    if (!(isloggedin(.master))) destroy;
    if(get(Hp, .master) < 1) destroy; // destroy if master is missing
    if(getmap(.master) != strnpcinfo(3)) destroy; // destroy if master left the map
    specialeffect FX_MAGIC_SPIKY_SPAWN;
    set .@i, 0;
    set .@x, getnpcx();
    set .@y, getnpcy();
    set .@map$, strnpcinfo(3);
    callsub S_SummonAll;
    end;

OnDestroy:
    destroy;

S_SummonAll:
    summon .@map$, rand(.@x-2,.@x+2), rand(.@y-2,.@y+2), .master, "Sunshroom Summon", Sunshroom, 2, .lifetime;
    set .@i, .@i + 1;
    if (.@i < .count) goto S_SummonAll;
    destroy;

L_SpecialRules6:
    if ($@KIMARR_EVENT < 1) goto L_Pay;
    if ($@Fluffy_FighterID == BL_ID) goto L_Pay;
    message strcharinfo(0), "You can't summon here unless hunting fluffies.";
    end;

OnInit:
    set .school, SKILL_MAGIC_ASTRAL;
    set .invocation$, chr(MAGIC_SYMBOL) + "kalsun"; // used in npcs that refer to this spell
    void call("magic_register", "OnCast");
    set .level, 2;
    set .exp_gain, 1;
    end;
}
