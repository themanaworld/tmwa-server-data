-|script|grow-alizarin|32767
{
    if(call("magic_checks")) end;
    if (Sp < 4) end;
    if (getskilllv(SKILL_MAGIC) < .level) end;
    if (countitem("AlizarinHerb") < 1 || countitem("Root") < 1) end;
    delitem "AlizarinHerb", 1;
    delitem "Root", 1;
    set @_M_BLOCK, 1; // block casting, until the timer clears it
    addtimer 2000, "Magic Timer::OnClear"; // set the new debuff
    sc_start SC_COOLDOWN, 2000, 0, BL_ID;
    callfunc "adjust_spellpower";
    set Sp, Sp - 4;
    set CASTS, CASTS + 1;
    if (CASTS < 0) set CASTS, 1; // overflow
    misceffect FX_MAGIC_GROW_CAST, strcharinfo(0);
    callfunc "magic_exp";
    addtimer 4000-(@spellpower-9), strnpcinfo(0)+"::OnSummon";
    end;

OnSummon:
    misceffect FX_MAGIC_GROW_SPAWN, strcharinfo(0);
    //                            map       X      Y    Rad  QTY                        OWNER  DisplayName              Mob ID        AI  Lifetime  (no custom event)
    void call("magic_summon_all", getmap(), POS_X, POS_Y, 2, (getskilllv(.school)/2)+1, BL_ID, "Alizarin Plant Summon2", AlizarinPlant, 1, (@spellpower*50)+10000, "");
    end;

OnInit:
    set .school, SKILL_MAGIC_NATURE;
    set .invocation$, chr(MAGIC_SYMBOL) + "modriphoo"; // used in npcs that refer to this spell
    void call("magic_register");
    set .level, 1;
    set .exp_gain, 1;
    end;
}
