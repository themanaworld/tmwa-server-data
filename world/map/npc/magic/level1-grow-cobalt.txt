-|script|grow-cobalt|32767
{
    callfunc "magic_checks"; if(@failed) end; // << I wish we had functions that could return >>
    if (Sp < 4) end;
    if (getskilllv(.school) < .level) end;
    if (countitem("CobaltHerb") < 1 || countitem("Root") < 1) end;
    delitem "CobaltHerb", 1;
    delitem "Root", 1;
    set MAGIC_CAST_TICK, gettimetick(2) + 2; // set the new debuff
    callfunc "adjust_spellpower";
    set Sp, Sp - 4;
    misceffect FX_MAGIC_GREEN, strcharinfo(0);
    misceffect FX_PENTAGRAM_BUILDUP, strcharinfo(0);
    setarray @summon[0], 0, (getskilllv(.school)/2)+1;
    callfunc "magic_exp";
    addtimer 4000-(@spellpower-9), strnpcinfo(0)+"::OnSummon";
    end;

OnSummon:
    misceffect FX_PENTAGRAM_BURST, strcharinfo(0);
    callsub S_SummonAll;
    end;

S_SummonAll:
    summon getmap(), rand(POS_X-2,POS_X+2), rand(POS_Y-2,POS_Y+2), BL_ID, 1030, 1, (@spellpower*50)+10000;
    set @summon[0], @summon[0] + 1;
    if (@summon[0] < @summon[1]) goto S_SummonAll;
    return;

OnInit:
    set .school, SKILL_MAGIC_NATURE;
    set .invocation$, chr(MAGIC_SYMBOL) + "modrisump"; // used in npcs that refer to this spell
    callfunc "magic_register";
    set .level, 1;
    set .exp_gain, 1;
    end;
}
