-|script|spell-aggravate|32767
{
    if(call("magic_checks")) end;
    if (Sp < 3) end;
    if (getskilllv(SKILL_MAGIC) < .level) end;
    set @_M_BLOCK, 1; // block casting, until the timer clears it
    addtimer 1000, "Magic Timer::OnClear"; // set the new debuff
    sc_start SC_COOLDOWN, 1000, 0, BL_ID;
    set @args$, ""; callfunc "adjust_spellpower"; // we reset @args$ because this spell should not have a target
    set @distance, (2 + (@spellpower / 50));
    set Sp, Sp - 3;
    set CASTS, CASTS + 1;
    if (CASTS < 0) set CASTS, 1; // overflow
    misceffect FX_MAGIC_AGGRAVATE_CAST, strcharinfo(0);
    callfunc "magic_exp";
    foreach 2, getmap(), (POS_X - @distance), (POS_Y - @distance), (POS_X + @distance), (POS_Y + @distance), strnpcinfo(0) + "::OnMob";
    end;

OnMob:
    if (target(BL_ID, @target_id, 0x20) != 0x20) end; // line of sight
    if (issummon(@target_id) != 0) end; // is a summoned creature
    aggravate @target_id;
    misceffect FX_MAGIC_AGGRAVATE_HIT, @target_id;
    end;

OnInit:
    set .school, SKILL_MAGIC_NATURE;
    set .invocation$, chr(MAGIC_SYMBOL) + "itenplz"; // used in npcs that refer to this spell
    void call("magic_register");
    set .level, 1;
    set .exp_gain, 0;
    end;
}
