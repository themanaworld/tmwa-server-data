-|script|magic barrier|32767
{
    if(call("magic_checks")) end;
    if (Sp < 15) end;
    set .@level, getskilllv(.school);
    if (.@level < .level) end;
    if (getskilllv(SKILL_MAGIC) < .level) end;
    if (.@level <= 3 && countitem("SmallMushroom") < 1) end;
    set @target_id, getcharid(3, @args$);
    if (@target_id < 1 || !(isloggedin(@target_id)) || get(INVISIBLE, @target_id) == 1)
        set @target_id, BL_ID; // fallback to self
    callfunc "adjust_spellpower";
    if (distance(BL_ID, @target_id) >= (@spellpower/30)+2) set @target_id, BL_ID;
    if (BL_ID == @target_id) set @args$, strcharinfo(0);
    if (get(GM, @target_id) == 1) end;
    if (get(Hp, @target_id) < 1) end;
    if (getequipid(equip_head, strcharinfo(0, @target_id)) == 888) end;
    if (getmap() == "033-1") goto L_SpecialRules6;
    goto L_Pay;

L_Pay:
    if (.@level <= 3) delitem "SmallMushroom", 1;
    set @_M_BLOCK, 1; // block casting, until the timer clears it
    addtimer 1000, "Magic Timer::OnClear"; // set the new debuff
    sc_start SC_COOLDOWN, 1000, 0, BL_ID;
    set Sp, Sp - 15;
    misceffect FX_MAGIC_BARRIER_CAST, strcharinfo(0);
    callfunc "magic_exp";
    if (BL_ID != @target_id) misceffect FX_MAGIC_BARRIER_HIT, @args$;
    set .@asorm_time, (@spellpower*200)+2000;
    sc_start SC_MBARRIER, .@asorm_time, max(30,(@spellpower/8))+20, @target_id;
    message @args$, "Barrier : You are surrounded by a magical barrier.";
    set @asorm_future, gettimetick(0) + .@asorm_time - 100, @target_id;
    addtimer .@asorm_time - 100, strnpcinfo(0)+"::OnEnd", @target_id;
    end;

OnEnd:
    if (gettimetick(0) - @asorm_future < 0) end;
    if (sc_check(SC_MBARRIER) != 1) end;
    message strcharinfo(0), "Barrier : Your magical barrier dissipates.";
    misceffect FX_MAGIC_BARRIER_END, strcharinfo(0);
    end;

L_SpecialRules6:
    if ($@KIMARR_EVENT < 1) goto L_Pay;
    if (@target_id == BL_ID) goto L_Pay;
    message strcharinfo(0), "You can't cast this spell at others here.";
    end;

OnInit:
    set .school, SKILL_MAGIC_ASTRAL;
    set .invocation$, chr(MAGIC_SYMBOL) + "asorm"; // used in npcs that refer to this spell
    void call("magic_register");
    set .level, 2;
    set .exp_gain, 3;
    end;
}
