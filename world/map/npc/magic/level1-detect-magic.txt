-|script|detect-magic|32767
{
    if(call("magic_checks")) end;
    if (Sp < 3) end;
    if (getskilllv(SKILL_MAGIC) < .level) end;
    set @_M_BLOCK, 1; // block casting, until the timer clears it
    addtimer 6000, "Magic Timer::OnClear"; // set the new debuff
    sc_start SC_COOLDOWN, 6000, 0, BL_ID;
    callfunc "adjust_spellpower";
    set Sp, Sp - 3;
    set CASTS, CASTS + 1;
    if (CASTS < 0) set CASTS, 1; // overflow
    misceffect FX_MAGIC_DETECT_CAST, strcharinfo(0);
    set .@range, (@spellpower/50)+1;
    foreach 1, getmap(), POS_X - .@range, POS_Y - .@range, POS_X + .@range, POS_Y + .@range,
            strnpcinfo(0) + "::OnNearbyNpc";
    if (getmap() == "099-4")
        addtimer 10, "#TMWFinalExam::OnMiteyo";
    if (getmap() == "099-5")
        addtimer 10, "#TMWFinalExam::OnMiteyo";
    callfunc "magic_exp";
    end;

OnNearbyNpc:
    set .@e$, strnpcinfo(2,@target_id);
    if(.@e$ == "#_M" || .@e$ == "#MAGIC" || get(.IS_MAGIC, @target_id))
        misceffect FX_MAGIC_DETECT_HIT, @target_id;
    end;

OnInit:
    set .school, SKILL_MAGIC;
    set .invocation$, chr(MAGIC_SYMBOL) + "miteyo"; // used in npcs that refer to this spell
    void call("magic_register");
    set .level, 1;
    set .exp_gain, 0;
    end;
}
