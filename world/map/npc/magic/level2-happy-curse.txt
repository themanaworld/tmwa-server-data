-|script|happy-curse|32767
{
    if(call("magic_checks")) end;
    if (Sp < 13) end;
    set .@level, getskilllv(.school);
    if (.@level < .level) end;
    if (getskilllv(SKILL_MAGIC) < .level) end;
    if (.@level <= 3 && countitem("GingerBreadMan") >= 1) delitem "GingerBreadMan", 1;
    elif (.@level <= 3) end;

    set @_M_BLOCK, 1; // block casting, until the timer clears it
    addtimer 1000, "Magic Timer::OnClear"; // set the new debuff
    sc_start SC_COOLDOWN, 1000, 0, BL_ID;
    callfunc "adjust_spellpower";
    set Sp, Sp - 13;
    set CASTS, CASTS + 1;
    if (CASTS < 0) set CASTS, 1; // overflow
    misceffect FX_MAGIC_JOY_CAST, strcharinfo(0);
    callfunc "magic_exp";

    set @distance, (@spellpower / 100) + 1;
    foreach 0, getmap(), (POS_X - @distance), (POS_Y - @distance), (POS_X + @distance), (POS_Y + @distance), strnpcinfo(0) + "::OnHit";
    end;

OnHit:
    set @joyplim_count, 1, @target_id;
    set @joyplim_emote, if_then_else(getskilllv(SKILL_MAGIC_DARK) > 1, EMOTE_EVIL, EMOTE_HAPPY), @target_id;
    set @joyplim_total, (@spellpower / 10), @target_id;
    emotion @joyplim_emote, strcharinfo(0, @target_id);
    addtimer 500, strnpcinfo(0)+"::OnEmote", @target_id;
    end;

OnEmote:
    emotion @joyplim_emote, "self";
    set @joyplim_count, @joyplim_count + 1;
    if (@joyplim_count < @joyplim_total) addtimer 500, strnpcinfo(0)+"::OnEmote";
    end;

OnInit:
    set .school, SKILL_MAGIC_NATURE;
    set .invocation$, chr(MAGIC_SYMBOL) + "joyplim"; // used in npcs that refer to this spell
    void call("magic_register");
    set .level, 2;
    set .exp_gain, 1;
    end;
}
