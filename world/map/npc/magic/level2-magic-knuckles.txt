-|script|magic-knuckles|32767
{
    if (call("magic_checks")) end;
    if (Sp < 20) end;
    if (getskilllv(SKILL_MAGIC) < .level) end;
    set .@level, getskilllv(.school);
    if (.@level < .level) end;
    if (getequipid(equip_hand1) != -1 || getequipid(equip_hand2) != -1) end;
    if (.@level <= 3 && countitem("Beer") >= 1) delitem "Beer", 1;
    elif (.@level <= 3) end;
    set @_M_BLOCK, 1; // block casting, until the timer clears it
    addtimer 500, "Magic Timer::OnClear"; // set the new debuff
    callfunc "adjust_spellpower";
    set Sp, Sp - 20;
    misceffect FX_MAGIC_KNUCKLE_CAST, strcharinfo(0);
    setarray @upmarmuspell[0],
        (BASE_ATK), // dmg
        ((Dex/3) + 10), // charges
        (sqrt(300 - (Agi * 2))+(5/2)), // delay
        Agi, // dmg bonus
        ((sqrt(BaseLevel + 9) + Str) * 2); // do not allow to equip light armor, cast, and then switch to heavy armor to get bonus str
    callfunc "magic_exp";
    goto L_FreeRecast;

OnAttack:
    if (getequipid(equip_hand1) != -1 || getequipid(equip_hand2) != -1) goto OnDischarge;
    if (target(BL_ID, @target_id, 22) != 22) goto L_FreeRecast; // 0x10 | 0x02 | 0x04
    void call("elt_damage", @upmarmuspell[0], (@upmarmuspell[3] + @upmarmuspell[4]), ELT_WATER, ELT_FIRE, FX_NONE);
    set @upmarmuspell[1], @upmarmuspell[1] - 1;
    goto L_FreeRecast;

L_FreeRecast:
    if (@upmarmuspell[1] > 0)
        addtimer 0, strnpcinfo(0) + "::OnSetRecast";
    end;

OnDischarge:
    if (@upmarmuspell[1] < 1) end;
    set @upmarmuspell[1], 0;
    misceffect FX_MAGIC_DISCHARGE, strcharinfo(0);
    overrideattack;
    end;

OnSetRecast:
    overrideattack (@upmarmuspell[2] * @upmarmuspell[2]), 1, ATTACK_ICON_GENERIC, OVERRIDE_KNUCKLES, strnpcinfo(0)+"::OnAttack", @upmarmuspell[1]; // delay neeeds to be squared
    end;

OnInit:
    set .school, SKILL_MAGIC_WAR;
    set .invocation$, chr(MAGIC_SYMBOL) + "upmarmu"; // used in npcs that refer to this spell
    void call("magic_register");
    set .level, 2;
    set .exp_gain, 1;
    end;
}
