-|script|summon-snakes|32767
{
    end;

OnCast:
    if(call("magic_checks")) end;
    if (Sp < 40) end;
    if (getskilllv(SKILL_MAGIC) < .level) end;
    if (getskilllv(.school) < .level) end;
    if (countitem("DarkCrystal") < 1 || countitem("SnakeEgg") < 1) end;
    if (OrumQuest <= 40) end;
    if (getmap() == "033-1") goto L_SpecialRules6;
    goto L_Pay;

L_Pay:
    delitem "DarkCrystal", 1;
    delitem "SnakeEgg", 1;
    set @_M_BLOCK, 1; // block casting, until the timer clears it
    addtimer 15000, "Magic Timer::OnClear"; // set the new debuff
    sc_start SC_COOLDOWN, 15000, 0, BL_ID;
    callfunc "adjust_spellpower";
    set Sp, Sp - 40;
    set CASTS, CASTS + 1;
    if (CASTS < 0) set CASTS, 1; // overflow
    misceffect FX_MAGIC_SNAKE_CAST, strcharinfo(0);
    callfunc "magic_exp";
    set .@puppet$, "#"+strnpcinfo(0)+"#"+BL_ID;
    set .@puppet, puppet(getmap(), POS_X, POS_Y, .@puppet$, 127);
    if (.@puppet < 1) end;
    set .count, (@spellpower/300)+1, .@puppet;
    set .master, BL_ID, .@puppet;
    set .lifetime, @spellpower*80, .@puppet;
    addnpctimer 4000-(@spellpower*9), .@puppet$+"::OnSummon";
    addnpctimer 6000, .@puppet$+"::OnDestroy";
    end;

OnSummon:
    if (!(isloggedin(.master))) destroy;
    if(get(Hp, .master) < 1) destroy; // destroy if master is missing
    if(getmap(.master) != strnpcinfo(3)) destroy; // destroy if master left the map
    specialeffect FX_MAGIC_SNAKE_SPAWN;
    setarray .@mobs[0], CaveSnake, Snake, MountainSnake, GrassSnake; // Which snake to summon?
    goto L_SummonRandom; // Spawn one by one to randomize

L_SummonRandom:
    set .count, .count - 1;
    set .mob, .@mobs[rand(getarraysize(.@mobs))]; // Pick random mob
    set .nm$, mobinfo(.mob, MOB_ENG_NAME) + " Summon"; // Get mob's name + add Summon
    //                            map            X          Y         Rad QTY OWNER  Disp  MobID  AI Lifetime (no custom event)
    void call("magic_summon_all", strnpcinfo(3), getnpcx(), getnpcy(), 2, 1, .master, .nm$, .mob, 2, .lifetime, "");
    if (.count > 0) goto L_SummonRandom; 
    destroy;

OnDestroy:
    destroy;

L_SpecialRules6:
    if ($@KIMARR_EVENT < 1) goto L_Pay;
    if ($@Fluffy_FighterID == BL_ID) goto L_Pay;
    message strcharinfo(0), "You can't summon here unless hunting fluffies.";
    end;

OnInit:
    set .school, SKILL_MAGIC_DARK;
    set .invocation$, chr(MAGIC_SYMBOL) + "halhiss"; // used in npcs that refer to this spell
    void call("magic_register", "OnCast");
    set .level, 2;
    set .exp_gain, 3;
    end;
}
