-|script|summon-snakes|32767
{
    end;

OnCast:
    if(call("magic_checks")) end;
    if (Sp < 40) end;
    if (getskilllv(SKILL_MAGIC) < .level) end;
    if (getskilllv(.school) < .level) end;
    if (countitem("DarkCrystal") < 1 || countitem("SnakeEgg") < 1) end;
    if (OrumQuest <= 40) end;
    if (getmap() == "033-1") goto L_SpecialRules6;
    goto L_Pay;

L_Pay:
    delitem "DarkCrystal", 1;
    delitem "SnakeEgg", 1;
    set @_M_BLOCK, 1; // block casting, until the timer clears it
    addtimer 15000, "Magic Timer::OnClear"; // set the new debuff
    sc_start SC_COOLDOWN, 15000, 0, BL_ID;
    callfunc "adjust_spellpower";
    set Sp, Sp - 40;
    set CASTS, CASTS + 1;
    if (CASTS < 0) set CASTS, 1; // overflow
    misceffect FX_MAGIC_SNAKE_CAST, strcharinfo(0);
    callfunc "magic_exp";
    set .@puppet$, "#"+strnpcinfo(0)+"#"+BL_ID;
    set .@puppet, puppet(getmap(), POS_X, POS_Y, .@puppet$, 127);
    if (.@puppet < 1) end;
    set .count, (@spellpower/300)+1, .@puppet;
    set .master, BL_ID, .@puppet;
    set .lifetime, @spellpower*80, .@puppet;
    addnpctimer 4000-(@spellpower*9), .@puppet$+"::OnSummon";
    addnpctimer 6000, .@puppet$+"::OnDestroy";
    end;

OnSummon:
    if (!(isloggedin(.master))) destroy;
    if(get(Hp, .master) < 1) destroy; // destroy if master is missing
    if(getmap(.master) != strnpcinfo(3)) destroy; // destroy if master left the map
    specialeffect FX_MAGIC_SNAKE_SPAWN;
    set .@i, 0;
    set .@x, getnpcx();
    set .@y, getnpcy();
    set .@map$, strnpcinfo(3);
    callsub S_SummonAll;
    end;

OnDestroy:
    destroy;

S_SummonAll:
    set .@sn, rand(0, 3);

    if   (.@sn == 0) summon .@map$, rand(.@x-2,.@x+2), rand(.@y-2,.@y+2), .master, "Cave Snake Summon", CaveSnake, 2, .lifetime;
    elif (.@sn == 1) summon .@map$, rand(.@x-2,.@x+2), rand(.@y-2,.@y+2), .master, "Snake Summon", Snake, 2, .lifetime;
    elif (.@sn == 2) summon .@map$, rand(.@x-2,.@x+2), rand(.@y-2,.@y+2), .master, "Mountain Snake Summon", MountainSnake, 2, .lifetime;
    elif (.@sn == 3) summon .@map$, rand(.@x-2,.@x+2), rand(.@y-2,.@y+2), .master, "Grass Snake Summon", GrassSnake, 2, .lifetime;
    set .@i, .@i + 1;
    if (.@i < .count) goto S_SummonAll;
    destroy;

L_SpecialRules6:
    if ($@KIMARR_EVENT < 1) goto L_Pay;
    if ($@Fluffy_FighterID == BL_ID) goto L_Pay;
    message strcharinfo(0), "You can't summon here unless hunting fluffies.";
    end;

OnInit:
    set .school, SKILL_MAGIC_DARK;
    set .invocation$, chr(MAGIC_SYMBOL) + "halhiss"; // used in npcs that refer to this spell
    void call("magic_register", "OnCast");
    set .level, 2;
    set .exp_gain, 3;
    end;
}
