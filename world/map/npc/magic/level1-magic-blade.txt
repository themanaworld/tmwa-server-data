-|script|magic-blade|32767
{
    if(call("magic_checks")) end;
    if (Sp < 9) end;
    set .@level, getskilllv(.school);
    if (getskilllv(SKILL_MAGIC) < .level) end;
    if   (.@level <= 2 && countitem("SharpKnife") >= 1) set .@component$, "SharpKnife";
    elif (.@level <= 2 && countitem("Knife") >= 1)      set .@component$, "Knife";
    elif (.@level <= 2) end;
    if (.@component$ != "") delitem .@component$, 1;
    set @_M_BLOCK, 1; // block casting, until the timer clears it
    addtimer 500, "Magic Timer::OnClear"; // set the new debuff
    callfunc "adjust_spellpower";
    set Sp, Sp - 9;
    misceffect FX_MAGIC_BLACK, strcharinfo(0);
    setarray @chizaspell[0],
        if_then_else(.@component$ == "Knife", 40, 60), // dmg
        Str, // do not allow to equip light armor, cast, and then switch to heavy armor to get bonus str
        (@spellpower/15) + 10, // charges
        (((200 - Agi) * 1200) / 200), // delay
        @spellpower;

    callfunc "magic_exp";
    goto L_FreeRecast;

OnDischarge:
    if (@chizaspell[2] < 1) end;
    set @chizaspell[2], 0;
    misceffect FX_FIRE_BURST, strcharinfo(0);
    overrideattack;
    end;

OnAttack:
    if (target(BL_ID, @target_id, 22) != 22) goto L_FreeRecast; // 0x10 | 0x02 | 0x04
    void call("melee_damage", @chizaspell[4], @target_id, (@chizaspell[0] + rand(@chizaspell[1] + 5)));
    set @chizaspell[2], @chizaspell[2] - 1;
    goto L_FreeRecast;

L_FreeRecast:
    if (@chizaspell[2] > 0)
        addtimer 0, strnpcinfo(0) + "::OnSetRecast";
    end;

OnSetRecast:
    overrideattack @chizaspell[3], 1, ATTACK_ICON_GENERIC, 30, strnpcinfo(0)+"::OnAttack";
    end;

OnInit:
    set .school, SKILL_MAGIC_WAR;
    set .invocation$, chr(MAGIC_SYMBOL) + "chiza"; // used in npcs that refer to this spell
    void call("magic_register");
    set .level, 1;
    set .exp_gain, 1;
    end;
}
