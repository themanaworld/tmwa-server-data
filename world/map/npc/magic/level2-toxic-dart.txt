-|script|toxic-dart|32767
{
    if(call("magic_checks")) end;
    if (Sp < 15) end;
    set .@level, getskilllv(.school);
    if (.@level < .level) end;
    if (OrumQuest <= 37) end;
    if (.@level <= 2 && countitem("Root") >= 2) delitem "Root", 2;
    elif (.@level <= 2) end;
    set @_M_BLOCK, 1; // block casting, until the timer clears it
    addtimer 500, "Magic Timer::OnClear"; // set the new debuff
    callfunc "adjust_spellpower";
    set Sp, Sp - 15;
    misceffect FX_MAGIC_DARKRED, strcharinfo(0);
    setarray @phlexspell[0],
        (sqrt(@spellpower) * 5), // elt damage
        ((BaseLevel/3) + 5), // elt damage bonus
        (((200 - Agi) * 1200) / 200), // delay
        ((@spellpower/75) + 3), // charges
        (5000+(@spellpower*1200)), // poison duration
        (max(15,@spellpower/15)+5); // poison strength

    callfunc "magic_exp";
    goto L_FreeRecast;

OnAttack:
    if (target(BL_ID, @target_id, 50) != 50) goto L_FreeRecast; // 0x20 | 0x02 | 0x10
    misceffect FX_MAGIC_DARKRED, strcharinfo(0);
    void call("elt_damage", @phlexspell[0], @phlexspell[1], ELT_NEUTRAL, ELT_POISON, FX_FIRE_BURST);
    if(@target_id != BL_ID && isloggedin(@target_id)) // this is a dirty trick to check if the target is a player
        sc_start sc_poison, @phlexspell[4], @phlexspell[5], @target_id;
    set @phlexspell[3], @phlexspell[3] - 1;
    goto L_FreeRecast;

L_FreeRecast:
    if (@phlexspell[3] > 0)
        addtimer 0, strnpcinfo(0) + "::OnSetRecast";
    end;

OnDischarge:
    if (@phlexspell[3] < 1) end;
    set @phlexspell[3], 0;
    misceffect FX_FIRE_BURST, strcharinfo(0);
    overrideattack;
    end;

OnSetRecast:
    overrideattack @phlexspell[2], 4, ATTACK_ICON_GENERIC, 31, strnpcinfo(0)+"::OnAttack";
    end;

OnInit:
    set .school, SKILL_MAGIC_DARK;
    set .invocation$, chr(MAGIC_SYMBOL) + "phlex"; // used in npcs that refer to this spell
    void call("magic_register");
    set .level, 2;
    set .exp_gain, 3;
    end;
}
