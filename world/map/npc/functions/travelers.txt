// Travelers
// Authors: Wombat, Wushin

// Multiple Travelers
// Cannot access until found (Player Bitmask)
// Costs Zeny

// Contains all warp points to find
-|script|Traveler|32767
{
    explode .@n$, strnpcinfo(0), "#";
    set @npcname$, .@n$[0];
    set @NpcTravelBit, (1 << $@NextLocationBit[.@n$[1]]);
    set @Cost, 10;
    if (BaseLevel < 45)
        set @Cost, 5;

    mes "["+@npcname$+"]";
    mes "\"Greetings. I am "+@npcname$+" the Traveler.\"";
    next;

    if (TravelFound & $@tut_bit)
        goto L_Main;
    goto L_TravelTut;

L_Main:
    if (TravelFound & @NpcTravelBit)
        goto L_Start;
    goto L_SetTravelBit;

L_TravelTut:
    mes "["+@npcname$+"]";
    mes "\"We travelers are found all over the world. Once you have found a traveler at a certain location, you can be sent back there instantly by another traveler.\"";
    next;
    if (TravelFound & $@tut_bit)
        goto L_Main;
    goto L_SetBit;

L_SetBit:
    set TravelFound, TravelFound | $@tut_bit;
    goto L_Main;

L_SetTravelBit:
    mes "["+@npcname$+"]";
    mes "\"Uplink set. You can now return to this spot for a fee.\"";
    next;
    set TravelFound, TravelFound | @NpcTravelBit;
    goto L_Start;

L_Start:
    mes "\"Where would you like to go?\"";
    menu
        $@NextLocationNames$[0] + " (" + (@Cost * $@NextLocationCost[0]) + " GP)", L_TravelSelection,
        $@NextLocationNames$[1] + " (" + (@Cost * $@NextLocationCost[1]) + " GP)", L_TravelSelection,
        $@NextLocationNames$[2] + " (" + (@Cost * $@NextLocationCost[2]) + " GP)", L_TravelSelection,
        $@NextLocationNames$[3] + " (" + (@Cost * $@NextLocationCost[3]) + " GP)", L_TravelSelection,
        $@NextLocationNames$[4] + " (" + (@Cost * $@NextLocationCost[4]) + " GP)", L_TravelSelection,
        $@NextLocationNames$[5] + " (" + (@Cost * $@NextLocationCost[5]) + " GP)", L_TravelSelection,
        $@NextLocationNames$[6] + " (" + (@Cost * $@NextLocationCost[6]) + " GP)", L_TravelSelection,
        $@NextLocationNames$[7] + " (" + (@Cost * $@NextLocationCost[7]) + " GP)", L_TravelSelection,
        $@NextLocationNames$[8] + " (" + (@Cost * $@NextLocationCost[8]) + " GP)", L_TravelSelection,
        $@NextLocationNames$[9] + " (" + (@Cost * $@NextLocationCost[9]) + " GP)", L_TravelSelection,
        $@NextLocationNames$[10] + " (" + (@Cost * $@NextLocationCost[10]) + " GP)", L_TravelSelection,
        "Soul Menhir (" + (@Cost * 100) + ") GP", L_SoulMenhir,
        "Who are the Travelers?", L_TravelTut,
        "I'm not interested.", L_TravelNo;

L_SoulMenhir:
    set @NextLocationCost, (@Cost * 100);
    set @NextLocationMap$, getsavepoint(0);
    set @NextLocationX, getsavepoint(1);
    set @NextLocationY, getsavepoint(2);
    goto L_TravelPlayer;

L_TravelSelection:
    set @menu, @menu - 1;
    set @NextLocationCost, (@Cost * $@NextLocationCost[@menu]);
    set @NextLocationBit, (1 << $@NextLocationBit[@menu]);
    set @NextLocationMap$, $@NextLocationMap$[@menu];
    set @NextLocationX, $@NextLocationX[@menu];
    set @NextLocationY, $@NextLocationY[@menu];
    goto L_TravelChecks;

L_TravelChecks:
    if (@NpcTravelBit == @NextLocationBit)
        goto L_AlreadyThere;
    if (!(TravelFound & @NextLocationBit))
        goto L_NoFound;
    if (Zeny < @NextLocationCost)
        goto L_NoMoney;
    goto L_TravelPlayer;

L_TravelPlayer:
    if(QUEST_MIRIAM_start != 0) 
        goto S_Cheat;
    mes "["+@npcname$+"]";
    mes "\"Be fearless!\"";
    set Zeny, Zeny - @NextLocationCost;
    warp @NextLocationMap$,@NextLocationX,@NextLocationY;
    goto L_Clearvars;

L_TravelNo:
    mes "["+@npcname$+"]";
    mes "\"Perhaps you will have the courage to help us some day.\"";
    goto L_Clearvars;

L_NoMoney:
    mes "["+@npcname$+"]";
    mes "\"I'm sorry, but you don't have enough money. Maybe next time.\"";
    goto L_Clearvars;

L_NoFound:
    mes "["+@npcname$+"]";
    mes "\"Sorry, but you haven't visited a traveler yet at that location. You should find and talk to a traveler there so you can quickly return to that location in the future.\"";
    goto L_Clearvars;

L_AlreadyThere:
    mes "["+@npcname$+"]";
    mes "\"Uh... You're already here. Are you sure you know where you are going?\"";
    goto L_Clearvars;

L_Clearvars:
    set @npcname$, "";
    set @Cost, 0;
    set @menu, 0;
    set @NextLocationCost, 0;
    set @NextLocationBit, 0;
    set @NextLocationMap$, "";
    set @NextLocationX, 0;
    set @NextLocationY, 0;
    close;

OnUse:
    set @seconds, TowelLastUsed - (gettimetick(2) - 1200);
    if (@seconds > 0)
        goto L_DontPanic;
    if (isin("botcheck",25,27,51,47))
        goto L_Prison;
    if (getmapflag(getmap(), MF_NOSAVE) || getmapflag(getmap(), MF_NOTELEPORT) || getmapflag(getmap(), MF_NOWARP) || isin("009-7",$@fightclub_x1,$@fightclub_y1,$@fightclub_x2,$@fightclub_y2))
        goto L_Forbid;

    mes "\"Where would you like to go?\"";
    menu
        $@NextLocationNames$[0], L_SetWarpPlayer,
        $@NextLocationNames$[1], L_SetWarpPlayer,
        $@NextLocationNames$[2], L_SetWarpPlayer,
        $@NextLocationNames$[3], L_SetWarpPlayer,
        $@NextLocationNames$[4], L_SetWarpPlayer,
        $@NextLocationNames$[5], L_SetWarpPlayer,
        $@NextLocationNames$[6], L_SetWarpPlayer,
        $@NextLocationNames$[7], L_SetWarpPlayer,
        $@NextLocationNames$[8], L_SetWarpPlayer,
        $@NextLocationNames$[9], L_SetWarpPlayer,
        $@NextLocationNames$[10], L_SetWarpPlayer,
        "Soul Menhir", L_SetWarpPlayerMenhir,
        "Nevermind.", L_Clearvars;

L_SetWarpPlayer:
    set @menu, @menu - 1;
    set @NextLocationBit, (1 << $@NextLocationBit[@menu]);
    set @NextLocationMap$, $@NextLocationMap$[@menu];
    set @NextLocationX, $@NextLocationX[@menu];
    set @NextLocationY, $@NextLocationY[@menu];

    if (!(TravelFound & @NextLocationBit))
        goto L_TowelNoFound;
    goto L_WarpPlayer;

L_SetWarpPlayerMenhir:
    set @NextLocationMap$, getsavepoint(0);
    set @NextLocationX, getsavepoint(1);
    set @NextLocationY, getsavepoint(2);
    goto L_WarpPlayer;

L_WarpPlayer:
    if(QUEST_MIRIAM_start != 0) 
        goto S_Cheat;
    set TowelLastUsed, gettimetick(2);
    warp @NextLocationMap$,@NextLocationX,@NextLocationY;
    goto L_Clearvars;

L_TowelNoFound:
    message strcharinfo(0), "Sorry, but you haven't visited a traveler yet at that location. You should find and talk to a traveler there so you can quickly return to that location in the future.";
    close2;
    goto L_Clearvars;

L_Forbid:
    message strcharinfo(0), "Towel : This area is protected by a force that doesn't tolerate the power of the Towel.";
    goto L_Clearvars;

L_Prison:
    message strcharinfo(0), "Towel : You must be warped by a GM to leave the botcheck area.";
    goto L_Clearvars;

L_DontPanic:
    callfunc "HumanTime";
    message strcharinfo(0), "Towel : Your towel is still too low on power to jump again. Try again in ##B"+ @time$ + "##b.";
    goto L_Clearvars;

S_Cheat:
    set QUEST_MIRIAM_cheat, 1;
    set QUEST_MIRIAM_run, gettimetick(2) - QUEST_MIRIAM_start;
    set QUEST_MIRIAM_start, 0;
    return;

OnInit:
    // TravelFound
    set $@tut_bit, (1 << 2);
    setarray $@NextLocationNames$[0], "Tonori - Tulimshar", "Argeas - Hurnscald", "Kaizei - Nivalis", "Tonori - Pachua's Village", "Argeas - Candor", "Argeas - Magic House", "Argeas - Farmsteads", "Argeas - Graveyard", "Argeas - Terranite Cave", "Kaizei - Barbarian Village", "Kaizei - Sage Nikolai's Mansion";
    setarray $@NextLocationBit[0], 7, 9, 10, 12, 16, 5, 15, 4, 6, 14, 8;
    setarray $@NextLocationCost[0], 100, 100, 100, 150, 200, 150, 150, 200, 200, 150, 200;
    setarray $@NextLocationMap$[0], "002-1", "008-1", "020-1", "006-1", "029-1", "013-1", "055-1", "026-1", "012-3", "033-1", "048-2";
    setarray $@NextLocationX[0], 60, 79, 53, 28, 69, 120, 135, 49, 445, 66, 26;
    setarray $@NextLocationY[0], 42, 84, 122, 97, 69, 93, 60, 45, 65, 33, 47;

    void
        puppet("002-1", 60, 41, "Nina the Traveler#0", 103),
        puppet("008-1", 81, 82, "Diryn the Traveler#1", 103),
        puppet("020-1", 57, 123, "Knox the Traveler#2", 103),
        puppet("006-1", 25, 95, "Rhutan the Traveler#3", 103),
        puppet("029-1", 69, 68, "Knitra the Traveler#4", 103),
        puppet("013-1", 120, 90, "Faris the Traveler#5", 103),
        puppet("055-1", 135, 60, "Sherman the Traveler#6", 103),
        puppet("026-1", 49, 46, "Styx the Traveler#7", 103),
        puppet("012-3", 439, 62, "Meridith the Traveleri#8", 103),
        puppet("033-1", 63, 30, "Khoenan the Traveler#9", 103),
        puppet("048-2", 23, 46, "Drake the Traveler#10", 103);
    end;
}
