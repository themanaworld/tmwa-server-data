//

function|script|SlotMachine
{
    mes "Pull the lever...";
    next;
    menu
        "Pull", L_Play,
        "Maybe later", L_Close;

L_Play:
    if(countitem("CasinoCoins") < 1)
        goto L_NoCoin;
    delitem "CasinoCoins", 1;
    set @Temp1,rand(7);
    set @Temp2,rand(7);
    set @Temp3,rand(7);
    mes "Numbers: " + @Temp1 + "/" + @Temp2 + "/" + @Temp3 + ".";
    next;

    if(@Temp1 != @Temp2)
        goto L_Lost;
    if(@Temp2 != @Temp3)
        goto L_Lost;
    if(@Temp1 != @Temp3)
        goto L_Lost;
    if (CSN >= 9)
        goto L_Default;
    if (rand(6) < CSN)
        goto L_Jackpot;
    set CSN, CSN+1;
    goto L_Default;

L_Jackpot:
    mes "Congratulations! You won!";
    mes "However, the slot machine";
    mes "do not give you the coins!";
    next;
    mes "[Staff]";
    mes "\"I apologize for this problem.";
    mes "I see you are a huge client of";
    mes "ours, so I'll give you a Monocle";
    mes "as a token of apology.\"";
    getitem "Monocle", 1;
    set CSN, 9;
    goto L_Close;

L_Default:
    mes "Congratulations! You won!";
    mes "You get 10 casino coins";
    getitem "CasinoCoins", 10;
    goto L_Close;

L_Lost:
    mes "You lost!";
    goto L_Close;

L_NoCoin:
    mes "Insert coin";
    goto L_Close;

L_Close:
    close2;
    return;
}
