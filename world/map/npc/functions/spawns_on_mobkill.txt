// Spawns some mobs on death of some other mob. Say split slime -> few smaller ones
// This function meant to be called with player RID attached (usually in OnMobKillEvent)
// Inputs: nothing, but expects @mobID, @mobX and @mobY set (usually by server)
// Return: nothing, but spawns few things.
// TODO: Add few big slime mobs and "split" these (this needs big slime GFX and making mob).
function|script|spawns_on_mobkill
{
    if (@mobID == SeaSlimeMother) goto L_SplitSea;
    if (@mobID == GreenSlimeMother) goto L_SplitGreen;
    if (@mobID == Tormenta) goto L_TorWitchDead;
    if (@mobID == Luvia) goto L_LuvWitchDead;
    return;

L_SplitSea:
    void call("spawn_mobs_around", getmap(), @mobX, @mobY, AngrySeaSlime, rand(8, 20));
    return;

L_SplitGreen:
    void call("spawn_mobs_around", getmap(), @mobX, @mobY, AngryGreenSlime, rand(8, 20));
    return;

L_TorWitchDead:
    void call("spawn_mobs_around", getmap(), @mobX, @mobY, VoidBat, rand(7, 12));
    void call("spawn_mobs_around", getmap(), @mobX, @mobY, DemonicSpirit, rand(5, 10));
    void call("spawn_mobs_around", getmap(), @mobX, @mobY, UndeadWitch, 1);
    return;

L_LuvWitchDead:
    void call("spawn_mobs_around", getmap(), @mobX, @mobY, "", VoidBat, rand(6, 10));
    void call("spawn_mobs_around", getmap(), @mobX, @mobY, "", DemonicSpirit, rand(4, 8));
    return;
}

// Spawns mobs around spot, if it can - or stacks mobs on spot if no room for 3x3 area
// This function can be called from any context.
// Inputs: arg0: map (string), arg1: X, arg2: Y, arg3: mob ID, ard4: amount
// Return: nothing, but spawns few things.
function|script|spawn_mobs_around
{
    set .@map$,   getarg(0, "");
    set .@mobX,   getarg(1, -1);
    set .@mobY,   getarg(2, -1);
    set .@mobID,  getarg(3, -1);
    set .@mobQTY, getarg(4, -1);
    if ((.@map$ == "") || (.@mobX < 1) || (.@mobY < 1) || (.@mobID < 1002) ||
        (.@mobX > getmapmaxx(.@map$))  || (.@mobX > getmapmaxy(.@map$))    ||
        (.@mobQTY < 1)) goto L_Abort;
    if ((.@mobX > 1) && (.@mobY > 1) && (.@mobX < getmapmaxx(.@map$)) && (.@mobY < getmapmaxy(.@map$)))
        areamonster .@map$, (.@mobX-1), (.@mobY-1), (.@mobX+1), (.@mobY+1), "", .@mobID, .@mobQTY; // Enough room for 3x3
    else
        monster .@map$, .@mobX, .@mobY, "", .@mobID, .@mobQTY; // 3x3 wouldnt fit -> use spot.
    return;

L_Abort:
    debugmes "spawn_mob_around: invalid args! Map=" + .@map$ + " x=" + .@mobX + " y=" + .@mobY + " mobID=" + .@mobID + " mobQTY=" + .@mobQTY;
    return;
}
