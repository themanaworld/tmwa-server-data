function|script|EquipHandler
{
    goto L_FightClub;

L_FightClub:
    if((@Duel_Fighter != 1) || (@iskillable != 1)) goto L_Return;
    if(isin("009-7.gat", $@fightclub_x1, $@fightclub_y1, $@fightclub_x2, $@fightclub_y2) == 0) goto L_Return;
    gmcommand "@killable"; // equiping an item undo @killer and @killable so we need to re-call it
    gmcommand "@killer";
    message strcharinfo(0), " "; // do not send the "you be a killa" message
    goto L_Return;

// Add other checks here

L_Return:
    return;
}

017-9.gat,0,0,0|script|#GlobalHandler|-1
{
    end;

OnPCLoginEvent: // this does not work yet but as soon as it is implemented it will start working
    callfunc "ClearVariables";
    end;

OnPCKillEvent:
    set Death_Kill, Death_Kill + 1; // this counts the number of players you have killed
    end;

OnPCKilledEvent:
    goto L_Fightclub_Killed; // this is used by the 1v1 arena

OnPCDieEvent:
    set @iskillable, 0;
    callfunc "fightclub_GoBack"; // this used by the battle master
    goto L_Fightclub_Die; // this is used by the 1v1 arena

// custom handlers below

L_Fightclub_Killed:
    set @iskillable, 0;
    set @duel_killer, 0;
    if(@Duel_Fighter != 1) goto L_End;
    if(getmap() != "009-7") goto L_End;
    set $@duel_loser, getcharid(3); // grab the rid of the victim
    set $@duel_winner, @killerrid; // grab the rid of the killer
    set @killerrid, 0; // reset killerid
    callfunc "fightclub_death";
    goto L_End;

L_Fightclub_Die:
    set @duel_killer, 0;
    if(@Duel_Fighter != 1) goto L_End;
    if(getmap() != "009-7") goto L_End;
    set @killerrid, 0; // since the player was not murdered, the killerrid is 0
    set $@duel_loser, getcharid(3); // grab the rid of the victim
    callfunc "fightclub_death";
    goto L_End;

L_End:
    end;
}
