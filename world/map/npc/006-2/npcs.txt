// Pachua's Tribe

006-2,49,25,0|script|Falkurn|178
{
    set @run, ((QUEST_NorthTulimshar & TWOBIT_8_MASK) >> TWOBIT_8_SHIFT);
    if ((@run == 1) && (QL_KYLIAN == 13))
        goto L_Deliver;
    goto L_Start;

L_Deliver:
    menu
        "I have a delivery for you", L_Delivery;

L_Delivery:
    mes "[Falkurn]";
    mes "\"Good now we can get to work on making some more leather goods.\"";
    set QL_KYLIAN, 14;
    goto L_Close;

L_Start:
    mes "[Falkurn]";
    mes "\"Greetings traveler, what wind brings you to our door?\"";
    menu
        "I'm looking for exotic goods.", L_Shops,
        "I need some leatherwork done.", L_Pachua,
        "Just Looking Around.", L_Close;

L_Shops:
    mes "\"My Wife, Ardra, tends the Kitchen see her for food and drink.\"";
    mes "\"She can be found in the West Cave.\"";
    next;
    mes "\"Reathe trades many of our leather goods to exotic lands.\"";
    mes "\"She always has the most interesting finds.\"";
    mes "\"She can be found in the East Cave.\"";
    goto L_Close;

L_Pachua:
    mes "\"Our Chief can be found by going through the southern most cave.\"";
    mes "\"He can help you with any leatherwork you may need done.\"";
    goto L_Close;

L_Close:
    close;
}

006-2,80,25,0|script|Darug|178
{
    mes "[Darug]";
    mes "\"We are the all thats left of the natives that used to wander the plains.\"";
    mes "\"Years of war between the various 'Civilized' nations drained the natural resources.\"";
    next;
    mes "\"The waters have begun to run dry.\"";
    mes "\"All manner of beasts are getting more agressive.\"";
    mes "\"The Mana has been poison by Dark Magic.\"";
    next;
    mes "\"Even now, I sense a evil presense nearby.\"";
    mes "\"I can feel it lying deep underground.\"";
    mes "\"Gnawing at the underbelly of Tonori.\"";
    next;
    mes "\"We believe it's one of the evils Nu'rem let loose to destroy The Mana\"";
    mes "\"We have been searching, but have not been able to locate the source.\"";
    mes "\"Maybe our scouts will return soon with the answers.\"";
    close;
}

006-2,34,64,0|script|Meluna|179
{
    mes "[Meluna]";
    mes "\"Greetings traveler, how blows the wind?\"";
    goto L_Main;

L_Main:
    menu
        "Um... What?", L_Explain,
        "Gentle.", L_Heal,
        "Strong.", L_Close;

L_Explain:
    mes "\"Ahh sorry, you are not familiar with our customs.\"";
    mes "\"The Mana was made into the 4 main elements by The Gods; Jande made Fire, Nu'rem made Earth, Tal made Air, and, Di'tal made Water.\"";
    mes "\"It was Tal that gives us our first and last breathe.\"";
    next;
    mes "\"When we ask you how 'blows the wind?'\"";
    mes "\"We mean how are your feeling?\"";
    next;
    mes "\"We typically respond with a strength of wind; normally Gentle or Strong\"";
    goto L_Main;

L_Heal:
    if (BaseLevel > 70) 
        goto L_NoHeal;
    heal 1000,100;
    goto L_Close;

L_NoHeal:
    mes "\"I already sense the power of the Gods carrying you.\"";
    mes "\"Any wounds you have are beyond my power to fix.\"";
    goto L_Close;

L_Close:
    mes "\"May Tal carry you on their back.\"";
    close;
}
