// Doctor

009-2.gat,148,25,0|script|Doctor|107,{
    callfunc "ClearVariables";

    set @inspector, ((QUEST_Hurnscald & NIBBLE_3_MASK) >> NIBBLE_3_SHIFT);

    if (getequipid(equip_head) == 616) goto L_Axe;
    if (getequipid(equip_head) == 621) goto L_Eyepatch;

    mes "[Doctor]";
    mes "\"Hello, can I help you?\"";
    next;

    if (@inspector == 1)
        menu
            "I think I am sick!", L_Cure,
            "No, I feel fine.", -,
            "Have you seen anything strange in town? Anything that might have to do with the robberies?", L_NohMask;
    if (@inspector != 1)
        menu
            "I think I am sick!", L_Cure,
            "No, I feel fine.", -;

    mes "[Doctor]";
    mes "\"Then please stop wasting my precious time.\"";
    goto L_Close;

L_Cure:
    mes "[Doctor]";
    if (sc_check(sc_poison))
        goto L_CurePoison;
    mes "The doctor examines you briefly.";
    mes "\"Nonsense!  You look fine and dandy to me.  All you need is a bit more exercise and fresh fruit in your diet!\"";
    goto L_Close;

L_CurePoison:
    mes "\"Well, well, well!  Look at all those green bubbles coming out of your head; that looks like poisoning to me!  Did you eat something rotten?\"";
    next;
    mes "[Doctor]";
    mes "The doctor pulls out a syringe and fills it with a white liquid from a jar on his desk.";
    mes "\"Now hold still, this won't hurt a bit...\"";
    next;
    mes "[Doctor]";
    sc_end(sc_poison);
    sc_end(sc_slowpoison);
    mes "*Ouch!*";
    mes "\"Next time, be more careful!  Make sure to cook any meats before you eat them, and don't eat fish once it starts to smell.\"";
    goto L_Close;

L_Axe:
    mes "[Doctor]";
    mes "\"Oh my, what happened to you?\"";
    next;
    mes "[Doctor]";
    mes "\"Wait. Thats just a fake. Shame on you!\"";
    goto L_Close;

L_Eyepatch:
    mes "[Doctor]";
    mes "\"Would you like a glass eye to replace that eye patch you have? We just got a whole load of them in today. I'll even let you keep the patch as a souvenir.\"";
    next;
    menu
        "Yes, please.", L_Eyepatch_GlassEye,
        "No thank you", -;
    mes "[Doctor]";
    mes "\"If you change your mind, please come back and see me.\"";
    goto L_Close;

L_Eyepatch_GlassEye:
    mes "[Doctor]";
    mes "\"Now, where did I put that box of eyes...\"";
    mes "He goes off to look for them and comes back empty handed.";
    next;
    mes "[Doctor]";
    mes "\"I can't seem to find where I put that box. You should come back later, I may have found them by then.\"";
    goto L_Close;

L_NohMask:
    mes "[Doctor]";
    mes "\"No, I haven't seen anything.\"";
    goto L_Close;

L_Close:
    set @inspector, 0;
    close;
}
