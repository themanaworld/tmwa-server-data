//Inspector, source of the Quest "Robberies In Hurnscald"
//This is very old, and the author is unknown. There's also no follow-up.
//
//questline -> CommonVar:
//  inspector -> 1
//  old woman -> 2
//  old woman (uniform) -> 3
//  inspector -> 4
//  troupe leader -> 5
//  inspector -> 6
//  old man -> 7
//  old woman -> 8
//  inspector -> 9
//  old woman (uniform) -> 10
//  hinnak -> 11
//  books -> 12
//  inspector -> 13
//  troupe leader -> 14
//  inspector -> 15 (end)

009-2,24,99,0|script|Inspector#Hurnscald|150
{
    if (QL_INSPECTOR == 0 && BaseLevel >= 30) goto L_Start;
    if (QL_INSPECTOR == 1) goto L_AskVillage;
    if (QL_INSPECTOR == 2) goto L_OldWoman;
    if (QL_INSPECTOR == 3) goto L_TheaterMask;
    if (QL_INSPECTOR == 4) goto L_TravelingTroupe;
    if (QL_INSPECTOR == 5) goto L_OldMan;
    if (QL_INSPECTOR == 6) goto L_OldMan_2;
    if (QL_INSPECTOR == 7) goto L_Alibi;
    if (QL_INSPECTOR == 8) goto L_Alibi_2;
    if (QL_INSPECTOR == 9) goto L_Alibi_3;
    if (QL_INSPECTOR == 10) goto L_Satchel;
    if (QL_INSPECTOR == 11) goto L_Basement;
    if (QL_INSPECTOR == 12) goto L_Note;
    if (QL_INSPECTOR == 13) goto L_TravelingTroupe_2;
    if (QL_INSPECTOR == 14) goto L_Over;
    if (QL_INSPECTOR == 15) goto L_End;
    mes "[Inspector]";
    mes "\"I'm sorry, but I'm busy looking into this string of robberies.\"";
    goto L_Close;

L_Start:
    mes "\"Hmm...what to do.\"";
    mes "He looks up and into your face.";
    next;
    mes "[Inspector]";
    mes "\"You look capable, will you help me solve these robberies?\"";
    next;
    menu
        "Yes.", L_Accept,
        "No.", L_Close;

L_Accept:
    set QL_INSPECTOR, 1;
    mes "[Inspector]";
    mes "\"Ok then. I'd like you to ask the villagers about the recent string of robberies.\"";
    goto L_Close;

L_AskVillage:
    mes "[Inspector]";
    mes "\"Please continue talking to the villagers.\"";
    goto L_Close;

L_OldWoman:
    mes "[Inspector]";
    mes "\"Hm...I don't know if I trust her eyesight or memory. See if someone else knows anything.\"";
    goto L_Close;

L_TheaterMask:
    set QL_INSPECTOR, 4;
    mes "[Inspector]";
    mes "\"Someone in a theater mask, eh? There was a traveling theater troupe in town recently, but they've moved on to Tulimshar. Please go talk to their leader about this.\"";
    goto L_Close;

L_TravelingTroupe:
    mes "[Inspector]";
    mes "\"Please go talk to the leader of the traveling troupe about the theater mask.\"";
    goto L_Close;

L_OldMan:
    set QL_INSPECTOR, 6;
    mes "[Inspector]";
    mes "\"Hm...an old man? Could you interrogate him for me?\"";
    goto L_Close;

L_OldMan_2:
    mes "[Inspector]";
    mes "\"Have you talked with the old man yet?\"";
    goto L_Close;

L_Alibi:
    mes "[Inspector]";
    mes "\"Can you verify that with his wife?\"";
    goto L_Close;

L_Alibi_2:
    set QL_INSPECTOR, 9;
    mes "[Inspector]";
    mes "\"Hm...then it couldn't be him. I'm not sure where to go from here, maybe you can find something else. Try talking to everyone again.\"";
    goto L_Close;

L_Alibi_3:
    mes "[Inspector]";
    mes "\"Made any progress yet?\"";
    goto L_Close;

L_Satchel:
    mes "[Inspector]";
    mes "\"Then go north and investigate!\"";
    goto L_Close;

L_Basement:
    mes "[Inspector]";
    mes "\"Did you look over the basement?\"";
    goto L_Close;

L_Note:
    set QL_INSPECTOR, 13;
    mes "[Inspector]";
    mes "\"What a strange note. I'll keep track of this, while you return the mask to the troupe.\"";
    mes "";
    mes "\"By the way, stay sharp! I may call upon you again.\"";
    goto L_Close;

L_TravelingTroupe_2:
    mes "[Inspector]";
    mes "\"Please return the mask to the traveling troupe.\"";
    goto L_Close;

L_Over:
    set QL_INSPECTOR, 15;
    mes "[Inspector]";
    mes "\"My men have found all of the stolen items. They were all in the mining camp. We still don't know who did it.\"";
    mes "[2500 experience points]";
    getexp 2500, 0;
    goto L_Close;

L_End:
    mes "[Inspector]";
    mes "\"Remember to stay sharp. I might need your help on another case.\"";
    goto L_Close;

L_Close:
    close;
}
