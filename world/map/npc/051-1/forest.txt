//-----------------------------------------------------------------------------
// Forest
// $@illia_progress = 1
//
// Level progress:
//     0: Did not start the task yet
//     1: Players are searching for the rock.
//     2: Players found the rock
//     3: Players unlocked the door
//-----------------------------------------------------------------------------

051-1.gat,64,46,0|script|Strange Stone|400,{
    set @step, 1;
    if ($@illia_progress < @step) goto L_ShouldNotBeHere;
    if (isin("051-1.gat", 65,45, 67,48) == 0) end;
    if (strcharinfo(0) != $@ILLIA_HERO$) goto L_HandleHelper;
    if ($@illia_level_1_progress == 0) goto L_GiveTask;
    if ($@illia_level_1_progress == 1) goto L_TaskNotComplete;
    if ($@illia_level_1_progress == 2) goto L_TaskComplete;
    if ($@illia_level_1_progress == 3) goto L_NothingElseToDo;
    end;

L_ShouldNotBeHere:
    percentheal -100, 0;
    end;

L_HandleHelper:
    mes "This stuff sounds complicated.";
    mes "I should ask " + $@ILLIA_HERO$ + " what to do.";
    close;

L_NothingElseToDo:
    mes "The door is opened... What am I waiting for?";
    close;

L_GiveTask:
    mes "You look at the rocks with attention.";
    mes "You are smart enough to notice that there seems to be a missing rock that could activate a mechanism.";
    next;
    menu
        "This rock must not be far... Let's look around.", -;
    set $@illia_level_1_progress, 1;
    set $@illia_level_1_num_deaths, 0;
    close;

L_TaskNotComplete:
    mes "It's not working. I have to find the rock.";
    mes "With all the slimes around here, maybe one absorbed it.";
    close;

L_TaskComplete:
    set $@illia_level_1_progress, 3;
    set $@illia_progress, 2;
    callfunc "UpdateIlliaProgress";
    set $@illia_max_time, $@illia_max_time + 900;
    mes "You insert the rock in the mechanism, then hear an unlock noise. The rocks move by themselves in a rumbling noise, making an opening in the wall.";
    close;

}

// Warp from the forest, to the cave
051-1.gat,66,44,0|script|#ToCave|32767,1,1,{
    set @step, 1;
    if ($@illia_progress < @step) goto L_ShouldNotBeHere;
    if ($@illia_level_1_progress < 2) goto L_Blocked;
    if ($@illia_level_1_progress == 2 && strcharinfo(0) == $@ILLIA_HERO$) goto L_SeeStone;
    if ($@illia_level_1_progress == 3) goto L_Warp;
    end;

L_ShouldNotBeHere:
    percentheal -100, 0;
    end;

L_Blocked:
    message strcharinfo(0), "There should be a way to open this... but not with my bare hands.";
    end;

L_SeeStone:
    message strcharinfo(0), "I should take a look at the stone.";
    end;

L_Warp:
    warp "051-3.gat", 24, 60;
    end;
}

