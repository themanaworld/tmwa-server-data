//

018-2.gat,50,24,0|script|Receptionist|108,{
    callfunc "ClearVariables";

    set @inspector, ((QUEST_Hurnscald & NIBBLE_3_MASK) >> NIBBLE_3_SHIFT);

    mes "[Receptionist]";
    mes "\"Welcome to the mining camp!";
    mes "Do you need anything?\"";
    next;

L_Menu:
    if (@inspector == 10)
        menu
            "Why was a mine setup here?", L_Mine,
            "Can I go into the mine?", L_Enter,
            "Did the company build this building?", L_Building,
            "Did you hear a commotion here the other night?", L_NohMask,
            "Thank you, no.", L_Close;
    if (@inspector != 10)
        menu
            "Why was a mine setup here?", L_Mine,
            "Can I go into the mine?", L_Enter,
            "Did the company build this building?", L_Building,
            "Thank you, no.", L_Close;
    goto L_Close;

L_Mine:
    mes "[Receptionist]";
    mes "\"Minerals have been easy to find in this area historically. The mine entrance we're using collapsed from the mountain side when the volcano errupted. Plenty of minerals were exposed then.\"";
    next;
    goto L_Question_End;

L_Enter:
    mes "[Receptionist]";
    mes "\"If you think you can handle all the monsters that have appeared there.\"";
    next;
    goto L_Question_End;

L_Building:
    mes "[Receptionist]";
    mes "\"No, the company bought this building. It's been abandoned for a while. The villagers think it's haunted, but we haven't seen anything, except for the basement. None of us have been able to stay there for very long.\"";
    next;
    menu
        "Why not?", -,
        "Not my problem", L_Question_End;
    mes "[Receptionist]";
    mes "\"I haven't been down there myself, and the ones who have haven't been able to describe why at all.\"";
    next;
    goto L_Question_End;

L_NohMask:
    mes "[Receptionist]";
    mes "\"I stay in town, and so couldn't have heard anything over here. One of the miners might be able to help you.\"";
    next;
    goto L_Question_End;

L_Question_End:
    mes "[Receptionist]";
    mes "\"Do you need anything else?\"";
    next;
    goto L_Menu;

L_Close:
    set @inspector, 0;
    close;
}
