//You can only enter the cat cave naked, otherwise the cat's reaction scares
//you away. You can also get in while wearing the cat ears.
//See katze.txt for more info

//This quest mainly uses QL_CAT_EARS, but @Hiss is also stored separately
//using the older bitshift technique, because I'm not going to dedicate two
//quest log indexes to one quest.

015-3,31,42,0|script|#CatCaveBarrier|45,1,1
{
    set @Hiss, ((QUEST_Katze & NIBBLE_1_MASK) >> NIBBLE_1_SHIFT);

    if (getequipid(equip_head) != 1217
        && (getequipid(equip_head) > 0
        || getequipid(equip_shield) > 0
        || getequipid(equip_hand1) > 0
        || getequipid(equip_hand2) > 0
        || getequipid(equip_gloves) > 0
        || getequipid(equip_shoes) > 0
        || getequipid(equip_misc1) > 0
        || getequipid(equip_misc2) > 0
        || getequipid(equip_torso) > 0
        || getequipid(equip_legs) > 0))
            goto L_Not_Naked;

    if (@Hiss != 2)
        mes "\"Hissss...\"";
    set @Hiss, 2;

    if (QL_CAT_EARS == 0) goto L_FirstEntry;

    goto L_Close;

L_Not_Naked:
    warp "015-1", 60, 32;
    set @Hiss, 1;
    mes "\"GRAAUWL! Hissss...\"";
    mes "Scared of what you hear, you flee back outside.";
    goto L_Close;

L_FirstEntry:
    //first successful entry enables the quest log entry
    if (QL_CAT_EARS == 0) set QL_CAT_EARS, 1;
    goto L_Close;

L_Close:
    set QUEST_Katze, (QUEST_Katze & ~(NIBBLE_1_MASK)) | (@Hiss << NIBBLE_1_SHIFT);
    set @Hiss, 0;
    close;
}
