//The pot where the player can deposit stuff for the cat.
//See katze.txt for more info

015-3,37,29,0|script|Pot|400
{
    if (QL_CAT_EARS < 3) mes "It's a pot.";
    if (QL_CAT_EARS >= 3) mes "It's that old pot again.";
    next;

    //handle each state
    if (QL_CAT_EARS == 8) goto L_HasWood; //leave to progress
    if (QL_CAT_EARS == 7) goto L_NeedsWood; //has asked

    if (QL_CAT_EARS == 5) goto L_NeedsFur;

    if (QL_CAT_EARS == 4) goto L_HasFood; //leave to progress
    if (QL_CAT_EARS == 3) goto L_NeedsFood;

    if (QL_CAT_EARS == 2) goto L_HasMilk; //leave to progress
    if (QL_CAT_EARS == 1) goto L_NeedsMilk;

    close;

L_NeedsMilk:
    if (countitem("Milk") > 0)
        menu
            "Pour in some milk.", L_GiveMilk,
            "Leave it alone.", L_Close;
    goto L_Close;

L_GiveMilk:
    delitem "Milk", 1;
    set QL_CAT_EARS, 2;
    goto L_Close;

L_HasMilk:
    mes "There is milk in it.";
    goto L_Close;

L_NeedsFood:
    mes "The milk is gone!";
    next;

    if (countitem("BirdLeg") > 0 && countitem("Steak") > 0)
        menu
            "Put in a bird leg.", L_GiveBird,
            "Put in a steak.", L_GiveSteak,
            "Leave it alone.", L_Close;
    if (countitem("BirdLeg") > 0 && countitem("Steak") == 0)
        menu
            "Put in a bird leg.", L_GiveBird,
            "Leave it alone.", L_Close;
    if (countitem("BirdLeg") == 0 && countitem("Steak") > 0)
        menu
            "Put in a steak.", L_GiveSteak,
            "Leave it alone.", L_Close;
    goto L_Close;

L_GiveBird:
    delitem "BirdLeg", 1;
    set QL_CAT_EARS, 4;
    goto L_Close;

L_GiveSteak:
    delitem "Steak", 1;
    set QL_CAT_EARS, 4;
    goto L_Close;

L_HasFood:
    mes "There is some food in it.";
    goto L_Close;

L_NeedsFur:
    mes "And it's empty!";
    next;
    if (countitem("WhiteFur") > 0)
        menu
            "Put a white fur next to the pot.", L_GiveFur,
            "Leave it alone.", L_Close;
    goto L_Close;

L_GiveFur:
    mes "You put down the fur, but the cat doesn't seem to take any notice. Maybe there's something else you could do. You pick the fur up again.";
    goto L_Close;

L_NeedsWood:
    if (countitem("RawLog") > 0)
        menu
            "Put a wooden log next to the pot.", L_GiveWood,
            "Leave it alone.", L_Close;
    goto L_Close;

L_GiveWood:
    delitem "RawLog", 1;
    set QL_CAT_EARS, 8;
    mes "You put the wooden log next to the pot. The cat eyes it suspiciously, but remains on her spot.";
    goto L_Close;

L_HasWood:
    mes "A wooden log is patiently lying next to it.";
    goto L_Close;

L_Close:
    close;
}
