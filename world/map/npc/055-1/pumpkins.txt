055-1,0,0,0|script|Pumpkin#0|1063
{
    callfunc "KillPumpkin";
    end;
OnTimer10000:
    callfunc "RepositionPumpkin";
    end;
OnInit:
    callfunc "RepositionPumpkin";
    end;
}

055-1,0,0,0|script|Pumpkin#1|1063
{
    callfunc "KillPumpkin";
    end;
OnTimer10000:
    callfunc "RepositionPumpkin";
    end;
OnInit:
    callfunc "RepositionPumpkin";
    end;
}

055-1,0,0,0|script|Pumpkin#2|1063
{
    callfunc "KillPumpkin";
    end;
OnTimer10000:
    callfunc "RepositionPumpkin";
    end;
OnInit:
    callfunc "RepositionPumpkin";
    end;
}

// add as many pumpkins as you want


function|script|RepositionPumpkin
{
    npcareawarp 46, 31, 137, 73, 1, strnpcinfo(0); // ==> set the rectangle area here
    fakenpcname strnpcinfo(0), strnpcinfo(0), 1063; // become visible once again
    stopnpctimer strnpcinfo(0);
    return;
}

function|script|KillPumpkin
{
    callfunc "PCtoNPCRange";
    getexp 17, 1;
    fakenpcname strnpcinfo(0), strnpcinfo(0), 32767; // disappear
    initnpctimer strnpcinfo(0); // start the respawn timer

    if (Quest_Halloween != 1 || !(gettime(6) == 11 && (gettime(5) >= 4 && gettime(5) <= 30)))
        goto L_HeavyPoison;
    if (@discover_poisonous_pumpkin == 0)
        message strcharinfo(0), "Yuke, this pumpkin released some ugly poison gas!";
    set @discover_poisonous_pumpkin, 1;
    misceffect FX_PUMPKIN_EXPLOSION, strcharinfo(0);
    heal -15, 0;
    sc_start sc_poison, 1, 4;
    if (rand(0,100) < 10)
        getitem "PumpkinSeeds", 1;

    if (countitem("PumpkinSeeds") >= $@halloween_num_seeds)
        goto L_EnoughSeeds;

    set @mobID, 1063;
    callfunc "MobPoints";
    return;

L_EnoughSeeds:
    message strcharinfo(0), $@halloween_num_seeds + " should be enough seeds for Oscar. And this pumpkin gas gets too bad to stand it any longer...";
    set Quest_Halloween, 2;
    // Reset this to get the message for the "heavy" poison
    set @discover_poisonous_pumpkin, 0;
    return;

L_HeavyPoison:
    if (@discover_poisonous_pumpkin == 0)
        goto L_HeavyPoison0;
    if (@discover_poisonous_pumpkin == 1)
        goto L_HeavyPoison1;
    if (@discover_poisonous_pumpkin > 1)
        goto L_HeavyPoison2;
    return;

L_HeavyPoison0:
    set @discover_poisonous_pumpkin, @discover_poisonous_pumpkin + 1;
    message strcharinfo(0), "Ouch, this pumpkin is totally poisonous! Let's get away from them!";
    misceffect FX_PUMPKIN_EXPLOSION, strcharinfo(0);
    heal -150,0;
    sc_start sc_poison, 1, 60;
    return;

L_HeavyPoison1:
    set @discover_poisonous_pumpkin, @discover_poisonous_pumpkin + 1;
    message strcharinfo(0), "This is really awful! I should not touch them!";
    misceffect FX_PUMPKIN_EXPLOSION, strcharinfo(0);
    heal -350,0;
    sc_start sc_poison, 1, 120;
    return;

L_HeavyPoison2:
    set @discover_poisonous_pumpkin, @discover_poisonous_pumpkin + 1;
    misceffect FX_PUMPKIN_EXPLOSION, strcharinfo(0);
    heal -Hp, 0;
    return;
}
