// author: Jenalya
// love triangle quest, involved npcs: Reid's Ghost, Hamond, Savaric, Aldred, Golbenez
// state 0: just talking, if lovers state is >= 2, go on
// state 1: ignores you until lovers state is >= 3, player can ask about the inn. needs if woman >= 7, lover >= 3 and husband >= 4 to go on
// state 2: shows reids memory if the player brings @ROTTENRAGS_AMOUNT rotten rags and an orange cake
// state 3: shows hamonds memory if the player brings @UNDEADEAR_AMOUNT undead ears and a chocolate cake
// state 4: shows savarics memory if the player brings @UNDEADEYE_AMOUNT undead eyes and an apple cake
// state 5: shows all memories again if asked for them. if @lover == 6, player can ask for savarics soul
// state 6: wants jackOsouls, random chance of 1/50 to success, small amount of exp for fail
// state 7: done

027-2.gat,39,91,0|script|Golbenez|307,{

    set @Graveyard_Inn_MASK, NIBBLE_4_MASK;
    set @Graveyard_Inn_SHIFT, NIBBLE_4_SHIFT;

    set @state, ((QUEST_Graveyard_Inn & @Graveyard_Inn_MASK) >> @Graveyard_Inn_SHIFT);

    set @Graveyard_Inn_Woman_MASK, NIBBLE_0_MASK;
    set @Graveyard_Inn_Woman_SHIFT, NIBBLE_0_SHIFT;

    set @woman, ((QUEST_Graveyard_Inn & @Graveyard_Inn_Woman_MASK) >> @Graveyard_Inn_Woman_SHIFT);

    set @Graveyard_Inn_Lover_MASK, NIBBLE_1_MASK;
    set @Graveyard_Inn_Lover_SHIFT, NIBBLE_1_SHIFT;

    set @lover, ((QUEST_Graveyard_Inn & @Graveyard_Inn_Lover_MASK) >> @Graveyard_Inn_Lover_SHIFT);

    set @Graveyard_Inn_Husband_MASK, NIBBLE_3_MASK;
    set @Graveyard_Inn_Husband_SHIFT, NIBBLE_3_SHIFT;

    set @husband, ((QUEST_Graveyard_Inn & @Graveyard_Inn_Husband_MASK) >> @Graveyard_Inn_Husband_SHIFT);

    set @ROTTENRAGS_AMOUNT, 50;
    set @ROTTENRAGS_EXP, 100000;
    set @UNDEADEAR_AMOUNT, 15;
    set @UNDEADEAR_EXP, 150000;
    set @UNDEADEYE_AMOUNT, 10;
    set @UNDEADEYE_EXP, 150000;
    set @JACKOSOUL_EXP, 2000;

    if (@state >= 7) goto L_Done;
    if (@state == 6) goto L_Soul;
    if (@state == 5) goto L_All;
    if (@state == 4) goto L_Savaric;
    if (@state == 3) goto L_Hamond;
    if (@state == 2) goto L_Reid;
    if (@state == 1) goto L_Back;

    mes "[Golbenez]";
    mes "\"How do you like my place of leisure, mortal?\"";
    menu
        "What is this place? Why is it full of dead people?",-,
        "You look different here. Nice horns.",L_Horns,
        "I'm enjoying myself, thanks for asking.",L_Close;
    mes "[Golbenez]";
    mes "\"Mortal, this is beyond your comprehension. Do not worry about it and enjoy your time.\"";

    if (@lover < 2)
        goto L_Close;
    menu
        "I want to know. Try me.",-;
    mes "Golbenez takes a piercing look at you.";
    mes "[Golbenez]";
    mes "\"Fine.\"";
    next;
    mes "\"This place was a normal inn many, many of your years ago. Chance brought me here and it became one of my favorite playgrounds.\"";
    next;
    mes "Golbenez laughs in a way which really gives you the creeps.";
    next;
    mes "[Golbenez]";
    mes "\"Unfortunately, it got destroyed. I had no choice but to take the memories from the dead and recreate this place.\"";
    menu
        "What do you mean by 'playground?'",-,
        "Recreate? What do you mean by that?",-;
    mes "[Golbenez]";
    mes "\"As I said, you do not understand. You are starting to bore me. Leave.\"";

    set @state, 1;
    callsub S_Update_Mask;

    goto L_Close;

L_Back:
    if (@lover >= 3)
        goto L_Memory;
    mes "Golbenez ignores your presence.";
    goto L_Close;

L_Memory:
    mes "[Golbenez]";
    mes "\"Mortal, you are back. Are you going to bore me with other stupid questions?\"";
    menu
        "I want to know what happened in the inn, before it was destroyed.",-,
        "Nevermind.",L_Close;
    if ((@woman >= 7)
        && (@lover >= 3)
        && (@husband >= 4))
            goto L_Detective;
    mes "[Golbenez]";
    mes "\"I was watching you play detective. I find it amusing. Play it a little longer and I may show you some of the memories from that time.\"";
    goto L_Close;

L_Detective:
    mes "[Golbenez]";
    mes "\"Watching you digging into this dirty little love story of those foolish humans has certainly been amusing so far.\"";
    next;
    mes "\"I can show you all their memories. Would you like that?\"";
    next;
    mes "\"The only thing you would have to do is promise me your soul.\"";
    menu
        "No!",-,
        "That's too expensive. Make another offer.",-,
        "My soul?!",-;
    mes "Golbenez giggles, a strange and somewhat unnerving sound from such a being.";
    mes "[Golbenez]";
    mes "\"You should have seen your face. Really, mortals can be so funny.\"";
    next;
    mes "\"Very well, I will show you the birdbrained woman's memories if you bring me " + @ROTTENRAGS_AMOUNT + " Rotten Rags. And an Orange Cake.\"";
    set @state, 2;
    callsub S_Update_Mask;
    goto L_Close;

L_Reid:
    mes "[Golbenez]";
    mes "\"So, do you have the " + @ROTTENRAGS_AMOUNT + " Rotten Rags I want?\"";
    if (countitem("RottenRags") < @ROTTENRAGS_AMOUNT)
        menu
            "What do you need them for?",L_Why_Stuff,
            "I'm working on that.",L_Close;
    menu
        "Here you go.",-,
        "I'm working on that.",L_Close;
    if (countitem("OrangeCake") < 1)
        goto L_No_Cake;
    if (countitem("RottenRags") < @ROTTENRAGS_AMOUNT)
        goto L_Betray;
    delitem "RottenRags", @ROTTENRAGS_AMOUNT;
    delitem "OrangeCake", 1;
    getexp @ROTTENRAGS_EXP, 0;

    mes "[Golbenez]";
    mes "\"Very nice. I will show you the woman's memories of that night. Come closer.\"";
    next;
    mes "\"You hesitate but then take a step towards Golbenez. He grabs your head with his claw-like fingers.\"";
    next;
    callsub S_Reidsmem;
    set @state, 3;
    callsub S_Update_Mask;
    menu
        "Can I see it again?",-,
        "Wow, that was interesting.",-,
        "Why do you collect such personal memories?",-;
    mes "Golbenez throws you an amused look.";
    next;
    mes "[Golbenez]";
    mes "\"I will explain the deal to you. Once you have paid for a memory, you can come and see it again as often as you like. And I collect whatever memory I want to. Mortals are a constant source of amusement for me.\"";
    next;
    mes "\"I have more memories you might want to see. Bring me "+ @UNDEADEAR_AMOUNT + " Undead Ears. And a Chocolate Cake.\"";
    goto L_Close;

L_Hamond:
    mes "[Golbenez]";
    mes "\"Ah, mortal. Do you have the " + @UNDEADEAR_AMOUNT + " Undead Ears I want?\"";
    if (countitem("UndeadEar") < @UNDEADEAR_AMOUNT)
        menu
            "I want to see Reid's memory again.",L_R_Again,
            "What will you do with them?",L_Why_Stuff,
            "Not yet.",L_Close;
    menu
        "I want to see Reid's memory again.",L_R_Again,
        "I have what you want.",-,
        "I'm working on that.",L_Close;
    if (countitem("ChocolateCake") < 1)
        goto L_No_Cake;
    if (countitem("UndeadEar") < @UNDEADEAR_AMOUNT)
        goto L_Betray;
    delitem "UndeadEar", @UNDEADEAR_AMOUNT;
    delitem "ChocolateCake", 1;
    getexp @UNDEADEAR_EXP, 0;
    mes "[Golbenez]";
    mes "\"Good. I will show you the memories of the dumb husband now. Come to me.\"";
    next;
    mes "You step closer to Golbenez and he holds your head again.";
    callsub S_Hamondsmem;
    set @state, 4;
    callsub S_Update_Mask;
    mes "[Golbenez]";
    mes "\"It is really interesting how illogical humans behave.\"";
    next;
    mes "\"And how predictable they are. I can see in your eyes that you want to know how this drama went on.\"";
    next;
    mes "\"Bring me " + @UNDEADEYE_AMOUNT + " Undead Eyes and an Apple Cake.\"";
    goto L_Close;

L_Savaric:
    mes "[Golbenez]";
    mes "\"I hope you are bringing me the " + @UNDEADEYE_AMOUNT + " Undead Eyes I want.\"";
    if (countitem("UndeadEye") < @UNDEADEYE_AMOUNT)
        menu
            "I want to see Reid's memory again.",L_R_Again,
            "I want to see Hamond's memory again.",L_H_Again,
            "I really wonder what you do with the stuff I bring you.",L_Why_Stuff,
            "They're hard to get. I'm still working on that.",L_Close;
    menu
        "I want to see Reid's memory again.",L_R_Again,
        "I want to see Hamond's memory again.",L_H_Again,
        "I got what you want.",-,
        "I'm working on that.",L_Close;
    if (countitem("AppleCake") < 1)
        goto L_No_Cake;
    if (countitem("UndeadEye") < @UNDEADEYE_AMOUNT)
        goto L_Betray;
    delitem "UndeadEye", @UNDEADEYE_AMOUNT;
    delitem "AppleCake", 1;
    getexp @UNDEADEYE_EXP, 0;
    mes "[Golbenez]";
    mes "\"Very good. Now I will show you the pitiful mage's memory.\"";
    next;
    mes "He holds your head and everything goes black again.";
    callsub S_Savaricsmem;
    set @state, 5;
    callsub S_Update_Mask;
    mes "[Golbenez]";
    mes "\"Humans can be so amusing!\"";
    menu
        "YOU CRUEL MONSTER!",-,
        "I agree. What a bunch of idiots.",-;
    mes "Golbenez's mouth twists into something resembling a smile.";
    mes "[Golbenez]";
    mes "\"Oh, thank you.\"";
    goto L_Close;

L_All:
    if (@lover == 6)
        goto L_Negotiate;
    mes "[Golbenez]";
    mes "\"Did you come back to see the memories again?\"";
    menu
        "I want to see Reid's memory again.",L_R_Again,
        "I want to see Hamond's memory again.",L_H_Again,
        "I want to see Savaric's memory again.",L_S_Again,
        "Nevermind.",L_Close;
    goto L_Close;

L_Negotiate:
    mes "Golbenez's eyes are glowing greedily.";
    mes "[Golbenez]";
    mes "\"Did you come back to see the memories again? Or are you going to offer me another deal?\"";
    menu
        "I want Savaric's soul back.",-,
        "I want to see Reid's memory again.",L_R_Again,
        "I want to see Hamond's memory again.",L_H_Again,
        "I want to see Savaric's memory again.",L_S_Again,
        "Nevermind.",L_Close;
    mes "[Golbenez]";
    mes "\"Savaric's soul, yes. It is a pretty nice soul, you know. From a mage... no, even better... from a lovestruck mage. It is valuable.\"";
    next;
    mes "\"Why do you think it would be easy to get it back? Do you have anything of equal worth to offer?\"";
    next;
    menu
        "What about another cake?",L_Cake,
        "I'm not sure. What do you want?",-,
        "No.",L_Close;
    mes "[Golbenez]";
    mes "\"Bring me a soul of at least equal power to the mage's. I do not care about its origin.\"";
    set @state, 6;
    callsub S_Update_Mask;
    goto L_Close;

L_Soul:
    mes "[Golbenez]";
    mes "\"So, did you get a soul for me?\"";

L_Soul_Try:
    if (countitem("JackOSoul") < 1)
        menu
            "I'll go and get one.",L_Close;
    menu
        "What about this Jack O Soul?",-,
        "I'll go and try to find a soul.",L_Close;

    if (countitem("JackOSoul") < 1)
        goto L_Betray;
    delitem "JackOSoul", 1;
    set @soul, rand(50);
    if (@soul == 0)
        goto L_Soul_Success;
    mes "[Golbenez]";
    mes "\"No, this one is not as powerful as the mage's. Bring me another one of those.\"";
    getexp @JACKOSOUL_EXP, 0;
    goto L_Soul_Try;

L_Soul_Success:
    mes "Golbenez suddenly gets excited.";
    mes "[Golbenez]";
    mes "\"Yes! This one is full of energy.\"";
    next;
    mes "\"So be it, mortal. I will release Savaric's soul in exchange for this one.\"";
    set @state, 7;
    callsub S_Update_Mask;
    goto L_Close;

L_Done:
    mes "[Golbenez]";
    mes "\"I hope you enjoyed your little detective game. I certainly did.\"";
    menu
        "You're cruel.",L_Close,
        "I want to see Reid's memory again.",L_R_Again,
        "I want to see Hamond's memory again.",L_H_Again,
        "I want to see Savaric's memory again.",L_S_Again;

L_Cake:
    mes "Golbenez laughs.";
    mes "[Golbenez]";
    mes "\"This is why I like you. Despite being human, you have a delightful sense of humor.\"";
    goto L_Close;

L_S_Again:
    callsub S_Savaricsmem;
    mes "[Golbenez]";
    mes "\"What a fool he was, even for a human.\"";
    goto L_Close;

L_R_Again:
    callsub S_Reidsmem;
    mes "\"This memory seems to excite you. Interesting.\"";
    goto L_Close;

L_H_Again:
    callsub S_Hamondsmem;
    mes "[Golbenez]";
    mes "\"Humans can be so stupid, have you noticed that?\"";
    goto L_Close;

L_Horns:
    mes "Golbenez bursts out with laughter.";
    next;
    mes "[Golbenez]";
    mes "\"Mortals! They never stop surprising me.\"";
    goto L_Close;

L_Why_Stuff:
    mes "[Golbenez]";
    mes "\"That is not your concern.\"";
    goto L_Close;

L_Betray:
    mes "[Golbenez]";
    mes "\"I am warning you, mortal. Do not try to betray me!\"";
    goto L_Close;

L_No_Cake:
    mes "[Golbenez]";
    mes "\"Fool! You forgot my cake!\"";
    goto L_Close;

L_Close:
    set @Graveyard_Inn_MASK, 0;
    set @Graveyard_Inn_SHIFT, 0;
    set @state, 0;
    set @Graveyard_Inn_Woman_MASK, 0;
    set @Graveyard_Inn_Woman_SHIFT, 0;
    set @woman, 0;
    set @Graveyard_Inn_Lover_MASK, 0;
    set @Graveyard_Inn_Lover_SHIFT, 0;
    set @lover, 0;
    set @Graveyard_Inn_Husband_MASK, 0;
    set @Graveyard_Inn_Husband_SHIFT, 0;
    set @husband, 0;
    set @ROTTENRAGS_AMOUNT, 0;
    set @ROTTENRAGS_EXP, 0;
    set @UNDEADEAR_AMOUNT, 0;
    set @UNDEADEAR_EXP, 0;
    set @UNDEADEYE_AMOUNT, 0;
    set @UNDEADEYE_EXP, 0;
    set @JACKOSOUL_EXP, 0;
    set @soul, 0;
    close;

S_Reidsmem:
    mes "[Golbenez]";
    mes "\"I will send her memories into your brain now. Do not collapse or do something equally foolish.\"";
    next;
    mes "Everything turns black. Then, slowly, you see something. You're standing outside and watching an alive-looking Hamond ride away on a carriage drawn by a mouboo.\"";
    next;
    mes "\"You turn back and enter a nice looking building. It seems to be the inn.";
    next;
    mes "Obviously, you are watching Reid's memories through her eyes. You're getting excited. Or is it Reid who feels excited? You're not sure.";
    next;
    mes "You-Reid walks up the stairs and stops in front of the door you recognize as Savaric's room. You get a twisting feeling, something between desire, guilt and despair.";
    next;
    mes "It seems you're not only seeing Reid's memories, but also feeling them!";
    next;
    mes "Reid turns away from Savaric's door and walks to her rooms quickly. She is shivering. You feel her heart pounding.";
    next;
    mes "She enters the room and pulls a red dress out of a chest and changes into it. As you feel her heart pounding even faster, you recognize the dress as the one her ghost is wearing.";
    next;
    mes "She sits down on the bed holding her head in her hands.";
    next;
    mes "[Reid]";
    mes "\"This is wrong...\"";
    next;
    mes "Then she stands up and returns to Savaric's door. She hesitates again. Suddenly the door opens, Savaric standing there, smiling.";
    next;
    mes "[Savaric]";
    mes "\"I was hoping you would come.\"";
    next;
    mes "He reaches out with his hands and you feel Reid getting dizzy.";
    next;
    mes "You enter his room together. There is only one candle on the table, providing a dim light.";
    next;
    mes "[Reid]";
    mes "\"I- we- Savaric- this is not right- we shouldn't do this.\"";
    next;
    mes "[Savaric]";
    mes "\"But you came. Shh, don't worry. Let me hold you.\"";
    next;
    mes "Savaric puts his arms around Reid and leads her to the bed. You feel a rush of sexual desire.";
    next;
    mes "He starts to caress Reid's face with his lips, as his hands slip the dress from her shoulders.";
    next;
    mes "Reid's feelings are like a firestorm and it's hard for you to concentrate on what is happening.";
    next;
    mes "Savaric touches her body and - you suddenly stand in front of Golbenez again, wobbling around for a moment, before falling to the ground.";
    next;
    mes "[Golbenez]";
    mes "\"I told you not to collapse!\"";
    next;
    mes "\"It seems Reid's feelings were too strong for you to withstand.\"";
    next;
    mes "Golbenez grins.";
    next;
    mes "[Golbenez]";
    mes "\"But I guess you can imagine what followed.\"";
    next;
    return;

S_Hamondsmem:
    mes "The darkness vanishes and you see the back of a mouboo pulling the carriage you're sitting on. Or better to say, Hamond is sitting on.";
    next;
    mes "You feel very, very nervous and worried.";
    next;
    mes "[Hamond]";
    mes "\"I shouldn't leave her alone with that debaucher. But she loves me. She won't give in to such a dandy. I believe in her. I trust her.\"";
    next;
    mes "The carriage reaches a river, but it seems the bridge is damaged. A group of people is standing on the riverside. There is one little boat, taking the people across, one by one.";
    next;
    mes "You feel a great surge of relief growing in Hamond's chest.";
    next;
    mes "He shakes the reins and turns his carriage around. Obviously this is a great excuse for him to return to Reid at once.";
    next;
    mes "When he returns to the inn, the sun is already low on the horizon. He tells the stableboy to take care of the mouboo and carriage, then rushes up the stairs heading to his and Reid's rooms. You can feel his heart pounding.";
    next;
    mes "He rips the door open and the room is empty. A cold feeling grows in his chest. He turns around and sees Reid standing in front of him, wearing her best dress. The red one which makes her look so beautiful - you feel a sharp sting in Hamonds heart.";
    next;
    mes "The dress is crumpled and seems to have been put on in haste. Her hair is in a disarray.";
    next;
    mes "[Reid]";
    mes "\"Hamond! What are you doing here? Why are you already back?\"";
    next;
    mes "You feel a burning rage growing in Hamond.";
    mes "[Hamond]";
    mes "\"You're not happy to see me, are you? Would you prefer me to run this inn for you but ignore your cockish behavior?!\"";
    next;
    mes "The shocked, painful, and obviously guilty look on Reid's face fills Hamond with a strange mixture of pain and gratification.";
    next;
    mes "He grabs her arm, pulls her into the room and slams the door shut behind them. You see a fearful look on Reid's face.";
    next;
    mes "[Reid]";
    mes "\"Hamond! Please, calm down!\"";
    next;
    mes "The feeling of broken trust and disappoinment seems to drive you... Hamond... crazy.";
    next;
    mes "Hamond slaps her across the face.";
    next;
    mes "[Hamond]";
    mes "\"You are MY WIFE! I'll teach you what that means!\"";
    next;
    mes "As Hamond grabs Reid roughly and holds her tight, you can smell her sweat and fear, but under that is the smell of another man! Hamond's vision goes red.";
    next;
    mes "[Hamond]";
    mes "\"You dirty slut! I did everything for you!\"";
    next;
    mes "Reid is struggling wildly to get out of Hamond's hold. She is surprisingly strong and together they fall onto the table.";
    next;
    mes "The next thing you see is a teapot smashing into Hamond's face. He stumbles back.";
    next;
    mes "You see Reid running to the door, where she pauses and throws a look over her shoulder. Then she runs out of the room.";
    next;
    mes "Her face was wet with tears, her lips bloody and her eye was already turning black.";
    next;
    mes "You feel shock and regret.";
    mes "[Hamond]";
    mes "\"What did I do? Reid... How could I ever hurt you?\"";
    next;
    mes "Your sight turns black and with your next blink you're standing in front of Golbenez again.";
    next;
    return;

S_Savaricsmem:
    mes "As you gain your eyesight again, you find yourself bustling back and forth in Savaric's room.";
    next;
    mes "The mixture of feelings you sense is very confusing. Worry, guilt, craving and below all that a deep feeling of luck and satisfaction.";
    next;
    mes "Then you hear steps outside the room. Savaric turns to the door quickly, then Reid breaks into the room.";
    next;
    mes "She looks utterly tattered, her dress ripped and her lips bleeding. She has a black eye and she is sobbing uncontrollably. It takes Savaric less than a second to reach her and put his arms around her carefully.";
    next;
    mes "You feel a deep, cold anger.";
    next;
    mes "[Savaric]";
    mes "\"Reid, my love, what did he do to you? Please come and lay down.\"";
    next;
    mes "Savaric leads Reid to his bed and covers her gently. Then he holds and rocks her until she falls asleep. You notice Savaric is only wearing his underpants.";
    next;
    mes "After Reid's breathing has calmed down and her abused face looks peaceful again, he stands up and his cold burning anger comes back.";
    next;
    mes "[Savaric]";
    mes "\"I won't let him get away with that!\"";
    next;
    mes "Savaric pulls some things out of a chest and puts them on the table. He lights some candles and begins to mix together some powders and fluids. You can feel his concentration, but below that is a great anger keeping clear thoughts at bay.";
    next;
    mes "Savaric seems to be done with his magic ingredients and takes what he mixed together. You feel rising power inside of you. The moment you think you can't stand it anymore, the tension disappears and a purple light fills the room for a moment.";
    next;
    mes "Golbenez is standing in front of you.";
    next;
    mes "[Golbenez]";
    mes "\"Mortal! It was very rude of you to force me to this place with that spell - are you aware of the power you are playing around with?!\"";
    next;
    mes "Fear is rising inside of you, but also excitement accompanied by a feeling of power.";
    next;
    mes "[Savaric]";
    mes "\"I called you to get rid of the person who did this to this adorable woman.\"";
    mes "He points at Reid";
    next;
    mes "[Savaric]";
    mes "\"Tell me your price.\"";
    next;
    mes "Golbenez's behavior changes immediately and he suddenly seems much friendlier.";
    next;
    mes "[Golbenez]";
    mes "\"Ah, you are offering me a deal. That is something completely different. Let me see... I can free her from all pain and anything or anyone causing her trouble.\"";
    next;
    mes "You feel Savaric's heart pounding.";
    mes "[Savaric]";
    mes "\"Yes. That's what I want. I want her to be safe from any harm.\"";
    next;
    mes "Golbenez's face turns into a smile.";
    mes "[Golbenez]";
    mes "\"This requires some effort. It will have a remarkable price. I want your soul in exchange.\"";
    next;
    mes "The feeling of power disappears, giving place to uncertainty.";
    mes "[Savaric]";
    mes "\"My - soul? Can't it be something else?\"";
    next;
    mes "[Golbenez]";
    mes "\"Think about what you get! This woman will never feel any pain again!\"";
    next;
    mes "Savaric's hesitation vanishes.";
    mes "[Savaric]";
    mes "\"So be it. Take my soul and prevent Reid from feeling pain ever again.\"";
    next;
    mes "Golbenez's smile changes to an ugly grin filled with malice. He reaches out to Savaric and seems to pull something out of him. Suddenly, you feel very weak and empty.";
    next;
    mes "In the next moment, you're lying on the ground and see Golbenez raising his arms. You sense an unbelievable magic force - what is he doing?";
    next;
    mes "Undead enter the room and one of them walks to the bed and breaks Reid's neck with a quick movement. A dark and black feeling of despair rises inside of you.";
    next;
    mes "[Savaric]";
    mes "\"NO!! What are you doing?\"";
    next;
    mes "[Golbenez]";
    mes "\"She will never feel pain anymore. Just like everyone else in this inn - besides you, foolish mortal.\"";
    next;
    mes "Golbenez leaves the room and you feel Savaric giving in to his pain. He is lying on the floor and trying to understand what he did.";
    next;
    mes "After a few moments of only feeling his suffering, Savaric finally manages to stand up again. He can't stand to see Reid's dead body anymore and stumbles out of the room.";
    next;
    mes "But what you can see in the rest of the inn isn't able to ease the suffering you're feeling. The undead have slaughtered every person in the inn, leaving only Savaric alive.";
    next;
    mes "Savaric seems stunned by shock. He slowly returns to his room and kneels in front of the bed, taking Reids hand.";
    next;
    mes "[Savaric]";
    mes "\"Reid - I'm so sorry. My haughtiness was greater than my skills.\"";
    next;
    mes "The numb feeling gives way to a new feeling of determination.";
    next;
    mes "Savaric pulls a rope out of the chest beside the bed.";
    mes "[Savaric]";
    mes "\"Now there's only one thing left for me to do.\"";
    next;
    mes "Your vision turns black again and you find yourself standing in front of Golbenez.";
    return;

S_Update_Mask:
    set QUEST_Graveyard_Inn,
        (QUEST_Graveyard_Inn & ~(@Graveyard_Inn_MASK))
            | (@state << @Graveyard_Inn_SHIFT);
    return;
}
