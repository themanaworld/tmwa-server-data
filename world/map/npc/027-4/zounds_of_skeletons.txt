// Zounds of Skelle challenge (c) 2025 Hello=)
// My attempt on Hardcore Survival battle for TMW.
// Inspired by bug in generals battles. Also dubs as prelude to Reaper fight.

// In that room some image gives clear hint, portraying Reaper.
// Chatting shadow sets things in motion if player had items.

// Overall flow:
//  /-> Dark Shadow -> Reaper -> Battle Controller -> \
//  \-------------------------------------------------/

// In more details:
// 1) Player chats Dark Shadow#ZS, it either moans about items or starts action
// 2) Reaper#ZS::OnReaperIntro - reaper introduces self.
// 3) Reaper#ZS::OnReaperAnnounce - does announce, gives time to prepare.
// 4) Battle#ZS::OnBattleStart - battle actually starts and
//    5) zs_deferred_team_estimate()
//    6) Battle#ZS::BattleConf queued (to time after team estimation done)
//    7) Battle#ZS::BattleConf queues 1st battle round (Battle#ZS::OnBattleRound)
//      8) Battle loop, rearms Battle#ZS::OnBattleRound (while > 0 skels to spawn)
// 9) If (all mobs spawned || everyone dead) -> Battle#ZS::OnBattleEnd
// 10) OnBattleEnd invokes OnMaybeReward to give BPs to whoever survived it.
// 11) OnBattleEnd resets NPCs to pre-battle state.

// "Failed run" means "everyone died". Win by timeout is OK, like in candor.

// Strange dark shadow craving for Souls and Dark Crystals
027-4,120,75,0|script|Dark Shadow#ZS|369
{
    if !(isin(strnpcinfo(3), getnpcx()-3, getnpcy()-3, getnpcx()+3, getnpcy()+3))
        goto L_TooFar;
    if !(call("get_zs_items")) // No items -> no battle
        end;
    npctalk strnpcinfo(0), "He-he-he, thanks, mortal. These Souls and Dark Crystals were really useful...";
    set .caller$, strcharinfo(0), "Reaper#ZS"; // Inform Reaper who called it
    addnpctimer 4000, "Reaper#ZS::OnReaperIntro"; // Stage Reaper intro
    addnpctimer 10000, "Reaper#ZS::OnReaperAnnounce"; // Stage Global announce
    addnpctimer (7 * 60 * 1000), "Battle#ZS::OnBattleStart"; // Stage battle, 7 minutes to prepare
    end;

L_TooFar:
    message strcharinfo(0), "Dark Shadow : Come closer, mortal..."; 
    end;
}

// Reaper reveals self - cut scene/plot/battle wait.
027-4,120,75,0|script|Reaper#ZS|408
{
    if !($@ZS_SURVIVAL) // Delay before battle.
        message strcharinfo(0), "Reaper : Give me few moments, mortal. I will thank you properly, he-he-he!";
    else                // Battle in progress.
        message strcharinfo(0), "Reaper : Enjoy by fight while you can! Soon your corpse will serve me, he-he!";
    end;

// Thrown when Reaper introduces itself -> OnReaperAnnounce
OnReaperIntro:
    disablenpc "Dark Shadow#ZS"; // Shadow pikes off
    enablenpc "Reaper#ZS"; // Reaper appears instead
    npctalk strnpcinfo(0), "...so I can regain my strength and finally CROSS DIMENSIONS! He-he-he.";
    end;

OnReaperAnnounce:
    announce "Reaper : " + .caller$ + " dares to challenge my undead hordes at crypt Level 2!", 0;
    end;

OnInit:
    disablenpc "Reaper#ZS"; // By default its shadow, not Reaper.
    end;
}

// This NPC handles flow of battle behind the scenes (not visible)
-|script|Battle#ZS|32767
{
    end;

OnBattleStart:
    set $@ZS_SURVIVAL, 1; // Room locked, etc
    set .periodic, 2000;  // Team alive status checks period
    set .failed, 0;       // Not a failed attempt so far
    set .round, 0;        // Round # cleanup from prev battles
    set .mul, if_then_else(($ZS_DIFFICULTY_MUL > 0), $ZS_DIFFICULTY_MUL, 1); // Difficulty = team estimate * mul / div
    set .div, if_then_else(($ZS_DIFFICULTY_DIV > 0), $ZS_DIFFICULTY_DIV, 1);
    set .round_delay,  if_then_else(($ZS_DIFFICULTY_RDELAY > 0), $ZS_DIFFICULTY_RDELAY, 30000); // Delay between rounds
    set .spawner_mobs, if_then_else(($ZS_DIFFICULTY_MOBS > 0), $ZS_DIFFICULTY_MOBS, 10); // # of mobs per spawner.
    set .spawner_time, if_then_else(($ZS_DIFFICULTY_TIME > 0), $ZS_DIFFICULTY_TIME, 2800); // Delay between spawns of spawner
    set .max_spawners, if_then_else(($ZS_DIFFICULTY_SPAWNERS > 0), $ZS_DIFFICULTY_SPAWNERS, 15); // Max spawners
    fakenpcname "#ZS_WarpBouncerInt", "#ZS_WarpBouncerInt", 369; // Door NPC -> shadow
    npctalk "Reaper#ZS", "Let's see what you made of, mortals!";
    addnpctimer 0, strnpcinfo(0) + "::OnBattlePeriodic"; // Launch team str periodic monitor
    addnpctimer 1000, strnpcinfo(0) + "::OnBattleInit"; // Defer setup after team estimation
    end;

OnBattleInit:
    set .difficulty, (.team_str * .mul) / .div; // Allows on-the-fly battle recalibration
    set .skels, 1000 + (.difficulty * 3); // How many mobs in total?
    set .skels2spawn, .skels;
    set .bp, min(100, 30 + (.skels / 200));
    if ($ZS_DEBUG) debugmes "OBS2: Team = " + .team_str + "DiffAdj = " + .difficulty +
                            "Skels = " + .skels + " bp = " + .bp;
    addnpctimer 0, strnpcinfo(0) + "::OnBattleRound"; // Enqueue 1st round
    end;

OnBattleRound:
    if !($@ZS_SURVIVAL) end;  // No new spawners or rearm if battle aborted.
    set .round, .round + 1;
    set .spawners, 0;
    npctalk "Reaper#ZS", "Wave " + .round + " starts (" + .skels2spawn + " skeketons out of " + .skels + " total)";
    goto L_AddSpawner;

L_AddSpawner:
    set .spawners, .spawners + 1;
    if (call("zs_add_spawner", .spawner_time, .spawner_mobs) > 0)
        set .skels2spawn, (.skels2spawn - .spawner_mobs);
    if (.spawners < min(.round, .max_spawners))
        goto L_AddSpawner; // add more spawners -> increase battle difficulty

    if (.skels2spawn > 0)
        addnpctimer .round_delay, strnpcinfo(0) + "::OnBattleRound"; // Rearm self for more
    else
        donpcevent strnpcinfo(0) + "::OnBattleEnd"; // Players survived ordeal!
    end;

OnBattleEnd:
    if !(.failed) // Try to reward players if it isn't failed run
        areatimer 0, "027-4", 89, 70, 128, 84, 0, strnpcinfo(0) + "::OnMaybeReward";
    disablenpc "Reaper#ZS"; // Reaper disappears
    enablenpc "Dark Shadow#ZS"; // Shadow again
    fakenpcname "#ZS_WarpBouncerInt", "#ZS_WarpBouncerInt", 127; // Remove door shroud
    killmonster "027-4", "~ZoundsOfSkele~mob"; // Remove all mobs from battle
    set $@ZS_SURVIVAL, 0; // Unlock room
    end;

OnTeamEstimate: // Team Estimator handler, invoked per-player.
    if !(isdead()) // Skip dead players
        set .team_str, .team_str + BaseLevel;
    if ($ZS_DEBUG) debugmes "OTE: TeamStrAfter = " + .team_str;
    end;

OnBattlePeriodic: // Faster loop. Mostly monitors if all dead @ battle.
    if !($@ZS_SURVIVAL) end; // Halt periodic if battle not active.
    if ((.team_str <= 0) && (.round > 0))
        goto L_AllDead; // Everyone dead -> abort battle, Reaper laughs
    if ($ZS_DEBUG) debugmes "OBP: TeamStrNow = " + .team_str;
    addnpctimer .periodic, "Battle#ZS::OnBattlePeriodic"; // Rearm self
    set .team_str, 0; // Reset team str for next estimation.
    areatimer 0, "027-4", 89, 70, 128, 84, 0, strnpcinfo(0) + "::OnTeamEstimate"; // Queue next estimation
    end;

L_AllDead:
    mapannounce "027-4", "Reaper : Ha-ha-ha, pathetic mortals are no match to my legions! Soon you all will be in land of dead!", 0;
    set .failed, 1; // Explicitly mark failed attempt (more robust vs cheats)
    donpcevent strnpcinfo(0) + "::OnBattleEnd"; // Do cleanup
    end;

OnMaybeReward:
    if (isdead()) end; // No rewards for dead PCs.
    set BOSS_POINTS, (BOSS_POINTS + .bp);
    message strcharinfo(0), "Reaper : Whoah! I've miscalculated, you not as pathetic as I thought! I'm better back to shadows. We'll meet again!";
    message strcharinfo(0), "Narrator : You won hard battle and gain " + .bp + " Boss Points for " + BOSS_POINTS + " Boss Points total";
    end;
}


// This NPC is template for Mob Spawner puppets (scary shadows emitting mobs).
-|script|ZSMobSpawner|32767
{
    message strcharinfo(0), "Scary Shadow : something scary is about to appear!";
    end;

OnTouch: // Do nothing if player steps on it
    end;

OnAddNewSpawner:
    set .res, puppet("027-4", .x, .y, "Shadow#ZS" + .x + .y, 369, 0, 0); // instatiate NPC
    if (.res <= 0) end; // Omit puppet setup on failure.
    set .zs_shadow, 1, .res; // Mark spawner NPC as such
    end;

OnMobSpawn:
    if ((.count > 0) && ($@ZS_SURVIVAL)) // Still some mobs to spawn && in battle?
        addnpctimer .delay, strnpcinfo(0) + "::OnMobSpawn"; // Rearm timer
    else
        destroy;
    setarray .@mobs[0], LadySkeleton, Skeleton, VoidSoldier, SoulEater, Wight, RedBone, WitchGuard;
    set .@mobID, .@mobs[rand(getarraysize(.@mobs))]; // Pick random mob from list
    monster "027-4", getnpcx(), getnpcy(), "Reaper's servant", .@mobID, 1, "~ZoundsOfSkele~mob";
    set .count, .count - 1;
    end;
}

// Conditional warp/bouncer - outside of room
027-4,96,68,0|script|#ZS_WarpBouncerExt|127,0,0
{
    if !($@ZS_SURVIVAL) end; // Battle not active -> no-op
    warp "027-4",96,66;          // Battle in progress -> no entry to battle.
    message strcharinfo(0), "Force Field : magic force throws you away!";
    end;
}

// Conditional warp/bouncer - inside of room
027-4,96,69,0|script|#ZS_WarpBouncerInt|127,0,0
{
    if !($@ZS_SURVIVAL) end; // Battle not active -> no-op
    warp "027-4",96,71;          // Battle in progress -> no escape.
    message strcharinfo(0), "Force Field : magic force throws you away!";
    end;
}

// This function Private to Zounds of Skeletons challenge.
// It meant to be called with both player RID and NPC attached.
// Input: nothing, but expects player.
// Output: 0 if player lacks items, 1 if all good.
// Side effects: moans about missing items or consumes items.
function|script|get_zs_items
{
    set .@souls_want, 30;
    set .@darkcrystals_want, 10;
    set .@souls, countitem(Soul);
    set .@darkcrystals, countitem(DarkCrystal);
    if ((.@souls < .@souls_want) || (.@darkcrystals < .@darkcrystals_want))
        goto L_NoItems;
    // Got items -> consume them!
    delitem Soul, .@souls_want;
    delitem DarkCrystal, .@darkcrystals_want;
    return 1;

L_NoItems:
    if (.@souls < .@souls_want)
        message strcharinfo(0), "Dark Shadow : ##BSouls... more souls... bring me " + .@souls_want + " souls...";
    if (.@darkcrystals < .@darkcrystals_want)
        message strcharinfo(0), "Dark Shadow : ##BDarkness... more darkness... bring me " +.@darkcrystals_want + " dark crystals...";
    return 0;
}

// This function Private to Zounds of Skeletons challenge. Creates spawner NPC
// It designed to run in ANY context, whether RID attached or not.
// It does inter-NPC call to switch to "template" NPC.
// Inputs: see code below.
// Return: On success: NPC ID of NPC created, > 0
//         On failure: 0 if puppet failed, -1 if arg check failed.
// Side effects:
// * Puppet spawner NPC - self destructing on timer
// * Needs PandoraboxTemplate NPC to exist
function|script|zs_add_spawner
{
    set .@delay, getarg(0, 1000); // Delay between spawns
    set .@count, getarg(1, 10);   // Count of mobs to spawn
    set .@res, -1;
    goto L_PickSpot;

L_PickSpot:
    set .@x, rand(89, 128); // 027-4 room area
    set .@y, rand(70, 84);  
    if (iscollision("027-4", .@x, .@y)) goto L_PickSpot; // Find suitable spot

    set .@name$, "Evil#ZS" + .@x + .@y; // E.g. Evil#ZS9080 - NPCs need UNIQUE names
    // NPC var  value   Destination NPC (inter-npc call setup)
    set .x,     .@x,     "ZSMobSpawner";
    set .y,     .@y,     "ZSMobSpawner";
    set .name$, .@name$, "ZSMobSpawner";
    donpcevent("ZSMobSpawner::OnAddNewSpawner"); // do inter-npc call
    set .@res, get(.res, "ZSMobSpawner"); // Fetch .res from called npc
    if (.@res <= 0)
        goto L_Ret; // Skip puppet setup on failure
    // Setup puppet NPC
    addnpctimer .@delay, strnpcinfo(0, .@res) + "::OnMobSpawn"; // Stage event on puppet
    set .delay, .@delay, .@res; // Set count of mobs
    set .count, .@count, .@res; // Set delay between 
    return .@res;

L_Ret:
    return .@res;
}
