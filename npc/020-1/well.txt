
020-1,99,83,0	script	#Well	400,{
    @Q_MASK = NIBBLE_4_MASK;
    @Q_SHIFT = NIBBLE_4_SHIFT;
    @Q_status = ((QUEST_Nivalis_state) & @Q_MASK) >> @Q_SHIFT;

    if(@Q_status == 2) goto L_Finished;
    @item_ID = 0;
    setarray @quote_item$, "Yuck! Who has thrown that on me?","Ouch! Who hurts me?","White powder!? What's going on up there?","Ahw! It's raining ","","","";

    mes "...";
    menu
        "Throw something in the well.", L_trew,
        "Leave it alone.", L_Close;

L_trew:
    mes "What do you want to throw?";
    menu
        "Maggot slime", L_MenuItems,
        "A raw log", L_MenuItems,
        "Pile of ash", L_MenuItems,
        "Bottle of water", L_MenuItems,
        "Leave it alone", L_Close;

L_MenuItems:
    @menu = @menu - 1;

    // little IF to do @quote$ & @Item_ID
    @quote$ = @quote_item$[@menu];
    if(@menu == 0)
        @item_ID = 505;
    if(@menu == 1)
        @item_ID = 569;
    if(@menu == 2)
        @item_ID = 701;
    if(@menu == 3)
        @item_ID = 541;

    if(countitemcolor(@item_ID) < 1)
        goto L_No_Item;
    if(@menu == 3)
        getitem "EmptyBottle", 1;
    delitem @item_ID, 1;

    mes "[Mysterious voice inside the well]";
    mes "\" "+ @quote$ +"\"";
    next;
    menu
        "Who are you?", L_MenuItems1,
        "How did you get there?", L_MenuItems1,
        "Do you need help?", L_MenuItems1;

L_MenuItems1:
    @menu = @menu - 1;
        if (@menu == 0)
            @quote$ = "I'll talk about who I am after leaving the well.";
        if (@menu == 1)
            @quote$ = "Well, someone has pushed me in the well, I don't know who did that.";
        if (@menu == 2)
            @quote$ = "";
    mes "[Mysterious voice inside the well]";
    mes "\" "+ @quote$ +". So if you can call help for me... please do so!\"";
    @Q_status = 1;
    callsub S_Update_Var;
    close;

L_No_Item:
    mes "You don't have such an item... Come back when you have it.";
    close;

L_Finished:
    mes "This is a well.";
    close;

L_Close:
    close;

S_Update_Var:
    set QUEST_Nivalis_state,
          (QUEST_Nivalis_state & ~(@Q_MASK)
        | (@Q_status << @Q_SHIFT));
    return;
}

020-2,100,28,0	script	Miler	100,{
    @Q_MASK = NIBBLE_4_MASK;
    @Q_SHIFT = NIBBLE_4_SHIFT;
    @Q_status = ((QUEST_Nivalis_state) & @Q_MASK) >> @Q_SHIFT;

    mes "[Miler]";
    mes "\"Hello!\"";
    if(@Q_status == 2) close;
    if(@Q_status == 1)
        menu
            "Hello.", L_Close,
            "Hello, Can you help me?", L_Help;
    goto L_Close;

L_Help:
    mes "[Miler]";
    mes "\"What's the problem?\"";
    menu "Someone has fallen in the well.", L_Next;

L_Next:
    mes "[Miler]";
    mes "\"Ho! I'll help him!\"";
    getexp (BaseLevel * 111), 0;
    @Q_status = 2;
    callsub S_Update_Var;
    close;

L_Close:
    close;

S_Update_Var:
    set QUEST_Nivalis_state,
        (QUEST_Nivalis_state & ~(@Q_MASK)
      | (@Q_status << @Q_SHIFT));
    return;
}
