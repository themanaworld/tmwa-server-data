
002-2,21,22,0	script	Riskim	148,{
    mes "[Riskim]";
    mes "";
    mes "\"Welcome to our bakery!\"";

    callfunc "KadiyaSubquestConsts";
    if ((@Q_kadiya_status == @Q_STATUS_WANTS_CHOCOCAKE)
        && (countitem ("MopoxCurePotion") > 0))
            goto L_lace_chococake;

    if ((@Q_kadiya_status == @Q_STATUS_WANTS_ORANGECUPCAKE)
        && (countitem ("MopoxCurePotion") > 0))
            goto L_lace_cupcake;

    mes "\"Would you mind bringing me 6 acorns please? I need them to make flour for the chocolate cakes. I will be so delicious!\"";
    next;
    mes "\"Um, I mean, the cakes will be delicious...\"";
    emotion EMOTE_BLUSH;
    next;

    @dq_level = 15;
    @dq_cost = 3;
    @dq_count = 6;
    @dq_name$ = "Acorn";
    @dq_friendly_name$ = "acorns";
    @dq_money = 250;
    @dq_exp = 50;

    callfunc "DailyQuest";

    next;
    mes "\"And don't forget, our bakery is the best!\"";
    close;

L_lace_chococake:
    next;
    menu
        "Thanks!  Let me have a look around...", L_End,
        "Can you put this potion into a Chocolate Cake?", L_Next,
        "Goodbye!", L_End;

L_Next:
    mes "[Riskim]";
    mes "";
    mes "As you show Riskim the potion, he leans back, away from it.";
    mes "\"Ooooh, I remember that smell...\"";
    mes "\"Elanore's Mopox potion, right?  She made me drink that, too...\"";
    mes "He chuckles.";
    mes "\"Yes, well, you do want to hide that somehow.\"";
    next;
    mes "[Riskim]";
    mes "";
    mes "\"We'll need extra chocolate for that one. If you can give me ten pieces of chocolate and that potion, plus 400 GP, I will bake you a special Chocolate Cake that hides the taste.\"";
    next;
    menu
        "I will get them for you.", L_End,
        "Here you are!", L_Continue,
        "Maybe later.", L_End;

L_Continue:
    if (countitemcolor("ChocolateBar") < 10)
        goto L_lacking_ingredients;
    if (countitemcolor("MopoxCurePotion") < 1)
        goto L_lacking_ingredients;
    if (Zeny < 400)
        goto L_lacking_Zeny;
    delitem "MopoxCurePotion", 1;
    delitem "ChocolateBar", 10;
    getitem "LacedChocolateCake", 1;
    Zeny = Zeny - 400;

    mes "[Riskim]";
    mes "";
    mes "\"Please have a seat!\"";
    mes "You watch as Riskim melts the chocolate and stirs in the potion, then glazes a cake with the mixture.";
    mes "\"It's a bit more chocolatey than your average chocolate cake, of course...\"";
    next;
    mes "[Riskim]";
    mes "";
    mes "Riskim hands you the cake.";
    mes "\"Here you are. I hope that this works.\"";
    close;

L_lacking_Zeny:
    mes "[Riskim]";
    mes "";
    mes "\"I'm sorry, but I have to charge 400 GP.\"";
    close;

L_lacking_ingredients:
    mes "[Riskim]";
    mes "";
    mes "\"Sorry, but I need ten Chocolate Bars AND your cure potion for this. Please come back later when you have everything.\"";
    close;

L_lace_cupcake:
    next;
    menu
        "Thanks!  Let me have a look around...", L_End,
        "Can you put this potion into an Orange Cupcake?", L_More,
        "Goodbye!", L_End;

L_More:
    mes "[Riskim]";
    mes "";
    mes "\"Orange Cupcakes?  Oh dear... I'm sorry, but I have a very bad orange allergy.\"";
    next;
    mes "[Riskim]";
    mes "";
    mes "\"Perhaps the Chef at Dimond's Cove can help you?  We get all our cupcakes from him.\"";
    next;
    goto L_End;

L_End:
    close;
}

002-2,28,31,0	script	Sara	108,{
    mes "[Sara]";
    mes "";
    mes "\"Hello!\"";
    close;
}

002-2,25,22,0	shop	Drabur	112,736:500,737:550,738:600,739:600,513:30,519:100,534:90,527:300,508:70

002-2,21,28,0	script	Iormo	160,{
    mes "[Iormo]";
    mes "";
    mes "\"This room can get messy.\"";
    close;
}

002-2,24,27,0	script	Rynoh	159,{
    mes "[Rynoh]";
    mes "";
    mes "\"The cakes may be expensive, but they sure are delicious!\"";
    close;
}

002-2,26,27,0	script	Ontas	154,{
    mes "[Ontas]";
    mes "";
    mes "\"We come here every week. This place has some of the best bread I've ever eaten!\"";
    close;
}
