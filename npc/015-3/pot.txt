// The pot where the player can deposit stuff for the cat.

015-3.gat,37,29,0	script	Pot	127, {

    if (Katze > 0)
        mes "It's that old pot again.";
    if (Katze == 0)
        mes "It's a pot.";
    next;

    if (Katze == 1 && KatzeBeenOutside == 1) goto L_NeedsFood;
    if (Katze == 1) goto L_HasMilk;

    if (Katze == 2 && KatzeBeenOutside == 1) goto L_NeedsFur;
    if (Katze == 2) goto L_HasFood;

    if (Katze == 3) goto L_NeedsWood;

    if (Katze == 4 && KatzeBeenOutside == 0) goto L_HasWood;
    if (Katze >= 4) goto L_Finished;

L_NeedsMilk:
    if (countitem("Milk") > 0)
        menu
            "Pour in some milk", L_GiveMilk,
            "Leave it alone", -;
    close;

L_GiveMilk:
    delitem "Milk", 1;
    set Katze, 1;
    set KatzeBeenOutside, 0;
    close;

L_HasMilk:
    mes "There is milk in it.";
    close;

L_NeedsFood:
    mes "The milk is gone!";
    next;

    if (countitem("ChickenLeg") > 0 && countitem("Steak") > 0)
        menu
            "Put in a chicken leg", L_GiveChicken,
            "Put in a steak", L_GiveSteak,
            "Leave it alone", -;
    if (countitem("ChickenLeg") > 0 && countitem("Steak") == 0)
        menu
            "Put in a chicken leg", L_GiveChicken,
            "Leave it alone", -;
    if (countitem("ChickenLeg") == 0 && countitem("Steak") > 0)
        menu
            "Put in a steak", L_GiveSteak,
            "Leave it alone", -;
    close;

L_GiveChicken:
    delitem "ChickenLeg", 1;
    set Katze, 2;
    set KatzeBeenOutside, 0;
    close;

L_GiveSteak:
    delitem "Steak", 1;
    set Katze, 2;
    set KatzeBeenOutside, 0;
    close;

L_HasFood:
    mes "There is some food in it.";
    close;

L_NeedsFur:
    mes "And it's empty!";
    next;

    if (countitem("WhiteFur") > 0)
        menu "Put a white fur next to the pot", L_GiveFur,
             "Leave it alone", -;
    close;

L_GiveFur:
    mes "You put down the fur, but the cat doesn't seem to take any notice. Maybe there's something else you could do. You pick the fur up again.";
    close;

L_NeedsWood:
    if (countitem("RawLog") > 0)
        menu "Put a wooden log next to the pot", L_GiveWood,
             "Leave it alone", -;
    close;

L_GiveWood:
    delitem "RawLog", 1;
    set Katze, 4;
    set KatzeBeenOutside, 0;
    mes "You put the wooden log next to the pot. The cat eyes it suspiciously, but remains on her spot.";
    close;

L_HasWood:
    mes "A wooden log is patiently lying next to it.";
    close;

L_Finished:
    close;
}
