//#################################################################################
// Script by Shaili and QOAL
//#################################################################################
 
// Santa's Inn - Agostine, The Legendary Tailor
020-2.gat,102,28,0	script	Agostine	137,{
	if (QUEST_WG_state == 1) goto L_State_0_3;
	if (QUEST_WG_state == 2) goto L_State_1;
	if (QUEST_WG_state == 3) goto L_State_2;
	if (QUEST_WG_state == 4) goto L_State_4;
	if (QUEST_WG_state == 5) goto L_State_4_success;
	if (QUEST_WG_state == 6) goto L_State_6;
	if (QUEST_WG_state == 7) goto L_State_11;
	if (QUEST_WG_state == 8) goto L_State_12;
	if (QUEST_WG_state >= 9) goto L_State_13;
 
	mes "\"This goes up, this goes left...\"";
	next;
	mes "\"Mmmm...?\"";
	next;
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Oh, a customer! Let me introduce myself! My name is Agostine!";
	mes "Some people say that I am the best tailor in the world,";
	mes "but I think I am the best one in the universe!";
	mes "So, What can I do for you, my friend?\"";
	next;
	menu
		"I want something new for my wardrobe!", L_State_0_1,
		"Oh, nothing, thanks!", -;
	close;

// Quest #0 - Beginning - Needed: Iron Potion

L_State_0_1:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Well.. you know... working so hard makes me so tired!";
	mes "Can you bring me something tasty to drink, my friend?\"";
	next;
	menu
		"Sure, what kind of drink you like?", L_State_0_2,
		"I'm not your waiter!", -;
	close;

L_State_0_2:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Here we do not have a bar. Let me taste different beverages.";
	mes "I want something new to drink.\"";
	next;
	menu
		"I'll bring you different drinks to try!", L_State_Accept,
		"Maybe a bar will open soon, wait for it.", -;
	close;

L_State_Accept:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Thanks so much.\"";
	set QUEST_WG_state, 1;
	close;

L_State_0_3:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Well, what have you brought me?\"";
	next;
	menu
		"A Cactus Drink.", L_State_0_4,
		"A Cactus Potion.", L_State_0_5,
		"Some milk.", L_State_0_6,
		"A pint of beer.", L_State_0_7,
		"A Iron potion.", L_State_0_9,
		"A Concentration Potion.", L_State_0_8,
		"Nothing, at the moment.", -;
	close;

L_State_0_4:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"So, a Cactus Drink?\"";
	if (countitem("CactusDrink") < 1) goto L_State_neg;
	next;
	delitem "CactusDrink", 1;
	goto L_State_bad;
	close;

L_State_0_5:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"So, a Cactus Potion?\"";
	if (countitem("CactusPotion") < 1) goto L_State_neg;
	next;
	delitem "CactusPotion", 1;
	goto L_State_bad;
	close;

L_State_0_6:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"So, some milk for me?\"";
	if (countitem("Milk") < 1) goto L_State_neg;
	delitem "Milk", 1;
	goto L_State_bad;
	close;

L_State_0_7:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"So, a beer?\"";
	if (countitem("Beer") < 1) goto L_State_neg;
	next;
	delitem "Beer", 1;
	goto L_State_bad;
	close;

L_State_0_8:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"So, a Concentration Potion?\"";
	if (countitem("ConcentrationPotion") < 1) goto L_State_neg;
	next;
	delitem "ConcentrationPotion", 1;
	goto L_State_bad;
	close;

L_State_0_9:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"So, an Iron Potion?\"";
	if (countitem("IronPotion") < 1) goto L_State_neg;
	next;
	delitem "IronPotion", 1;
	mes "Agostine looks pleased as he drinks the potion.";
	next;
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Oh! This is damn good! I feel a new man, my friend!\"";
	next;
	set zeny, zeny + 500;
	mes "[Agostine, The Legendary Tailor]";
	mes "\"This is a little reward for your help. Take 500 GP, my friend!\"";
	set QUEST_WG_state, 2;
	close;

L_State_neg:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Seems that you lied. You don't have the drink you told me.\"";
	close;

L_State_bad:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Well, I don't like it so much. Bring me something else.\"";
	close;

// First Quest: edited version of Forest Bow Quest - Needed: about 30 White Furs and 15.000 GP

L_State_1:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Mmmm... Let me see... This year light colours are fashionable!";
	mes "Would you like a pair of snow-white Winter Gloves?\"";
	next;
	menu
		"No, thanks. I love dark clothes...", -,
		"You really can do this for me?", L_State_2;
	close;

L_State_2:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Well, Winter Gloves are so fashionable for their lovable fur!";
	mes "Only the best furs can be saw for these gloves!";
	mes "But, as you see, I am a tailor, not a hunter! So, why don't you bring me the best fluffy's fur you can see?\"";
	set QUEST_WG_state, 3;
	next;
	menu
		"You are crazy! I won't kill any animal for this!", -,
		"Sure, I will be back soon!", L_State_3;
	close;

L_State_3:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Remember, I want the best of fur!\"";
	set QUEST_WG_state, 4;
	close;

L_State_4:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Oh, my friend! Have you brought me some good furs?\"";
	if (countitem("WhiteFur") < 1) close;
	next;
	menu
		"Here, take a look!", L_State_4_try,
		"Yes, but I need it for something else.", -;
	close;

L_State_4_try:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Let me see, my friend! It needs to be perfect for my gloves!\"";
	mes "Agostine examines the patch of fur.";
	next;
	delitem "WhiteFur", 1;
	set @Temp1,rand(30);
	if (@Temp1 == 0) goto L_State_4_success;
	mes "Agostine throws away the white fur.";
	next;
	mes "[Agostine, The Legendary Tailor]";
	mes "\"It was awfully cut! You should pay more attention when you kill fluffies!\"";
	if (countitem("WhiteFur") < 1) close;
	next;
	menu
		"Here I have another one!", L_State_4_try,
		"Sorry, I will be careful...", -;
	close;

L_State_4_success:
	mes "Agostine turns into a smile";
	next;
	mes "[Agostine, The Legendary Tailor]";
	mes "\"It's perfect! Perfect! Good job, my friend! I will prepare your gloves right now!\"";
	next;
	set QUEST_WG_state, 5;
	goto L_State_5;

L_State_5:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Ehm, my friend, my work is not free. I need 15,000 GP for the work.\"";
	next;
	menu
		"You are not a tailor, you are a thief!!", -,
		"I think it's a reasonable price...", L_State_5_pay;
	close;

L_State_5_pay:
	if (zeny < 15000) goto L_State_5_nocash;
	getinventorylist;
	if (@inventorylist_count == 100) goto L_TooMany;
	set zeny, zeny - 15000;
	getitem "WinterGloves", 1;
	set QUEST_WG_state, 6;
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Here they are. You will have the more fashionable hands in the world!\"";
	close;

L_State_5_nocash:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Seems like you are out of cash. Come back when you have the money.\"";
	close;

L_State_6:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Oh, I remember you! Your gloves are sooo fashionable, my friend!";
	mes "Well, I have a good news for you. In this issue of 'TMW Fashion'";
	mes "I saw a pretty accessory, a pair of Furry Boots, my friend.";
	mes "I can make your own pair, if you want!\"";
	next;
	menu
		"It's a great idea!", L_State_10,
		"I'm glamourous enough, thanks", -;
	close;

// Start of Second Quest: Furry Boots - Needed: About 30 White Furs, 1 pair of Boots and 15.000 GP
L_State_10:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Perfect! For satisfying your request I need another perfect White Fur";
	mes "and a pair of Boots, like those you can find in the mines.";
	mes "They will make you lovely, my friend!\"";
	set QUEST_WG_state, 7;
	close;

L_State_11:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"So, my friend, have you brought me the right Fur?\"";
	if (countitem("WhiteFur") < 1) close;
	next;
	menu
		"Sure, I'm a fluffy hunter!", L_State_11_try,
		"Not yet, sorry.", -;
	close;

L_State_11_try:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"I'd rather see the patch of fur, first.\"";
	next;
	mes "Agostine examines the patch of fur.";
	next;
	delitem "WhiteFur", 1;
	set @Temp2,rand(30);
	if (@Temp2 == 0) goto L_State_11_success;
	mes "Agostine rips the white fur.";
	next;
	mes "[Agostine, The Legendary Tailor]";
	mes "\"That fur was terrible. I won't work with it!\"";
	if (countitem("WhiteFur") < 1) close;
	next;
	menu
		"Well, maybe this is better!", L_State_11_try,
		"I will hunt other fluffies...", -;
	close;

L_State_11_success:
	mes "Agostine turns into a smile";
	next;
	mes "[Agostine, The Legendary Tailor]";
	mes "\"This one is good enough, my friend. Good job.\"";
	delitem "WhiteFur", 1;
	set QUEST_WG_state, 8;
	goto L_State_12;

L_State_12:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"I need also a pair of Boots for the work.";
	mes "And my payment, obviously. 15,000 GP is right.\"";
	next;
	menu
		"Sure, here it is all.", L_State_12_pay,
		"I missed something, I will be back soon!", -;
	close;

L_State_12_pay:
	if (zeny < 15000) goto L_State_12_missing;
	if (countitem("Boots") < 1) goto L_State_12_missing;
	// No inventory check needed, as boots are removed, opening a slot
	set zeny, zeny - 15000;
	delitem "Boots", 1;
	getitem "FurBoots", 1;
	set QUEST_WG_state, 9;
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Enjoy your new boots, my friend!\"";
	close;

L_State_12_missing:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"Seems you have forgotten something. Check your inventory";
	mes "and your pockets!\"";
	close;

L_State_13:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"You are so glamourous, my dear friend!";
	mes "I have done a very good job on your clothes!\"";
	close;

L_TooMany:
	mes "[Agostine, The Legendary Tailor]";
	mes "\"You don't have anywhere to put them. Come back when you do.";
	close;
}
