//

009-2.gat,24,99,0	script	Inspector#Hurnscald	150,{
	if (Inspector == 0 && BaseLevel >= 30) goto L_NohMask_Start;
	if (Inspector == 1) goto L_NohMask_AskVillage;
	if (Inspector == 2) goto L_NohMask_OldWoman;
	if (Inspector == 3) goto L_NohMask_TheaterMask;
	if (Inspector == 4) goto L_NohMask_TravelingTroupe;
	if (Inspector == 5) goto L_NohMask_OldMan;
	if (Inspector == 6) goto L_NohMask_OldMan_2;
	if (Inspector == 7) goto L_NohMask_Alibi;
	if (Inspector == 8) goto L_NohMask_Alibi_2;
	if (Inspector == 9) goto L_NohMask_Alibi_3;
	if (Inspector == 10) goto L_NohMask_Satchel;
	if (Inspector == 11) goto L_NohMask_Basement;
	if (Inspector == 12) goto L_NohMask_Note;
	if (Inspector == 13) goto L_NohMask_TravelingTroupe_2;
	if (Inspector == 14) goto L_NohMask_Over;
	if (Inspector == 15) goto L_NohMask_End;
	mes "[Inspector]";
	mes "\"I'm sorry, but I'm busy looking into this string of robberies.\"";
	close;

L_NohMask_Start:
	mes "\"Hmm...what to do.\"";
	mes "He looks up and into your face.";
	next;

	mes "[Inspector]";
	mes "\"You look capable, will you help me solve these robberies?\"";
	next;

	menu
		"Yes", L_NohMask_Accept,
		"No", -;
	close;

L_NohMask_Accept:
	set Inspector, 1;
	mes "[Inspector]";
	mes "\"Ok then. I'd like you to ask the villagers about the recent string of robberies.\"";
	close;

L_NohMask_AskVillage:
	mes "[Inspector]";
	mes "\"Please continue talking to the villagers.\"";
	close;

L_NohMask_OldWoman:
	mes "[Inspector]";
	mes "\"Hm...I don't know if I trust her eyesight or memory. See if someone else knows anything.\"";
	close;

L_NohMask_TheaterMask:
	set Inspector, 4;
	mes "[Inspector]";
	mes "\"Someone in a theater mask, eh? There was a traveling theater troupe in town recently, but they've moved on to Tulimshar. Please go talk to their leader about this.\"";
	close;

L_NohMask_TravelingTroupe:
	mes "[Inspector]";
	mes "\"Please go talk to the leader of the traveling troupe about the theater mask.\"";
	close;

L_NohMask_OldMan:
	set Inspector, 6;
	mes "[Inspector]";
	mes "\"Hm...an old man? Could you interrogate him for me?\"";
	close;

L_NohMask_OldMan_2:
	mes "[Inspector]";
	mes "\"Have you talked with the old man yet?\"";
	close;

L_NohMask_Alibi:
	mes "[Inspector]";
	mes "\"Can you verify that with his wife?\"";
	close;

L_NohMask_Alibi_2:
	set Inspector, 9;
	mes "[Inspector]";
	mes "\"Hm...then it couldn't be him. I'm not sure where to go from here, maybe you can find something else. Try talking to everyone again.\"";
	close;

L_NohMask_Alibi_3:
	mes "[Inspector]";
	mes "\"Made any progress yet?\"";
	close;

L_NohMask_Satchel:
	mes "[Inspector]";
	mes "\"Then go north and investigate!\"";
	close;

L_NohMask_Basement:
	mes "[Inspector]";
	mes "\"Did you look over the basement?\"";
	close;

L_NohMask_Note:
	set Inspector, 13;
	mes "[Inspector]";
	mes "\"What a strange note. I'll keep track of this, while you return the mask to the troupe.\"";
	mes "";
	mes "\"By the way, stay sharp! I may call upon you again.\"";
	close;

L_NohMask_TravelingTroupe_2:
	mes "[Inspector]";
	mes "\"Please return the mask to the traveling troupe.\"";
	close;

L_NohMask_Over:
	set Inspector, 15;
	mes "[Inspector]";
	mes "\"My men have found all of the stolen items. They were all in the mining camp. We still don't know who did it.\"";
	mes "[2500 experience points]";
	getexp 2500, 0;
	close;

L_NohMask_End:
	mes "[Inspector]";
	mes "\"Remember to stay sharp. I might need your help on another case.\"";
	close;
}
