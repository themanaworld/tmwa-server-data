//

001-1.gat,110,71,0	script	Sandra	114,{
	if(Scorp == 2) goto L_Done;
	if(Scorp == 1) goto L_Progress;

	set @TEMP, rand(4);
	if(@TEMP == 0) goto L_Opening0;
	if(@TEMP == 1) goto L_Opening1;
	if(@TEMP == 2) goto L_Opening2;
	if(@TEMP == 3) goto L_Opening3;

L_Opening0:
	mes "[Sandra]";
	mes "\"In the outskirts of Tulimshar, there are some scorpions. I need a favor, please help me. Will you?\"";
	next;
	goto L_Ask;

L_Opening1:
	mes "[Sandra]";
	mes "\"When you venture in the outskirts of Tulimshar, you can spot scorpions. Will you help me kill some?\"";
	next;
	goto L_Ask;

L_Opening2:
	mes "[Sandra]";
	mes "\"The scorpion stinger carries many properties used in potions. Will you get some for me?\"";
	next;
	goto L_Ask;

L_Opening3:
	mes "[Sandra]";
	mes "\"You look sturdy enough, will you help me get something?\"";
	next;
	goto L_Ask;

L_Ask:
	menu
		"Yes", L_Yes,
		"No", L_No;

L_Yes:
	set @TEMP, rand(3);
	if(@TEMP == 0) goto L_Req0;
	if(@TEMP == 1) goto L_Req1;
	if(@TEMP == 2) goto L_Req2;

L_Req0:
	mes "[Sandra]";
	mes "\"I need 5 [Scorpion Stinger]s and 1 [Red Scorpion Stinger].\"";
	next;
	goto L_Set;

L_Req1:
	mes "[Sandra]";
	mes "\"I heard a while ago that scorpion stingers can be used for medical purposes. I need you to help me get 5 [Scorpion Stinger]s and 1 [Red Scorpion Stinger].\"";
	next;
	goto L_Set;

L_Req2:
	mes "[Sandra]";
	mes "\"Bring me 5 [Scorpion Stinger]s and 1 [Red Scorpion Stinger]. I'll give you something if you do.\"";
	next;
	goto L_Set;

L_Set:
	set Scorp,1;
	mes "[Sandra]";
	mes "\"Please bring me them!\"";
	close;

L_Progress:
	if (countitem("ScorpionStinger") < 5) goto L_NotEnough;
	if (countitem("RedScorpionStinger") < 1) goto L_NotEnough;
	mes "[Sandra]";
	mes "\"Excellent!";
	mes "You brought me 5 [Scorpion Stinger]s and 1 [Red Scorpion Stinger]!\"";
	getinventorylist;
	if ((@inventorylist_count - (countitem("ScorpionStinger") == 5) - (countitem("RedScorpionStinger") == 1)) > 99 - (countitem("Arrow") == 0)) goto L_TooMany;
	delitem "ScorpionStinger", 5;
	delitem "RedScorpionStinger", 1;
	getitem "Bow", 1;
	getitem "Arrow", 100;
	set Scorp, 2;
	close;

L_NotEnough:
	mes "[Sandra]";
	mes "\"Please hurry and bring me 5 [Scorpion Stinger]s and 1 [Red Scorpion Stinger].\"";
	close;

L_Done:
	mes "[Sandra]";
	mes "\"Thank you for all your help!\"";
	close;

L_No:
	close;

L_TooMany:
	next;
	mes "[Sandra]";
	mes "\"You don't have room for my reward. I'll wait until you do.\"";
	close;
}
