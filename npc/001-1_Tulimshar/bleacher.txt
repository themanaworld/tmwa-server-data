//

001-1.gat,27,53,0	script	Candide	156,{
	mes "[Candide]";
	mes "\"I've figured out how that volcanic ash bleaches cloth.";
	mes "I could do it for you, but I'll need three piles of ash and 5,000 GP for supplies per item.\"";
	next;

	if (countitem(701) < 3) goto L_no_ash;

	if (zeny < 5000) goto L_no_money;

	menu "I'd like to bleach something", -,
		 "No thanks", L_close;

L_bleach_menu:
	mes "[Candide]";
	mes "\"What would you like to bleach?\"";
	next;

	menu
		"Cotton shirt", L_cottonshirt,
		"V-Neck sweater", L_vneck,
		"Turtleneck sweater", L_tneck,
		"Cotton shorts", L_shorts,
		"Cotton skirt", L_skirt,
		"Miniskirt", L_miniskirt,
		"Tank top ", L_tanktop,
		"Short tank top", L_tanktop_short,
		"Silk robe", L_robe,
		"Cotton headband", L_cotton_headband,
		"Desert hat", L_desert_hat,
		"Cotton boots", L_cotton_boots,
		"Cotton gloves", L_cotton_gloves,
		"Nevermind", -;
	goto L_close;

L_cottonshirt:	set @normal, 1202;
	set @dyeBase, 2050;
	goto L_choose_color;

L_vneck:	set @normal, 624;
	set @dyeBase, 2060;
	goto L_choose_color;

L_tneck:	set @normal, 564;
	set @dyeBase, 2070;
	goto L_choose_color;

L_shorts:	set @normal, 586;
	set @dyeBase, 2110;
	goto L_choose_color;

L_skirt:	set @normal, 632;
	set @dyeBase, 2100;
	goto L_choose_color;

L_miniskirt:	set @normal, 771;
	set @dyeBase, 2170;
	goto L_choose_color;

L_tanktop:	set @normal, 688;
	set @dyeBase, 2090;
	goto L_choose_color;

L_tanktop_short:	set @normal, 689;
	set @dyeBase, 2120;
	goto L_choose_color;

L_robe:
	set @normal, 720;
	set @dyeBase, 2080;
	goto L_choose_color;

L_cotton_headband:
	set @normal, 724;
	set @dyeBase, 2140;
	goto L_choose_color;

L_desert_hat:
	set @normal, 723;
	set @dyeBase, 2130;
	goto L_choose_color;

L_cotton_boots:
	set @normal, 735;
	set @dyeBase, 2150;
	goto L_choose_color;

L_cotton_gloves:
	set @normal, 741;
	set @dyeBase, 2160;
	goto L_choose_color;

L_choose_color:
	mes "[Candide]";
	mes "\"And the color?\"";
	next;

	menu "Red", -,
		 "Green", -,
		 "Dark Blue", -,
		 "Yellow", -,
		 "Light Blue", -,
		 "Pink", -,
		 "Black", -,
		 "Orange", -,
		 "Purple", -,
		 "Dark Green", -;
	 
	set @del, @dyeBase + @menu - 1;

	if (countitem(@del) == 0) goto L_no_item;

	if (countitem(701) < 3) goto L_no_ash;

	if (zeny < 5000) goto L_no_money;

	delitem @del, 1;
	getitem @normal, 1;
	delitem 701, 3;
	set zeny, zeny - 5000;

L_again:
	mes "[Candide]";
	mes "\"Would you like to bleach something else?\"";
	next;

	menu "Yes", L_bleach_menu,
		 "No", L_close;

L_no_item:
	mes "[Candide]";
	mes "\"You don't have one of those.\"";
	next;

	goto L_again;

L_no_ash:
	mes "[Candide]";
	mes "\"You don't have enough ash for me to bleach anything.";
	mes "I need three piles.\"";
	next;

	goto L_close;

L_no_money:
	mes "[Candide]";
	mes "\"You don't have enough gold for me to bleach anything.";
	mes "I need 5,000 GP for supplies.\"";
	next;

L_close:
	mes "[Candide]";
	mes "\"Come again.\"";
	close;
}
