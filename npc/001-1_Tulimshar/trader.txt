//

001-1.gat,57,33,0	script	Trader	115,{
	set @TRADE_SKILL, 2;
	if (getskilllv(@TRADE_SKILL) > 0) goto L_Has;

	mes "[Trader]";
	mes "\"Hello. I came here to trade wares with the people of Tulimshar. Unfortunately for you, I've traded everything I had.\"";
	next;
	menu
		"Oh. I'll go then.", -,
		"You don't have anything?", L_More;
	close;

L_More:
	mes "[Trader]";
	mes "\"No. I have nothing for you. Except...\"";
	next;
	menu
		"Yes?", L_Except,
		"Ok then.",  -;
	close;

L_Except:
	mes "[Trader]";
	mes "\"I could teach you how to trade.  It'll cost you 5GP.\"";
	next;
	menu
		"Sure", L_Teach,
		"No thank you", -;
	close;

L_Teach:
	if (zeny < 5) goto L_NotEnoughMoney;
	set zeny, zeny - 5;
	setskill @TRADE_SKILL, 1;
	mes "[Trader]";
	mes "\"You can initiate trade with someone by right-clicking on them and choosing trade.";
	mes "You'll both add the items and set the GP you're putting up then press propose trade.";
	mes "After both parties have proposed their side, you can both review the trade, and then accept or reject by closing the window.\"";
	next;
	mes "[Trader]";
	mes "\"Items added to the trade cannot be removed, and so mistakes have to be dealt with by canceling the trade.";
	mes "You need to press the change button to let the other person know about GP changes.\"";
	close;

L_Has:
	mes "[Trader]";
	mes "\"I still don't have anything to trade. I wonder where that shipment is....\"";
	close;

L_NotEnoughMoney:
	mes "[Trader]";
	mes "\"You don't have enough GP\"";
	close;
}
