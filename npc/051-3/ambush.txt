
051-3,40,52,0	script	#WayBlockedTrigger	32767,2,2,{
    message strcharinfo(0), "There is no way I can pass here. Let's find another way.";
    end;
}

051-3,31,70,0	script	#AmbushReadyTrigger	32767,2,2,{
    @step = 2;
    if ($@illia_progress < @step) goto L_ShouldNotBeHere;
    if (strcharinfo(0) == $@ILLIA_HERO$ && $@illia_level_2_progress == 0) goto L_PrepareAmbush;
    end;

L_PrepareAmbush:
    disablenpc "Sneaky Bandit";
    disablenpc "Another Sneaky Bandit";
    end;

L_ShouldNotBeHere:
    percentheal -100, 0;
    end;

}

051-3,31,93,0	script	#BanditAmbushTrigger	32767,3,3,{
    @step = 2;
    if ($@illia_progress < @step) goto L_ShouldNotBeHere;
    if ($@illia_level_2_progress == 0 && strcharinfo(0) == $@ILLIA_HERO$) goto L_MakeAmbush;
    end;

L_MakeAmbush:
    $@illia_level_2_progress = 1;
    donpcevent "#BndtTl::OnCommandAmbush";
    end;

L_ShouldNotBeHere:
    percentheal -100, 0;
    message strcharinfo(0), "Your throat got cut by a sneaky bandit!";
    end;
}

051-3,1,1,0	script	#BndtTl	127,{
    end;

OnCommandAmbush:
    initnpctimer;
    end;

OnTimer1000:
    enablenpc "Sneaky Bandit";
    donpcevent "Sneaky Bandit::OnCommandTalk";
    enablenpc "Another Sneaky Bandit";
    donpcevent "Another Sneaky Bandit::OnCommandTalk";
    end;

OnTimer9000:
    areawarp "051-3", 22, 45, 42, 97, "051-3", 101, 13;
    end;

OnTimer11000:
    areatimer "051-3", 22, 12, 102, 97, 500, "#BndtTl::OnOuch";
    areatimer "051-3", 22, 12, 102, 97, 3000, "#BndtTl::OnA";
    stopnpctimer;
    setnpctimer 0;
    end;

OnOuch:
    message strcharinfo(0), "Ouch my head...";
    end;

OnA:
    if (strcharinfo(0) == $@ILLIA_HERO$)
        goto L_CaptureHero;
    if (strcharinfo(0) == $@ILLIA_HELPER1$)
        goto L_CaptureHelper1;
    if (strcharinfo(0) == $@ILLIA_HELPER2$)
        goto L_CaptureHelper2;
    if (strcharinfo(0) == $@ILLIA_HELPER3$)
        goto L_CaptureHelper3;
    // kill that player otherwise, since not part of the quest.
    percentheal -100,0;
    end;

L_CaptureHero:
    // Simulate the capture of the player.
    // Unequip all equipments
    nude;
    // put the health at 40%.
    percentheal 100, 0;
    percentheal -60, 0;
    warp "051-3", 69, 109;
    message strcharinfo(0), "Meh... Where am I?";
    end;

L_CaptureHelper1:
    // Simulate the capture of the player.
    // Unequip all equipments
    nude;
    // put the health at 40%.
    percentheal 100, 0;
    percentheal -60, 0;
    warp "051-3", 64, 102;
    message strcharinfo(0), "Meh... Where am I?";
    end;

L_CaptureHelper2:
    // Simulate the capture of the player.
    // Unequip all equipments
    nude;
    // put the health at 40%.
    percentheal 100, 0;
    percentheal -60, 0;
    warp "051-3", 62, 102;
    message strcharinfo(0), "Meh... Where am I?";
    end;

L_CaptureHelper3:
    // Simulate the capture of the player.
    // Unequip all equipments
    nude;
    // put the health at 40%.
    percentheal 100, 0;
    percentheal -60, 0;
    warp "051-3", 58, 102;
    message strcharinfo(0), "Meh... Where am I?";
    end;

}

051-3,29,94,0	script	Sneaky Bandit	127,{
    end;

OnCommandTalk:
    initnpctimer;
    end;

OnTimer100:
    npctalk "Did you think you could sneak in our den without a problem?";
    end;

OnTimer3600:
    npctalk "Drop your weapons, and come with us!";
    stopnpctimer;
    setnpctimer 0;
    end;

}

051-3,33,95,0	script	Another Sneaky Bandit	127,{
    end;

OnCommandTalk:
    initnpctimer;
    end;

OnTimer1500:
    npctalk "W00t! More slaves!";
    end;

OnTimer5500:
    npctalk "Ahaha, that was easy! Ok, knock them out!";
    stopnpctimer;
    setnpctimer 0;
    end;

}
