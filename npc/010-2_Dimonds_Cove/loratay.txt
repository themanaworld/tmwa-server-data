010-2.gat,85,85,0	script	Lora Tay	151,{

        set @ROBE_COCOONS_NR, 150; // must be multiple of ten
        set @ROBE_ZENY, 10000;

        if (QUEST_WG_state == 10)
           goto L_agostine_menu;
        if (QUEST_WG_state >= 11)
           goto L_regular_intro;

	set @t, rand(3);
        if (@t == 0) goto L_intro_0;
        if (@t == 1) goto L_intro_1;

	mes "You see a middle-aged woman furiously sewing away at what appears to be a very expensive dress.  Her fingers are moving so nimbly that you find it near-impossible to follow the needle.";

        goto L_intro_over;
L_intro_0:
        mes "You notice a middle-aged woman carefully drawing lines on a large sheet of cotton.";

        goto L_intro_over;
L_intro_1:
        mes "You see a middle-aged woman cautiously but surprisingly swiftly cutting out shapes from a huge cotton sheet.  The scissors glide along the cloth as if it were butter, never leaving the premarked lines.";

L_intro_over:
        next;

        mes "She suddenly stops and looks up.";
        next;

        mes "\"Now who are you, and what are you doing here?\"";
        mes "She squints, pushes her thick spectacles up the bridge of her nose and eyes you suspiciously.";
        next;

        set @agostine_msg0$, "";
        set @agostine_msg1$, "";

        if (QUEST_WG_state >= 1)
                set @agostine_msg0$, "Do you know Agostine?";
        if ((QUEST_WG_state >= 9) && (countitem(655)))
                set @agostine_msg1$, "Can you make fur boots, like Agostine?";

        menu "Hello!  My name is " + strcharinfo(0) + ".", L_hello_0,
             "Hello; are you a tailor?", L_hello_1,
             "Can you make something for me?", L_hello_2,
             @agostine_msg0$, L_agostine_0,
             @agostine_msg1$, L_agostine_1;

L_hello_0:
        mes "She raises an eyebrow.";
        mes "\"That,\", she notes dryly, \"is hardly my fault.  And now I would greatly appreciate it if you were to get out of here and leave me to my work!\"";
        close;

L_hello_1:
        mes "She frowns.";
        mes "\"No, I am an accupuncturist, I just happen to practice on cloth!  Get out of here with your silly questions!\"";
        close;

L_hello_2:
        mes "She glares at you indignantly.";
        mes "\"Do you even know whom you are talking to?  I am Lora Tay, greatest of the seven seamstresses of the South!\"";
        next;
        mes "[Lora Tay the Seamstress]";
        mes "\"I do not work for petty peasants or arrogant adventurers.  Get out of my sight!\"";
        mes "She waves you away and returns to her work.";
        close;


L_agostine_0:
        if (QUEST_WG_state < 1) close;
        mes "She shoots an incensed glare at you.";
        mes "\"Ah, Agostine!  The 'legendary tailor'!  Now who has not heard of him, been inspired by him!  Such charisma!  Such popularity!  Such unprecedented lack of talent!\"";
        mes "You notice that she prononuces the 't's as if she were trying to slap someone.";
        next;
        mes "\"Please spare me your tales of this fool.  He couldn't mend a sock without getting himself trapped inside!\"";
        mes "She frowns and turns back to her work, clearly not interested in discussing the matter further.";
        close;

L_agostine_1:
        if ((QUEST_WG_state < 9) || (countitem (655) == 0)) close;
        mes "[Lora Tay the Seamstress]";
        mes "She glares at your boots, her lips forming barely more than a thin line.  Deep furrows form above her brows.";
        mes "\"I can't believe it!  He copied Illana's design but used fluffy fur in place of ice wolf fur-- Fluffy fur!  Of all things!  Who would wear such an atrocity of fashion!  Everyone knows that you need ice-white, not pale-white, to go with such leather!\"";
        next;
        mes "[Lora Tay the Seamstress]";
        mes "She looks at them more closely.";
        mes "\"This insolent upstart!  Even worse, he copied my very own double-backward cross-stitch pattern... I can't believe it!\"";
        next;
        mes "[Lora Tay the Seamstress]";
        mes "Angry red spots begin to form on her cheeks.";
        mes "\"Get those... things out of my sight before I forget myself!\"";

        if (QUEST_WG_state == 9)
           set QUEST_WG_state, 10;

        close;

L_agostine_menu:
        mes "[Lora Tay the Seamstress]";
        mes "The seamstress looks up at you from her work.";
        mes "\"I do hope that you have not had any more dealings with this... this lumberjack of a tailor?\"";
        next;

        if (countitem(563) > 0)
                menu "You mean Agostine?", L_agostine_3,
                      "Actually, he also made me gloves...", L_agostine_2,
                      "Why don't you like him?", L_agostine_4,
                      "No, none, really.", L_agostine_5;

         menu "You mean Agostine?", L_agostine_3,
              "Why don't you like him?", L_agostine_4,
              "No, none, really.", L_agostine_5;

L_agostine_2:
        if ((QUEST_WG_state < 9) || (countitem(563) == 0)) close;
        mes "[Lora Tay the Seamstress]";
        mes "Lora inspects your gloves, shock apparent on her face.";
        mes "\"Winter gloves?  And he managed the join stitches without adding a single crease... no, that can't be the Agostine we were talking about...\"";
        mes "Without asking, she takes your gloves and inspects them further.";
        next;

        mes "[Lora Tay the Seamstress]";
        mes "\"Triple inverted crossbar... he stole another of my stitching patterns!  Except... no, that couldn't hold... what kind of yarn...\"";
        mes "Torn between excitement, worry, and latent disgust, she frantically turns the gloves from side to side, looking for obvious flaws, her face reddening.";
        next;

        mes "[Lora Tay the Seamstress]";
        mes "\"No, it's perfect... Inconceivable!  How could that little upstart... I don't believe it!\"";
        mes "Breathing heavily with something that might be anger or confusion, she turns them inside out.";
        next;

        mes "[Lora Tay the Seamstress]";
        mes "She slides her fingers over the now-exposed inside whilst the color of her face approaches that of an over-ripe tomato.";
        mes "\"Almost perfectly smooth...\", she whispers.";
        next;

        mes "[Lora Tay the Seamstress]";
        mes "She jumps up in a fit of sudden rage.";
        mes "\"That's impossible!  Where did he learn to sew like that!  How did he cut the shapes so perfectly!  When did he...";
        mes "Suddenly, she breaks into a cough.";
        next;

        mes "[Lora Tay the Seamstress]";
        mes "\"*cough* ... can't possibly... *cough*...\"";
        mes "Lora sinks back on her chair, trying to control her cough.";
        next;

        if (countitem(541))
           menu "(sneak out of the room)", L_end,
                "Are you alright?", -,
                "Here, have a bottle of water...", L_give_water,
                "Can I help you?", -;

        if (countitem(541) == 0)
           menu "(sneak out of the room)", L_end,
             "Are you alright?", -,
             "Can I help you?", -;

        mes "[Lora Tay the Seamstress]";
        mes "She holds out your gloves for you to pick up, then waves you out of the room.  Fortunately, her cough seems to be slowly subsiding.";
        close;

L_agostine_3:
        mes "[Lora Tay the Seamstress]";
        mes "She frowns at you.";
        mes "\"What other self-absorbed tailor-pretends with the sewing skill of a mouboo are there around?  Stop wasting my time.\"";
        close;

L_agostine_4:
        mes "[Lora Tay the Seamstress]";
        mes "\"Is it not obvious?  We are talking about someone so self-absorbed that he chose to ignore the advice of myself, greatest of the seven southern seamstresses!  I cannot stand such arrogance.\"";
        close;

L_agostine_5:
        mes "[Lora Tay the Seamstress]";
        mes "\"Good for you.  That arrogant good-for-nothing couldn't tell a piece of cotton from chain mail if wiped his nose with it!\"";
        close;

L_no_water:
        mes "Checking more closely, you realize that you are out of water.";
        mes "You apologise to Lora, though it fortunately seems that her cough is subsiding.";
        mes "She waves you out of the room.";
        close;

L_regular_intro:
        mes "[Lora Tay the Seamstress]";
        mes "\"Ah, you again.  Is there anything in particular you need?\"";
        next;
        goto L_main_menu;

L_give_water:
        if (countitem(541) == 0) goto L_no_water;
        delitem 541, 1;
        set QUEST_WG_state, 11;

        set @xpval, 50000;

        getexp @xpval, 0;
        mes "[Lora Tay the Seamstress]";

        mes "Lora grabs your water bottle and take a deep sip.  Slowly, her head regains a more natural color.  As she puts the bottle down, her cough seems to have subsided.";
        mes "\"Thank you, dear; that was just the right thing at the right time.\"";
        mes "[You gain " + @xpval + " experience points]";
        next;

        mes "[Lora Tay the Seamstress]";
        mes "She hands you the winter gloves again.";
        mes "\"Now what happened I don't know, but this wasn't made by the Agostine I remember.  But I shan't be made a fool out of by one such as him.\"";
        next;

        mes "[Lora Tay the Seamstress]";
        mes "\"If HE can sew petty every-day items for ordinary peasants, then so can I.  Very well, is there anything in particular that you need made?  I shall show you how a proper seamstress operates!\"";
        next;

L_main_menu:
        menu "Can you trim something for me?", L_trim,
             "Can you lengthen something for me?", L_lengthen,
             "Can you make me a shirt?", L_shirt,
             "Can you make me a tank top?", L_tanktop,
             "Can you make me a cape?", L_cape,
             "Can you make fur boots for me?", -,
             "Can you make winter gloves me?", -,
             "Can you sew a robe for me?", L_robe,
             "Can you make a desert hat for me?", L_desert_hat,
             "Goodbye for now!", L_end;

        mes "[Lora Tay the Seamstress]";
        mes "\"From what?  Fluffy fur?  I won't work with such lowly materials.\"";
        next;
        goto L_main_menu;

L_desert_hat:
        mes "[Lora Tay the Seamstress]";
        mes "The seamstress sighs.";
        mes "\"One of these days it would be nice to have an actual challenge... yes, of course I can make a desert hat.  A [Cotton headband], three pieces of [cotton cloth].  Hmm.  And let's add 300 GP to that.\"";
        next;

        set @default_choice$, "Never mind.";
        setarray @items, 724, 2140, 2141, 2142, 2143, 2144, 2145, 2146, 2147, 2148, 2149;
        setarray @item_names$, "Here is a white headband", "Here is a red headband", "Here is a green headband", "Here is a dark blue headband", "Here is a yellow headband", "Here is a light blue headband", "Here is a pink headband", "Here is a black headband", "Here is a orange headband", "Here is a puple headband", "Here is a dark green headband";
        set @items_nr, 11;

        callsub SUB_pick_one_of_many_items;
        if (@item == 0) goto L_main_menu;

        if (@item == 724)
           set @genitem, 723;
        if (@item != 724)
           set @genitem, @item - 10;

        if (countitem(@item) < 1)
        	goto L_desert_hat_noheadband;
        if (countitem("CottonCloth") < 3)
        	goto L_desert_hat_nocotton;
        if (zeny < 300)
        	goto L_desert_hat_nogp;

        delitem @item, 1;
        delitem "CottonCloth", 3;
        set zeny, zeny - 300;
        getitem @genitem, 1;
        mes "[Lora Tay the Seamstress]";
        mes "\"Now then, this should only take a minute.\"";
        mes "The seamstress folds your cotton cloth in a complicated fashion, wraps the resulting bundle into and out of the headband, and finally sews together several loose ends.";
        next;
        mes "[Lora Tay the Seamstress]";
        mes "The result resembles a huge knot.";
        mes "Lora picks two ends and pulls, hard-- to your amazement, the knot unfolds, yielding a desert hat.";
        mes "\"Here you are.\"";
        next;
	goto L_main_menu;

L_desert_hat_nocotton:
        mes "[Lora Tay the Seamstress]";
        mes "\"You're asking me to make a desert hat without enough cloth?  Here, have your headband back-- see, that's how much I can do without cloth!  And since I'm generous today, you can even keep your GP...\"";
        next;
	goto L_main_menu;

L_desert_hat_nogp:
        mes "[Lora Tay the Seamstress]";
        mes "\"No, no, no.  300 GP.  That's not that much, I've been told.  I can't just work for free, now can I?\"";
        next;
	goto L_main_menu;

L_desert_hat_noheadband:
        mes "[Lora Tay the Seamstress]";
        mes "\"Now that is odd.  Where did your headband vanish to?  Well, I'm not going to make you something as silly as a headband, so get one from elsewhere.\"";
        next;
	goto L_main_menu;

L_trim:
        mes "[Lora Tay the Seamstress]";
        mes "\"Trimming... you mean that you can't do that yourself?  Well, alright... I suppose I should charge 100 GP and do it.\"";
        mes "\"Alright.  What is it that you want trimmed?\"";
        next;

        set @default_choice$, "Never mind.";
        setarray @items, 1202, 2050, 2051, 2052, 2053, 2054, 2055, 2056, 2057, 2058, 2059,
                          688, 2090, 2091, 2092, 2093, 2094, 2095, 2096, 2097, 2098, 2099;
        setarray @item_names$, "cotton shirt", "red cotton shirt", "green cotton shirt", "dark blue cotton shirt", "yellow cotton shirt", "light blue cotton shirt", "pink cotton shirt", "black cotton shirt", "orange cotton shirt", "puple cotton shirt", "dark green cotton shirt",
                               "tank top", "red tank top", "green tank top", "dark blue tank top", "yellow tank top", "light blue tank top", "pink tank top", "black tank top", "orange tank top", "purple tank top", "dark green tank top";
        set @items_nr, 22;
        callsub SUB_pick_one_of_many_items;
        if (@item == 0) goto L_main_menu;

        set @delitem, @item;
        set @genitem, 0;
        if (@item == 1202) set @genitem, 688;
        if (@item == 688) set @genitem, 689;
        if (@item >= 2090 && @item <= 2099) set @genitem, @item + 30;
        if (@item >= 2050 && @item <= 2059) set @genitem, @item + 40;

        if (@genitem == 0) goto L_trim_impossible;
        if (zeny < 100) goto L_trim_nozeny;

        delitem @delitem, 1;
        getitem @genitem, 1;
        set zeny, zeny - 100;
        mes "[Lora Tay the Seamstress]";
        mes "";
        mes "\"There you are, dear.\"";
        next;
        goto L_main_menu;

L_trim_impossible:
        mes "[Lora Tay the Seamstress]"; // INTERNAL ERROR
        mes "\"I don't really want to shorten this any more.\"";
        next;

        goto L_main_menu;

L_trim_nozeny:
        mes "[Lora Tay the Seamstress]";
        mes "\"Not enough GP?  Well, just do it yourself-- trimming really is trivial.\"";
        next;

        goto L_main_menu;

L_lengthen:
        mes "[Lora Tay the Seamstress]";
        mes "\"Naturally, I will need a piece of [cotton cloth] to sew onto it; I must also charge you some... hmm... does 500 GP sound reasonable?  Yes, I think I shall charge that.\"";
        mes "\"Now, let me see.  What is it that you want lengthened?\"";
        next;

        set @default_choice$, "Never mind.";
        setarray @items, 688, 2090, 2091, 2092, 2093, 2094, 2095, 2096, 2097, 2098, 2099,
                         689, 2120, 2121, 2122, 2123, 2124, 2125, 2126, 2127, 2128, 2129;
        setarray @item_names$, "tank top", "red tank top", "green tank top", "dark blue tank top", "yellow tank top", "light blue tank top", "pink tank top", "black tank top", "orange tank top", "purple tank top", "dark green tank top",
                               "short tank top", "red short tank top", "green short tank top", "dark blue short tank top", "yellow short tank top", "light blue short tank top", "pink short tank top", "black short tank top", "orange short tank top", "purple short tank top", "dark green short tank top";
        set @items_nr, 22;
        callsub SUB_pick_one_of_many_items;
        if (@item == 0) goto L_main_menu;

        set @delitem, @item;
        set @genitem, 0;
        if (@item == 688) set @genitem, 1202;
        if (@item == 689) set @genitem, 688;
        if (@item >= 2120 && @item <= 2129) set @genitem, @item - 30;
        if (@item >= 2090 && @item <= 2099) set @genitem, @item - 40;

        if (@genitem == 0) goto L_lengthen_impossible;
        if (zeny < 500) goto L_lengthen_nozeny;
        if (countitem ("CottonCloth") < 1) goto L_lengthen_nocotton;

        delitem @delitem, 1;
        delitem "CottonCloth", 1;
        getitem @genitem, 1;
        set zeny, zeny - 500;
        mes "[Lora Tay the Seamstress]";
        mes "The seamstress cuts your piece of cotton cloth into stripes and sews them on.  Using some odd liquid, she flattens the seams and borders.";
        if (@delitem > 2000) // dyed
           mes "She then applies another liquid-- smelling strangely of fermented apples-- to the result, wraps it up, and wrings it tightly.  As she unwraps it, you observe to your amazement that the dye has spread to the newly attached area.";
        mes "\"There you are, dear.\"";
        next;
        goto L_main_menu;

L_lengthen_nocotton:
        mes "[Lora Tay the Seamstress]";
        mes "\"Who do you think I am?  The cotton fairy?  Get yourself some cloth before you ask me to sew it on.\"";
        next;

        goto L_main_menu;

L_lengthen_nozeny:
        mes "[Lora Tay the Seamstress]";
        mes "\"No gold, no sewing.\"";
        next;

        goto L_main_menu;

L_lengthen_impossible:
        mes "[Lora Tay the Seamstress]"; // INTERNAL ERROR
        mes "\"I really can't lengthen that any more.\"";
        next;

        goto L_main_menu;

L_shirt:
        mes "[Lora Tay the Seamstress]";
        mes "\"Oh, how silly.  You can by those everywhere.  Please don't bore me with such trifles, dear.\"";
        next;

        goto L_main_menu;

L_tanktop:
        mes "[Lora Tay the Seamstress]";
        mes "\"A tank top?  Well, hardly a challenge, but I suppose I could do that.  Please get me some cloth-- plain cotton will do-- and, let's see, how does 100,000 GP for my efforts sound?\"";
        mes "\"Wait, I keep forgetting, you are just an adventurer... let's make that 1000 GP, then.\"";
        next;

L_tanktop_menu:
        menu "I think I would like something else.", L_main_menu,
             "A tank top, please (6 [cotton cloth])", L_tanktop_long,
             "A short tank top, please (5 [cotton cloth])", L_tanktop_short,
             "Goodbye for now.", L_end;

L_tanktop_short:
        if (countitem ("CottonCloth") < 5) goto L_tanktop_insufficient_cloth;
        if (zeny < 1000) goto L_tanktop_insufficient_zeny;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem("CottonCloth") > 5) goto L_TooMany;
        set zeny, zeny - 1000;
        delitem "CottonCloth", 5;
        getitem "ShortTankTop", 1;
        mes "[Lora Tay the Seamstress]";
        mes "You watch as Lora sews the pieces of cloth together, then flattens the seams and ends with some odd liquid.";
        mes "Amazingly, the result looks like a single piece of cloth.";
        mes "After little more than a few elegant cuts and folds, your tank top is ready.";
        mes "\"Oh dear, it seems that I am done already.  Do you need anything else?\"";
        next;
        goto L_tanktop_menu;

L_tanktop_long:
        if (countitem ("CottonCloth") < 6) goto L_tanktop_insufficient_cloth;
        if (zeny < 1000) goto L_tanktop_insufficient_zeny;
	getinventorylist;
	if (@inventorylist_count == 100 && countitem("CottonCloth") > 6) goto L_TooMany;
        set zeny, zeny - 1000;
        delitem "CottonCloth", 6;
        getitem "TankTop", 1;
        mes "[Lora Tay the Seamstress]";
        mes "After combining your pieces of cloth into one-- miraculously making the seams and stitches vanish-- the seamstress cuts out all that doesn't belong into a tank top, then folds and stabilises the edges with additional seam lines.";
        mes "\"There you are.  Don't worry, I have trimmed it to match your size perfectly.\"";
        next;
        goto L_tanktop_menu;

L_tanktop_insufficient_cloth:
        mes "[Lora Tay the Seamstress]";
        mes "\"No, no, no, count again-- how much [cotton cloth] did I say I needed?  That's not enough.\"";
        next;
        goto L_tanktop_menu;

L_tanktop_insufficient_zeny:
        mes "[Lora Tay the Seamstress]";
        mes "As you note that you don't have enough GP on you, Lora rolls her eyes.";
        mes "\"I don't work for free, you know.  I already gave you a discounted peasant price.\"";
        next;
        goto L_main_menu;

L_cape:
        mes "[Lora Tay the Seamstress]";
        mes "She shakes her head.";
        mes "\"No capes.  You're an adventurer, right? Do you want to get caught by a closing portcullis?  Or strangled when your cape gets tangled in a tree?  Or brought down by a mushroom stomping on your cape while you're trying to run away?  No capes.\"";
        next;

        goto L_main_menu;

L_robe:
        mes "[Lora Tay the Seamstress]";
        mes "\"A robe?  Well, as long as you're not asking me to make it out of cotton...\"";
        next;

        if (countitem("SilkCocoon") == 0)
        	goto L_robe_nosilk;

L_robe_menu:
        menu "Here, I have some silk cocoons!", -,
             "Here, I have some silk sheets!", L_robe_nosilk,
             "Where can I find silk?", L_where_silk,
             "How much silk do you need?", L_how_much_silk,
             "Oh, never mind.", L_main_menu;

        mes "[Lora Tay the Seamstress]";
        mes "The seamstress stares at you as if you had gone out of your mind.";
        mes "\"And what precisely do you expect me to do with [silk cocoons]?  String them together in some baubly chain?  Somehow remove the thread and spin and weave it so that some random person can have their robe?\"";
        next;

        menu "Will you trade cocoons for sheets?", -,
             "Never mind.", L_main_menu;

        mes "[Lora Tay the Seamstress]";
        mes "Lora frowns.";
        mes "\"It's not all that easy to get properly processed silk around here.  Easier for me than for you, I admit...\"";
        next;

        mes "[Lora Tay the Seamstress]";
        mes "She sighs.";
        mes "\"All right, very well then.  I have enough spare silk sheets right now, but you pay the silk processing for your cocoons.\"";
        next;

        mes "[Lora Tay the Seamstress]";
        mes "\"Let's see... " + @ROBE_COCOONS_NR + " silk cocoons and " + @ROBE_ZENY + " GP should just about cover that.\"";
        next;

        menu "I'm not interested.", L_main_menu,
             "Very well then, here you are.", -;

        if (countitem("SilkCocoon") < @ROBE_COCOONS_NR)
		goto L_robe_missing_cocoons;

        if (zeny < @ROBE_ZENY)
		goto L_robe_missing_zeny;

	getinventorylist;
	if (@inventorylist_count == 100 && countitem("SilkCocoon") > @ROBE_COCOONS_NR) goto L_TooMany;

        set zeny, zeny - @ROBE_ZENY;
        delitem "SilkCocoon", @ROBE_COCOONS_NR;
        getitem "SilkRobe", 1;
        mes "[Lora Tay the Seamstress]";
        mes "The seamstress puts your cocoons and GP away, takes your measurements and pulls out several silk sheets.  She asks you to stretch out your arms, then pins the silk sheets in place all over your body.";
        next;

        mes "[Lora Tay the Seamstress]";
        mes "She proceeds to make a number of mysterious marks on the sheets with a charcoal pen.  Meanwhile, your arms are getting tired, but you're afraid to lower them-- she used a lot of pins to put everything into place, and you lost track of where they went...";
        next;

        mes "[Lora Tay the Seamstress]";
        mes "\"Splendid.\"";
        mes "After removing the sheets (and pins!), she picks up a pair of scissors and plows through the cloth at her usual amazing pace, only stopping briefly to sew together parts here and there.";
        next;

        mes "[Lora Tay the Seamstress]";
        mes "She then treats the seams with some odorless liquid, making them vanish completely.  Finally she hands you the finished robe.";
        mes "\"Here you are now, this will fit.  And don't you dare tell anyone that I did this!  I'm not about to start a silk cocoon collection!\"";
        next;

        goto L_main_menu;

L_robe_missing_cocoons:
        mes "[Lora Tay the Seamstress]";
        mes "\"" + @ROBE_COCOONS_NR + " cocoons is what I said, not " + countitem("SilkCocoon") + ".";
        mes @ROBE_COCOONS_NR + " is " + (@ROBE_COCOONS_NR / 10) + " as many times as you have fingers, in case that helps.\"";
        next;
        goto L_main_menu;

L_robe_missing_zeny:
        mes "[Lora Tay the Seamstress]";
        mes "\"No, no no.  The way this works is that you give me the GP first and I give you the robe afterwards.  You're " + (@ROBE_ZENY - zeny) + " GP short, so come back when you can afford the robe!\"";
        next;
        goto L_main_menu;

L_where_silk:
        mes "[Lora Tay the Seamstress]";
        mes "\"Oh, Hetchel on the Tulimshar Marketplace can weave them for you.\"";
        next;
        mes "[Lora Tay the Seamstress]";
        mes "She hesitates.";
        mes "\"I didn't see her there the last time I visited, though.  She may be visiting family.  Tough luck.\"";
        next;
        goto L_robe_menu;

L_how_much_silk:
        mes "[Lora Tay the Seamstress]";
        mes "\"Six sheets should do fine, if they're the usual double-elbow squares.\"";
        next;
        goto L_robe_menu;

L_robe_nosilk:
        mes "[Lora Tay the Seamstress]";
        mes "\"What is that?  You don't have any silk?  Well, you're out of luck, I would say.  I shan't make one out of those cotton rags; you might as well be wearing an apple sack.\"";
        next;
        goto L_main_menu;

L_end:
        close;



// Input: @items (array of items you want to choose from)
//        @item_names (names of the items in @items)
//        @items_nr (number of entries in @items)
//        @default_choice$ (default option)
// Return:  @item (The selected item, or 0 if the default/something invalid was chosen)
SUB_pick_one_of_many_items:
        set @c, 0;
        set @i, 0;

        setarray @choice_n$, "", "", "", "", "", "", "", "",
                             "", "", "", "", "", "", "", "",
                             "", "", "", "", "", "", "", "",
                             "", "", "", "", "", "", "", "";

L_SUB_pick_choice_loop:
        if (@i >= @items_nr) goto L_SUB_choice_init_done;
        set @current, @items[@i];
        set @current_name$, @item_names$[@i];
        set @i, @i + 1;

        if (countitem(@current) == 0) goto L_SUB_pick_choice_loop;
        set @choice_v[@c], @current;
        set @choice_n$[@c], @current_name$;
        set @c, @c + 1;
        goto L_SUB_pick_choice_loop;

L_SUB_choice_init_done:
        set @choice_v[@c], 0;
        set @choice_n$[@c], @default_choice$;
        set @c, @c + 1;

        if (@c < 10)
        menu @choice_n$[0], -,
             @choice_n$[1], -,
             @choice_n$[2], -,
             @choice_n$[3], -,
             @choice_n$[4], -,
             @choice_n$[5], -,
             @choice_n$[6], -,
             @choice_n$[7], -,
             @choice_n$[8], -,
             @choice_n$[9], -;
        if (@c < 10) goto L_SUB_choice_join;

        if (@c < 20)
        menu @choice_n$[0], -,
             @choice_n$[1], -,
             @choice_n$[2], -,
             @choice_n$[3], -,
             @choice_n$[4], -,
             @choice_n$[5], -,
             @choice_n$[6], -,
             @choice_n$[7], -,
             @choice_n$[8], -,
             @choice_n$[9], -,
             @choice_n$[10], -,
             @choice_n$[11], -,
             @choice_n$[12], -,
             @choice_n$[13], -,
             @choice_n$[14], -,
             @choice_n$[15], -,
             @choice_n$[16], -,
             @choice_n$[17], -,
             @choice_n$[18], -,
             @choice_n$[19], -;
        if (@c < 20) goto L_SUB_choice_join;

        menu @choice_n$[0], -,
             @choice_n$[1], -,
             @choice_n$[2], -,
             @choice_n$[3], -,
             @choice_n$[4], -,
             @choice_n$[5], -,
             @choice_n$[6], -,
             @choice_n$[7], -,
             @choice_n$[8], -,
             @choice_n$[9], -,
             @choice_n$[10], -,
             @choice_n$[11], -,
             @choice_n$[12], -,
             @choice_n$[13], -,
             @choice_n$[14], -,
             @choice_n$[15], -,
             @choice_n$[16], -,
             @choice_n$[17], -,
             @choice_n$[18], -,
             @choice_n$[19], -,
             @choice_n$[20], -,
             @choice_n$[21], -,
             @choice_n$[22], -,
             @choice_n$[23], -,
             @choice_n$[24], -,
             @choice_n$[25], -,
             @choice_n$[26], -,
             @choice_n$[27], -,
             @choice_n$[28], -,
             @choice_n$[29], -,
             @choice_n$[30], -,
             @choice_n$[31], -;

L_SUB_choice_join:
        set @menu, @menu - 1;
        set @item, @choice_v[@menu];
        if (@menu >= @c) set @item, 0;

        return;

L_TooMany:
        mes "[Lora Tay the Seamstress]";
        mes "\"You don't have anywhere to put this. Come back when you have more room.\"";
	close;
}
