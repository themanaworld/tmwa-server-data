
034-1,79,98,0	script	#Yerrnk_YetiTrigger	32767,1,3,{
    @state = ((QUEST_Barbarians & $@Q_Barbarians_MASK) >> $@Q_Barbarians_SHIFT);
    if (@state != 7)
        end;

    if ($@Yerrnk_Yeti_Active != 0)
        end;
    $@Yerrnk_Yetifighter_ID = getcharid(3);
    $@Yerrnk_Yeti_Active = 1;
    donpcevent "#Yerrnk_Yeti::OnYeti";
    end;
}

034-1,78,98,0	script	#Yerrnk_Yeti	32767,{
end;

OnYeti:
    if (attachrid($@Yerrnk_Yetifighter_ID) == 0)
        goto L_Abort;

    message strcharinfo(0), "There is the Yeti!";
    areamonster "034-1", 67, 93, 74, 102, "", 1072, 1, "#Yerrnk_Yeti::OnYetiDeath";
    end;

L_Abort:
    $@Yerrnk_Yeti_Active = 0;
    $@Yerrnk_Yetifighter_ID = 0;
    end;

OnYetiDeath:
    @mobId = 1072;

    @state = ((QUEST_Barbarians & $@Q_Barbarians_MASK) >> $@Q_Barbarians_SHIFT);
    if (@state != 7)
        goto L_NonQuestKill;
    @state = 8;
    callsub S_Update_Mask;
    BOSS_POINTS = BOSS_POINTS + 40;
    message strcharinfo(0), "You defeated the Yeti and gain 40 Boss Points giving you a total of " + BOSS_POINTS + ".";
    goto L_End;

L_End:
    $@Yerrnk_Yeti_Active = 0;
    $@Yerrnk_Yetifighter_ID = 0;
    end;

L_NonQuestKill:
    message strcharinfo(0), "The dying Yeti takes you along to the other side.";
    percentheal -100, 0;
    goto L_End;

S_Update_Mask:
        set QUEST_Barbarians,
            (QUEST_Barbarians & ~($@Q_Barbarians_MASK))
                | (@state << $@Q_Barbarians_SHIFT);
        return;
}
